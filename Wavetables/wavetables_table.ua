# Uiua 0.13.0-dev.2
# https://uiua.org/pad?src=0_13_0-dev_2__IyBVaXVhIDAuMTMuMC1kZXYuMgojIFt1cmxdCgpTIOKGkCAyMDQ4ICAgICAgICAgICAgICAgICAgICAjIFNhbXBsZXMgcGVyIG9zY2lsbGF0b3IKTyDihpAgMjU2ICAgICAgICAgICAgICAgICAgICAgIyBPc2NpbGxhdG9ycyBwZXIgdGFibGUKVCDihpAgw5dTIE8gICAgICAgICAgICAgICAgICAgICMgU2FtcGxlcyBwZXIgdGFibGUKU1Ug4oaQIMO3MisxICAgICAgICAgICAgICAgICAgICMgU2lnbmVkIHRvIHVuc2lnbmVkClVTIOKGkCAtMcOXMiAgICAgICAgICAgICAgICAgICAjIFVuc2lnbmVkIHRvIHNpZ25lZApPVSDihpAgw7fin5zih6FTICAgICAgICAgICAgICAgICAgICMgT3NjaWxsYXRvciBiYXNlLCB1bnNpZ25lZCAwLTEKT1Qg4oaQIMOXz4RPVSAgICAgICAgICAgICAgICAgICAjIE9zY2lsbGF0b3IgYmFzZSwgdHJpZ29ub21ldHJpYyAwLTLPgApSIOKGkCDihq_CpFQgICAgICAgICAgICAgICAgICAgICAjIE9zY2lsbGF0b3IgdG8gdGFibGUgKHJlcGVhdCkKVSDihpAg4o2lKOKKouKNieKGr-KKnzrin5zDtzLiirjip7spICAgICAgICAgICAjIE9jdGF2ZSB1cCAoZm9yIHBsYXliYWNrIG9ubHkpCkdIIOKGkCAyMDAgICAgICAgICAgICAgICAgICAgICMgR3JhcGggaGVpZ2h0CkMg4oaQIOKGpcKvMeKGpzEgICAgICAgICAgICAgICAgICAgIyBDbGFtcApTVyDihpAg4oqP4pe_4qe7LCAgICAgICAgICAgICAgICAgICAjIFNlbGVjdCAod3JhcHBlZCkKU0kg4oaQICviipnDl-KfnC3iioPiioMoU1fijIopKFNX4oyIKSgt4oq44oyKKSAjIFNlbGVjdCAoaW50ZXJwb2xhdGVkKQpSSSDihpAgU0nDl-KHoTrDtyzip7ssICAgICAgICAgICAgICAjIFJlc2l6ZSAoaW50ZXJwb2xhdGVkKQpDUyDihpAg4oi_K863ICAgICAgICAgICAgICAgICAgICAjIENvc2luZQpOUiDihpAg4oan4oq44oalICAgICAgICAgICAgICAgICAgICAjIE5hTiByZXBsYWNlClNDIOKGkCBOUjHDt-KfnOKIvyAgICAgICAgICAgICAgICAgIyBTaW5jClZCIOKGkCDDty0x4p-c4oehTyAgICAgICAgICAgICAgICAgIyBWYWx1ZSBiYXNlClZSIOKGkCAr4oqZw5c6LeKfnDogICAgICAgICAgICAgICAgIyBWYWx1ZSByYW5nZSBbbWluLCBtYXgsIHZhbHVlc10KUuKCgCDihpAg4piHMCAgICAgICAgICAgICAgICAgICAgICMgUmVyYW5rIHplcm8KUuKCgSDihpAg4piHMSAgICAgICAgICAgICAgICAgICAgICMgUmVyYW5rIG9uZQpWISDihpAgUuKCgeKJoV4hUuKCgApWViEg4oaQIFLigoHiiaFeIVLigoDiiplS4oKACkEhIOKGkCBS4oKB4omhXiFS4oKBClZBISDihpAgUuKCgeKJoV4hUuKCgOKKmVLigoEKCiMgVGFibGUgdmFsdWVzClZhbEwg4oaQIFZS4oqZ4oqZKFZCKSAgICAjIFZhbHVlcyAobGluZWFyKSBbbWluLCBtYXhdClZhbFBJIOKGkCBWUuKKmeKKmSjigb86VkIpICMgVmFsdWVzIChwb3dlciBpbikgW21pbiwgbWF4LCBiYXNlXQpWYWxQTyDihpAg4oeMVmFsUEk6ICAgICMgVmFsdWVzIChwb3dlciBvdXQpIFttaW4sIG1heCwgYmFzZV0KCiMgT3BlcmF0aW9ucwpBbXAg4oaQIFZBIcOXICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBBbXBsaWZ5IFtBbW91bnQsIFdhdmVdCk1heCDihpAgQSEo4peM4o2l4p-cw7fiiaAwLuKGpeKIqS_ihqXin5zCry4pICAgICAgICAgICAgICAgICAgICMgTWF4aW1pemUgW1dhdmVdClBocyDihpAgVkEhKOKGu-KBhcOX4qe7LCkgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFBoYXNlIHNoaWZ0IFtBbW91bnQsIFdhdmVdCkJpdCDihpAgVkEhKFVT4oanMcO3LTE64oyKw5csU1U64oGF4oG_OjIrMcOXMTUtOjHiiJrihqcx4oalMCkgIyBCaXRjcnVzaCBbQW1vdW50ICgwLTEpLCBXYXZlXQoKIyBCYXNpYyB3YXZlZm9ybXMKU2luIOKGkCBWISjiiL_DlzpPVCkgICAgICAjIFNpbmUgd2F2ZSBbSGFybW9uaWNdClNhdyDihpAgViEoVVPil78xw5c6T1UpICAgIyBTYXcgd2F2ZSBbSGFybW9uaWNdClB1bCDihpAgVlYhKFVT4omkVVM6U2F3KSAjIFB1bHNlIHdhdmUgW0hhcm1vbmljLCBEdXR5XQpTcXIg4oaQIFYhKFB1bDowLjUpICAgICMgU3F1YXJlIHdhdmUgW0hhcm1vbmljXQpOc3cg4oaQICjimIcxVVPijaUo4oqC4pqCKVNbXSkgIyBOb2lzZSB3YXZlIFtdCk5zdCDihpAgKOKNpSjiioJOc3cpT1tdKSAgICMgTm9pc2UgdGFibGUgW10KCiMgVXRpbGl0eQpQcmludCDihpAgJnBmCkV4cG9ydCDihpAgJmFwIEPima0KVGVzdCDihpAgJmFwIFUzUiBD4pmtCkltYWdlIOKGkCAmaW1z4oavT19TIFNVIFIgQ-KZrQoKIyBTYW1wbGUgd2F2ZWZvcm1zL3dhdmV0YWJsZXMKVGJs4oKBIOKGkCBCaXQoVmFsUE8gMCAxIDEuNSkgTnN3CgpUZXN0IFRibOKCgQpJbWFnZSBUYmzigoEK

S ← 2048                    # Samples per oscillator
O ← 256                     # Oscillators per table
T ← ×S O                    # Samples per table
SU ← ÷2+1                   # Signed to unsigned
US ← -1×2                   # Unsigned to signed
OU ← ÷⟜⇡S                   # Oscillator base, unsigned 0-1
OT ← ×τOU                   # Oscillator base, trigonometric 0-2π
R ← ↯¤T                     # Oscillator to table (repeat)
U ← ⍥(⊢⍉↯⊟:⟜÷2⊸⧻)           # Octave up (for playback only)
GH ← 200                    # Graph height
C ← ↥¯1↧1                   # Clamp
SW ← ⊏◿⧻,                   # Select (wrapped)
SI ← +⊙×⟜-⊃⊃(SW⌊)(SW⌈)(-⊸⌊) # Select (interpolated)
RI ← SI×⇡:÷,⧻,              # Resize (interpolated)
CS ← ∿+η                    # Cosine
NR ← ↧⊸↥                    # NaN replace
SC ← NR1÷⟜∿                 # Sinc
VB ← ÷-1⟜⇡O                 # Value base
VR ← +⊙×:-⟜:                # Value range [min, max, values]
R₀ ← ☇0                     # Rerank zero
R₁ ← ☇1                     # Rerank one
V! ← R₁≡^!R₀
VV! ← R₁≡^!R₀⊙R₀
A! ← R₁≡^!R₁
VA! ← R₁≡^!R₀⊙R₁

# Table values
ValL ← VR⊙⊙(VB)    # Values (linear) [min, max]
ValPI ← VR⊙⊙(ⁿ:VB) # Values (power in) [min, max, base]
ValPO ← ⇌ValPI:    # Values (power out) [min, max, base]

# Operations
Amp ← VA!×                                 # Amplify [Amount, Wave]
Max ← A!(◌⍥⟜÷≠0.↥∩/↥⟜¯.)                   # Maximize [Wave]
Phs ← VA!(↻⁅×⧻,)                           # Phase shift [Amount, Wave]
Bit ← VA!(US↧1÷-1:⌊×,SU:⁅ⁿ:2+1×15-:1√↧1↥0) # Bitcrush [Amount (0-1), Wave]

# Basic waveforms
Sin ← V!(∿×:OT)      # Sine wave [Harmonic]
Saw ← V!(US◿1×:OU)   # Saw wave [Harmonic]
Pul ← VV!(US≤US:Saw) # Pulse wave [Harmonic, Duty]
Sqr ← V!(Pul:0.5)    # Square wave [Harmonic]
Nsw ← (☇1US⍥(⊂⚂)S[]) # Noise wave []
Nst ← (⍥(⊂Nsw)O[])   # Noise table []

# Utility
Print ← &pf
Export ← &ap C♭
Test ← &ap U3R C♭
Image ← &ims↯O_S SU R C♭

# Sample waveforms/wavetables
Tbl₁ ← Bit(ValPO 0 1 1.5) Nsw

Test Tbl₁
Image Tbl₁
