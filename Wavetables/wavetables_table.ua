# Uiua 0.13.0-dev.2
# https://uiua.org/pad?src=0_13_0-dev_2__IyBVaXVhIDAuMTMuMC1kZXYuMgojIFt1cmxdCgpTIOKGkCAyMDQ4ICAgICAgICAgICAgICAgICAgICAjIFNhbXBsZXMgcGVyIG9zY2lsbGF0b3IKTyDihpAgMjU2ICAgICAgICAgICAgICAgICAgICAgIyBPc2NpbGxhdG9ycyBwZXIgdGFibGUKVCDihpAgw5dTIE8gICAgICAgICAgICAgICAgICAgICMgU2FtcGxlcyBwZXIgdGFibGUKU1Ug4oaQIMO3MisxICAgICAgICAgICAgICAgICAgICMgU2lnbmVkIHRvIHVuc2lnbmVkClVTIOKGkCAtMcOXMiAgICAgICAgICAgICAgICAgICAjIFVuc2lnbmVkIHRvIHNpZ25lZApPVSDihpAgw7fin5zih6FTICAgICAgICAgICAgICAgICAgICMgT3NjaWxsYXRvciBiYXNlLCB1bnNpZ25lZCAwLTEKT1Qg4oaQIMOXz4RPVSAgICAgICAgICAgICAgICAgICAjIE9zY2lsbGF0b3IgYmFzZSwgdHJpZ29ub21ldHJpYyAwLTLPgApSIOKGkCDihq_CpFQgICAgICAgICAgICAgICAgICAgICAjIE9zY2lsbGF0b3IgdG8gdGFibGUgKHJlcGVhdCkKVSDihpAg4o2lKOKKouKNieKGr-KKnzrin5zDtzLiirjip7spICAgICAgICAgICAjIE9jdGF2ZSB1cCAoZm9yIHBsYXliYWNrIG9ubHkpCkdIIOKGkCAyMDAgICAgICAgICAgICAgICAgICAgICMgR3JhcGggaGVpZ2h0CkMg4oaQIOKGpcKvMeKGpzEgICAgICAgICAgICAgICAgICAgIyBDbGFtcApTVyDihpAg4oqP4pe_4qe7LCAgICAgICAgICAgICAgICAgICAjIFNlbGVjdCAod3JhcHBlZCkKU0kg4oaQICviipnDl-KfnC3iioPiioMoU1fijIopKFNX4oyIKSgt4oq44oyKKSAjIFNlbGVjdCAoaW50ZXJwb2xhdGVkKQpSSSDihpAgU0nDl-KHoTrDtyzip7ssICAgICAgICAgICAgICAjIFJlc2l6ZSAoaW50ZXJwb2xhdGVkKQpDUyDihpAg4oi_K863ICAgICAgICAgICAgICAgICAgICAjIENvc2luZQpOUiDihpAg4oan4oq44oalICAgICAgICAgICAgICAgICAgICAjIE5hTiByZXBsYWNlClNDIOKGkCBOUjHDt-KfnOKIvyAgICAgICAgICAgICAgICAgIyBTaW5jClZCIOKGkCDDty0x4p-c4oehTyAgICAgICAgICAgICAgICAgIyBWYWx1ZSBiYXNlClZSIOKGkCAr4oqZw5c6LeKfnDogICAgICAgICAgICAgICAgIyBWYWx1ZSByYW5nZSBbbWluLCBtYXgsIHZhbHVlc10KR0wg4oaQIDE2ICAgICAgICAgICAgICAgICAgICAgIyBHSUYgbGVuZ3RoIChmcmFtZXMpClLigoAg4oaQIOKYhzAgICAgICAgICAgICAgICAgICAgICAjIFJlcmFuayB6ZXJvClLigoEg4oaQIOKYhzEgICAgICAgICAgICAgICAgICAgICAjIFJlcmFuayBvbmUKViEg4oaQIFLigoHiiaFeIVLigoAKVlYhIOKGkCBS4oKB4omhXiFS4oKA4oqZUuKCgApBISDihpAgUuKCgeKJoV4hUuKCgQpWQSEg4oaQIFLigoHiiaFeIVLigoDiiplS4oKBCgojIFRhYmxlIHZhbHVlcwpWYWxMIOKGkCBWUuKKmeKKmShWQikgICAgIyBWYWx1ZXMgKGxpbmVhcikgW21pbiwgbWF4XQpWYWxQSSDihpAgVlLiipniipko4oG_OlZCKSAjIFZhbHVlcyAocG93ZXIgaW4pIFttaW4sIG1heCwgYmFzZV0KVmFsUE8g4oaQIOKHjFZhbFBJOiAgICAjIFZhbHVlcyAocG93ZXIgb3V0KSBbbWluLCBtYXgsIGJhc2VdCgojIE9wZXJhdGlvbnMKQW1wIOKGkCBWQSHDlyAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgQW1wbGlmeSBbQW1vdW50LCBXYXZlXQpNYXgg4oaQIEEhKOKXjOKNpeKfnMO34omgMC7ihqXiiKkv4oal4p-cwq8uKSAgICAgICAgICAgICAgICAgICAjIE1heGltaXplIFtXYXZlXQpQaHMg4oaQIFZBISjihrvigYXDl-KnuywpICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBQaGFzZSBzaGlmdCBbQW1vdW50LCBXYXZlXQpCaXQg4oaQIFZBIShVU-KGpzHDty0xOuKMisOXLFNVOuKBheKBvzoyKzHDlzE1LTox4oia4oanMeKGpTApICMgQml0Y3J1c2ggW0Ftb3VudCAoMC0xKSwgV2F2ZV0KCiMgQmFzaWMgd2F2ZWZvcm1zClNpbiDihpAgViEo4oi_w5c6T1QpICAgICAgIyBTaW5lIHdhdmUgW0hhcm1vbmljXQpTYXcg4oaQIFYhKFVT4pe_McOXOk9VKSAgICMgU2F3IHdhdmUgW0hhcm1vbmljXQpQdWwg4oaQIFZWIShVU-KJpFVTOlNhdykgIyBQdWxzZSB3YXZlIFtIYXJtb25pYywgRHV0eV0KU3FyIOKGkCBWIShQdWw6MC41KSAgICAjIFNxdWFyZSB3YXZlIFtIYXJtb25pY10KTnN3IOKGkCAo4piHMVVT4o2lKOKKguKagilTW10pICMgTm9pc2Ugd2F2ZSBbXQpOc3Qg4oaQICjijaUo4oqCTnN3KU9bXSkgICAjIE5vaXNlIHRhYmxlIFtdCgojIFV0aWxpdHkKUHJpbnQg4oaQICZwZgpFeHBvcnQg4oaQICZhcCBD4pmtClRlc3Qg4oaQICZhcCBVM1IgQ-KZrQpJbWFnZSDihpAgJmltc-KGr09fUyBTVSBSIEPima0KR3JhcGgg4oaQIOKNouKKoig9MeKnuynilr09MOKHoeKMisO3R0wgT-KJoSjih4zirJox4oqe4omg4oehR0jigYXDly0xR0ggU1UgVTJDKQoKIyBTYW1wbGUgd2F2ZWZvcm1zL3dhdmV0YWJsZXMKVGJs4oKBIOKGkCBTaW4oVmFsTCAxIDIpCgpUZXN0IFRibOKCgQpJbWFnZSBUYmzigoEKR3JhcGggVGJs4oKBCg==

S ← 2048                    # Samples per oscillator
O ← 256                     # Oscillators per table
T ← ×S O                    # Samples per table
SU ← ÷2+1                   # Signed to unsigned
US ← -1×2                   # Unsigned to signed
OU ← ÷⟜⇡S                   # Oscillator base, unsigned 0-1
OT ← ×τOU                   # Oscillator base, trigonometric 0-2π
R ← ↯¤T                     # Oscillator to table (repeat)
U ← ⍥(⊢⍉↯⊟:⟜÷2⊸⧻)           # Octave up (for playback only)
GH ← 200                    # Graph height
C ← ↥¯1↧1                   # Clamp
SW ← ⊏◿⧻,                   # Select (wrapped)
SI ← +⊙×⟜-⊃⊃(SW⌊)(SW⌈)(-⊸⌊) # Select (interpolated)
RI ← SI×⇡:÷,⧻,              # Resize (interpolated)
CS ← ∿+η                    # Cosine
NR ← ↧⊸↥                    # NaN replace
SC ← NR1÷⟜∿                 # Sinc
VB ← ÷-1⟜⇡O                 # Value base
VR ← +⊙×:-⟜:                # Value range [min, max, values]
GL ← 16                     # GIF length (frames)
R₀ ← ☇0                     # Rerank zero
R₁ ← ☇1                     # Rerank one
V! ← R₁≡^!R₀
VV! ← R₁≡^!R₀⊙R₀
A! ← R₁≡^!R₁
VA! ← R₁≡^!R₀⊙R₁

# Table values
ValL ← VR⊙⊙(VB)    # Values (linear) [min, max]
ValPI ← VR⊙⊙(ⁿ:VB) # Values (power in) [min, max, base]
ValPO ← ⇌ValPI:    # Values (power out) [min, max, base]

# Operations
Amp ← VA!×                                 # Amplify [Amount, Wave]
Max ← A!(◌⍥⟜÷≠0.↥∩/↥⟜¯.)                   # Maximize [Wave]
Phs ← VA!(↻⁅×⧻,)                           # Phase shift [Amount, Wave]
Bit ← VA!(US↧1÷-1:⌊×,SU:⁅ⁿ:2+1×15-:1√↧1↥0) # Bitcrush [Amount (0-1), Wave]

# Basic waveforms
Sin ← V!(∿×:OT)      # Sine wave [Harmonic]
Saw ← V!(US◿1×:OU)   # Saw wave [Harmonic]
Pul ← VV!(US≤US:Saw) # Pulse wave [Harmonic, Duty]
Sqr ← V!(Pul:0.5)    # Square wave [Harmonic]
Nsw ← (☇1US⍥(⊂⚂)S[]) # Noise wave []
Nst ← (⍥(⊂Nsw)O[])   # Noise table []

# Utility
Print ← &pf
Export ← &ap C♭
Test ← &ap U3R C♭
Image ← &ims↯O_S SU R C♭
Graph ← ⍢⊢(=1⧻)▽=0⇡⌊÷GL O≡(⇌⬚1⊞≠⇡GH⁅×-1GH SU U2C)

# Sample waveforms/wavetables
Tbl₁ ← Sin(ValL 1 2)

Test Tbl₁
Image Tbl₁
Graph Tbl₁