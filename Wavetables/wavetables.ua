# Uiua 0.13.0-dev.2
# https://uiua.org/pad?src=0_13_0-dev_2__IyBVaXVhIDAuMTMuMC1kZXYuMgojIFt1cmxdCgpTIOKGkCAyMDQ4ICAgICAgICAgICAgICAgICAgICAjIFNhbXBsZXMgcGVyIG9zY2lsbGF0b3IKTyDihpAgMjU2ICAgICAgICAgICAgICAgICAgICAgIyBPc2NpbGxhdG9ycyBwZXIgdGFibGUKVCDihpAgw5dTIE8gICAgICAgICAgICAgICAgICAgICMgU2FtcGxlcyBwZXIgdGFibGUKU1Ug4oaQIMO3MisxICAgICAgICAgICAgICAgICAgICMgU2lnbmVkIHRvIHVuc2lnbmVkClVTIOKGkCAtMcOXMiAgICAgICAgICAgICAgICAgICAjIFVuc2lnbmVkIHRvIHNpZ25lZApPVSDihpAgw7fin5zih6FTICAgICAgICAgICAgICAgICAgICMgT3NjaWxsYXRvciBiYXNlLCB1bnNpZ25lZCAwLTEKT1Qg4oaQIMOXz4RPVSAgICAgICAgICAgICAgICAgICAjIE9zY2lsbGF0b3IgYmFzZSwgdHJpZ29ub21ldHJpYyAwLTLPgApSIOKGkCDihq_CpFQgICAgICAgICAgICAgICAgICAgICAjIE9zY2lsbGF0b3IgdG8gdGFibGUgKHJlcGVhdCkKVSDihpAg4o2lKOKKouKNieKGr-KKnzrin5zDtzLiirjip7spICAgICAgICAgICAjIE9jdGF2ZSB1cCAoZm9yIHBsYXliYWNrIG9ubHkpCkdIIOKGkCAyMDAgICAgICAgICAgICAgICAgICAgICMgR3JhcGggaGVpZ2h0CkMg4oaQIOKGpcKvMeKGpzEgICAgICAgICAgICAgICAgICAgIyBDbGFtcApTVyDihpAg4oqP4pe_4qe7LCAgICAgICAgICAgICAgICAgICAjIFNlbGVjdCAod3JhcHBlZCkKU0kg4oaQICviipnDl-KfnC3iioPiioMoU1fijIopKFNX4oyIKSgt4oq44oyKKSAjIFNlbGVjdCAoaW50ZXJwb2xhdGVkKQpSSSDihpAgU0nDl-KHoTrDtyzip7ssICAgICAgICAgICAgICAjIFJlc2l6ZSAoaW50ZXJwb2xhdGVkKQpDUyDihpAg4oi_K863ICAgICAgICAgICAgICAgICAgICAjIENvc2luZQpOUiDihpAg4oan4oq44oalICAgICAgICAgICAgICAgICAgICAjIE5hTiByZXBsYWNlClNDIOKGkCBOUjHDt-KfnOKIvyAgICAgICAgICAgICAgICAgIyBTaW5jClZCIOKGkCDDty0x4p-c4oehTyAgICAgICAgICAgICAgICAgIyBWYWx1ZSBiYXNlClZSIOKGkCAr4oqZw5c6LeKfnDogICAgICAgICAgICAgICAgIyBWYWx1ZSByYW5nZSBbbWluLCBtYXgsIHZhbHVlc10KCiMgVGFibGUgdmFsdWVzClZhbEwg4oaQIFZS4oqZ4oqZKFZCKSAgICAjIFZhbHVlcyAobGluZWFyKSBbbWluLCBtYXhdClZhbFFJIOKGkCBWUuKKmeKKmSjigb8yVkIpICMgVmFsdWVzIChxdWFkcmF0aWMgaW4pIFttaW4sIG1heF0KVmFsQ0kg4oaQIFZS4oqZ4oqZKOKBvzNWQikgIyBWYWx1ZXMgKGN1YmljIGluKSBbbWluLCBtYXhdClZhbFFPIOKGkCDih4xWYWxRSTogICAgIyBWYWx1ZXMgKHF1YWRyYXRpYyBvdXQpIFttaW4sIG1heF0KVmFsQ08g4oaQIOKHjFZhbENJOiAgICAjIFZhbHVlcyAoY3ViaWMgb3V0KSBbbWluLCBtYXhdCgojIENvbnZvbHV0aW9ucwpDSWQg4oaQIMKkMSAgICAgICAgICAgICAjIElkZW50aXR5IChkZWx0YSkgY29udm9sdXRpb24KQ0xQUyDihpAgw7fPgFNDw5figb86Mjotz4BPVCAjIExvdy1wYXNzIChzaW5jKSBjb252b2x1dGlvbgoKIyBIZWxwZXJzClJuZEludCDihpAgKzHijIrDl-KagiAjIFJhbmRvbSBpbnRlZ2VyIDEtTiBbTWF4XQoKIyBPcGVyYXRpb25zCk1heCDihpAg4peM4o2l4p-cw7fiiaAwLuKGpeKIqS_ihqXin5zCry4gICAgICAgICAgICAgICAgICAjICpNYXhpbWl6ZSBbV2F2ZV0KUGhzIOKGkCDihrvigYXDl-KnuywgICAgICAgICAgICAgICAgICAgICAgICAgICAjICpQaGFzZSBzaGlmdCBbQW1vdW50LCBXYXZlXQpaZXIg4oaQIOKGu-KKly_ihqfijLUuLiAgICAgICAgICAgICAgICAgICAgICAgICAjICpQaGFzZSBzaGlmdCB0byB6ZXJvIHBvaW50IFtXYXZlXQpBdmcg4oaQIMO3Oi8r4p-c4qe7ICAgICAgICAgICAgICAgICAgICAgICAgICAjICpBdmVyYWdlIFtXYXZlQXJyYXldCkJpdCDihpAgVVPihqcxw7ctMTrijIrDlyxTVTrigYXigb86Misxw5cxNS06MeKImuKGpzHihqUwICMgKkJpdGNydXNoIFtBbW91bnQgKDAtMSksIFdhdmVdCkFNIOKGkCDDlysxw7cyw5ctMTogICAgICAgICAgICAgICAgICAgICAgICAjICpBbXBsdXR1ZGUgbW9kdWxhdGlvbiBbQW1wbGl0dWRlLCBNb2R1bGF0b3IsIENhcnJpZXJdCkZNIOKGkCBTScOXUytPVcOXICAgICAgICAgICAgICAgICAgICAgICAgICMgKkZyZXF1ZW5jeSBtb2R1bGF0aW9uIFtBbXBsaXR1ZGUsIE1vZHVsYXRvciwgQ2Fycmllcl0KQ252IOKGkCAvK8OX4o2J4per4oqZKOKGr-KZrSk6LTHiirgr4qe7LOKnu-KfnDogICAgICAgICAgICAjICpDb252b2x2ZSBbQ29udm9sdXRpb24sIFdhdmVdClJldiDihpAg4oeMICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgKlJldmVyc2UgW1dhdmVdCkludiDihpAgwq8gICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyAqSW52ZXJ0IFtXYXZlXQpNaXIg4oaQIOKKgi7ihpnDtzJTICAgICAgICAgICAgICAgICAgICAgICAgICAjICpNaXJyb3IgMXN0IGhhbGYgW1dhdmVdCkZhZCDihpAg4oeM4qyaMcOX4oeMOuKsmjHin5zDl8O34qe7LuKHoeKMisO3MsOXU-KGpzHihqUwICAgICAgICAgIyAqRmFkZSBpbiAmIG91dCBbQW1vdW50ICgwLTEpLCBXYXZlXQpMUCDihpAgUGhzMC41IE1heCBDbnYgQ0xQUyAgICAgICAgICAgICAgIyAqTkVFRFMgV09SSyBMb3cgcGFzcyBbVmFsdWUsIFdhdmVdCkFtcCDihpAg4oqew5cgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjICpBbXBsaWZ5IFtBbW91bnQsIFdhdmVdCgojIEJhc2ljIHdhdmVzClNpbiDihpAg4oi_w5c6T1QgICAgICMgU2luZSB3YXZlIFtIYXJtb25pY10KU2F3IOKGkCBVU-KXvzHDlzpPVSAgIyBTYXcgd2F2ZSBbSGFybW9uaWNdClB1bCDihpAgVVPiiaRVUzpTYXcgIyBQdWxzZSB3YXZlIFtIYXJtb25pYywgRHV0eV0KU3FyIOKGkCBQdWw6MC41ICAgIyBTcXVhcmUgd2F2ZSBbSGFybW9uaWNdCgojIFV0aWxpdHkKUHJpbnQg4oaQICZwZgpFeHBvcnQg4oaQICZhcCBD4pmtClRlc3Qg4oaQICZhcCBVM1IgTWF4IEPima0KR3JhcGgg4oaQICZpbXPih4zirJox4oqe4omg4oehR0jigYXDly0xR0ggU1UgVTJDICMgKgpJbWFnZSDihpAgJmltc-KGr09fUyBTVSBSIEPima0KSW1wb3J0V2F2ZSDihpAgUkkgU-KXjOKXjMKwYXVkaW8mZnJhYgpJbXBvcnRJbWFnZSDihpAgUkkgT-KJoVJJIFPijZzijYniiaEow7czLyvihpkzKSDil4zCsGltZyAmZnJhYgpJbWFnZVRvVGFibGUg4oaQIOKZrVVTICAgICAgICAgICMgKgpUYWJsZUF2ZXJhZ2Ug4oaQIMO3Ty8r4oavT19TICAgICAjIFRhYmxlIHRvIHdhdmUgW1RhYmxlXQpUYWJsZUluZGV4IOKGkCDiiqHihqctMU_ihqUw4oGFOuKGr09fUzogIyBUYWJsZSB0byB3YXZlIFtJbmRleCAoMC0yNTUpLCBUYWJsZV0KR3JhcGhDb252IOKGkCBHcmFwaOKsmjDDl-KGr1MxCgojIFNhbXBsZSB3YXZlZm9ybXMKV2F2ZeKCgSDihpAgQXZnW1NpbjMgw5cwLjdTaW4yIFNpbjFdCldhdmXigoIg4oaQIEFNMC40U2luNyBBTTAuNVNpbjIgU2luMQpXYXZl4oKDIOKGkCBBdmdbU2F3MSBQdWwy4oiYMC4yXQpXYXZl4oKEIOKGkCBMUDYgQml0MC43IEZNMC4xU2luNyBTaW4xCgpQcmludCAiV2F2ZeKChCIKRXhwb3J0IFdhdmXigoQKVGVzdCBXYXZl4oKECkdyYXBoIFdhdmXigoQKSW1hZ2UgV2F2ZeKChAo=

S ← 2048                    # Samples per oscillator
O ← 256                     # Oscillators per table
T ← ×S O                    # Samples per table
SU ← ÷2+1                   # Signed to unsigned
US ← -1×2                   # Unsigned to signed
OU ← ÷⟜⇡S                   # Oscillator base, unsigned 0-1
OT ← ×τOU                   # Oscillator base, trigonometric 0-2π
R ← ↯¤T                     # Oscillator to table (repeat)
U ← ⍥(⊢⍉↯⊟:⟜÷2⊸⧻)           # Octave up (for playback only)
GH ← 200                    # Graph height
C ← ↥¯1↧1                   # Clamp
SW ← ⊏◿⧻,                   # Select (wrapped)
SI ← +⊙×⟜-⊃⊃(SW⌊)(SW⌈)(-⊸⌊) # Select (interpolated)
RI ← SI×⇡:÷,⧻,              # Resize (interpolated)
CS ← ∿+η                    # Cosine
NR ← ↧⊸↥                    # NaN replace
SC ← NR1÷⟜∿                 # Sinc
VB ← ÷-1⟜⇡O                 # Value base
VR ← +⊙×:-⟜:                # Value range [min, max, values]

# Table values
ValL ← VR⊙⊙(VB)    # Values (linear) [min, max]
ValQI ← VR⊙⊙(ⁿ2VB) # Values (quadratic in) [min, max]
ValCI ← VR⊙⊙(ⁿ3VB) # Values (cubic in) [min, max]
ValQO ← ⇌ValQI:    # Values (quadratic out) [min, max]
ValCO ← ⇌ValCI:    # Values (cubic out) [min, max]

# Convolutions
CId ← ¤1             # Identity (delta) convolution
CLPS ← ÷πSC×ⁿ:2:-πOT # Low-pass (sinc) convolution

# Helpers
RndInt ← +1⌊×⚂ # Random integer 1-N [Max]

# Operations
Max ← ◌⍥⟜÷≠0.↥∩/↥⟜¯.                  # *Maximize [Wave]
Phs ← ↻⁅×⧻,                           # *Phase shift [Amount, Wave]
Zer ← ↻⊗/↧⌵..                         # *Phase shift to zero point [Wave]
Avg ← ÷:/+⟜⧻                          # *Average [WaveArray]
Bit ← US↧1÷-1:⌊×,SU:⁅ⁿ:2+1×15-:1√↧1↥0 # *Bitcrush [Amount (0-1), Wave]
AM ← ×+1÷2×-1:                        # *Amplutude modulation [Amplitude, Modulator, Carrier]
FM ← SI×S+OU×                         # *Frequency modulation [Amplitude, Modulator, Carrier]
Cnv ← /+×⍉◫⊙(↯♭):-1⊸+⧻,⧻⟜:            # *Convolve [Convolution, Wave]
Rev ← ⇌                               # *Reverse [Wave]
Inv ← ¯                               # *Invert [Wave]
Mir ← ⊂.↙÷2S                          # *Mirror 1st half [Wave]
Fad ← ⇌⬚1×⇌:⬚1⟜×÷⧻.⇡⌊÷2×S↧1↥0         # *Fade in & out [Amount (0-1), Wave]
LP ← Phs0.5 Max Cnv CLPS              # *NEEDS WORK Low pass [Value, Wave]
Amp ← ⊞×                              # *Amplify [Amount, Wave]

# Basic waves
Sin ← ∿×:OT     # Sine wave [Harmonic]
Saw ← US◿1×:OU  # Saw wave [Harmonic]
Pul ← US≤US:Saw # Pulse wave [Harmonic, Duty]
Sqr ← Pul:0.5   # Square wave [Harmonic]

# Utility
Print ← &pf
Export ← &ap C♭
Test ← &ap U3R Max C♭
Graph ← &ims⇌⬚1⊞≠⇡GH⁅×-1GH SU U2C # *
Image ← &ims↯O_S SU R C♭
ImportWave ← RI S◌◌°audio&frab
ImportImage ← RI O≡RI S⍜⍉≡(÷3/+↙3) ◌°img &frab
ImageToTable ← ♭US          # *
TableAverage ← ÷O/+↯O_S     # Table to wave [Table]
TableIndex ← ⊡↧-1O↥0⁅:↯O_S: # Table to wave [Index (0-255), Table]
GraphConv ← Graph⬚0×↯S1

# Sample waveforms
Wave₁ ← Avg[Sin3 ×0.7Sin2 Sin1]
Wave₂ ← AM0.4Sin7 AM0.5Sin2 Sin1
Wave₃ ← Avg[Saw1 Pul2∘0.2]
Wave₄ ← LP6 Bit0.7 FM0.1Sin7 Sin1

Print "Wave₄"
Export Wave₄
Test Wave₄
Graph Wave₄
Image Wave₄
