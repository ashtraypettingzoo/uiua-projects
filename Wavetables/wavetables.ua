# Experimental!
# Uiua 0.13.0-dev.2
# https://github.com/ashtraypettingzoo/uiua-projects/blob/main/Wavetables/wavetables.ua
# https://uiua.org/pad?src=0_13_0-dev_2__IyBFeHBlcmltZW50YWwhCiMgVWl1YSAwLjEzLjAtZGV2LjIKIyBodHRwczovL2dpdGh1Yi5jb20vYXNodHJheXBldHRpbmd6b28vdWl1YS1wcm9qZWN0cy9ibG9iL21haW4vV2F2ZXRhYmxlcy93YXZldGFibGVzLnVhCiMgW3VybF0KCiMgVE9ETzogaGlnaC9sb3cgcGFzcyBmaWx0ZXJzCiMgVE9ETzogSVIgaW1wb3J0aW5nCiMgVE9ETzogZml4IFN0ciAoc3RyZXRjaCkgYnV6emluZwojIFRPRE86IFNpZCAoc2lkZSBieSBzaWRlKQojIFRPRE86IFNocCAod2F2ZXNoYXBlKQojIFRPRE86IEVycm9yIGhhbmRsaW5nCiMgVE9ETzogQmF0Y2ggcHJvY2Vzc2luZwoKUyDihpAgMjA0OCAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgU2FtcGxlcyBwZXIgb3NjaWxsYXRvcgpPIOKGkCAyNTYgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBPc2NpbGxhdG9ycyBwZXIgdGFibGUKVCDihpAgw5dTIE8gICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFNhbXBsZXMgcGVyIHRhYmxlClNVIOKGkCDDtzIrMSAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBTaWduZWQgdG8gdW5zaWduZWQKVVMg4oaQIC0xw5cyICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFVuc2lnbmVkIHRvIHNpZ25lZApPVSDihpAgw7fin5zih6FTICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIE9zY2lsbGF0b3IgYmFzZSwgdW5zaWduZWQgMC0xCk9UIOKGkCDDl8-ET1UgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgT3NjaWxsYXRvciBiYXNlLCB0cmlnb25vbWV0cmljIDAtMs-AClIg4oaQIOKGr8KkVCAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIE9zY2lsbGF0b3IgdG8gdGFibGUgKHJlcGVhdCkKVSDihpAg4o2lKOKKouKNieKGr-KKnzrin5zDtzLiirjip7spICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBPY3RhdmUgdXAgKGZvciBwbGF5YmFjayBvbmx5KQpHSCDihpAgMjAwICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBHcmFwaCBoZWlnaHQKQyDihpAg4oalwq8x4oanMSAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBDbGFtcApTVyDihpAg4oqP4pe_4qe7LCAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBTZWxlY3QgKHdyYXBwZWQpClNJIOKGkCAr4oqZw5fin5wt4oqD4oqDKFNX4oyKKShTV-KMiCkoLeKKuOKMiikgICAgICAgICAgICAgICAgICAgICMgU2VsZWN0IChpbnRlcnBvbGF0ZWQpClJJIOKGkCBTScOX4oehOsO3LOKnuywgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFJlc2l6ZSAoaW50ZXJwb2xhdGVkKQpDUyDihpAg4oi_K863ICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBDb3NpbmUKTlIg4oaQIOKGp-KKuOKGpSAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgTmFOIHJlcGxhY2UKU0Mg4oaQIE5SMcO34p-c4oi_ICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBTaW5jClZCIOKGkCDDty0x4p-c4oehTyAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgVmFsdWUgYmFzZQpWUiDihpAgK-KKmcOXOi3in5w6ICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFZhbHVlIHJhbmdlIFttaW4sIG1heCwgdmFsdWVzXQpHTCDihpAgMTYgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBHSUYgbGVuZ3RoIChmcmFtZXMpClNIIOKGkCAyMDAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFNwZWN0cnVtIGdyYXBoIGhlaWdodApGViDihpAgw7cy4oiaUyAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBGb3VyaWVyIG1heCB2YWx1ZQpTViDihpAgKzHijIrDl8O3MlPigb8yw7fin5zih6HDtzRTICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBTcGVjdHJ1bSBpbmRleCB2YWx1ZXMKSVMg4oaQIOKorCgmZ2lmczE1fCZpbXMpPTLip7vilrMuICAgICAgICAgICAgICAgICAgICAgICMgU2hvdyBpbWFnZSBvciBnaWYKU00g4oaQIOKHjOKIpyjiioIr4oq44oqD4ouF4oiYKMOX4oqZ4ouF4oqiKSniipkoOsKkwrDiioIpICAgICAgICAgICAgICAgICAgICMgU2NhbiBhZGQgKyBtdWx0aXBseQpNQiDihpAgw7c6MeKBvzE2IDIgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBNaW5pbXVtIDE2LWJpdCB2YWx1ZQpGVCDihpAg4oqDKOKIoMKw4oSCKSjDt0ZW4oy1KeKGmSsxw7cyUyBmZnQgICAgICAgICAgICAgICAgICAgICMgRm91cmllciB0cmFuc2Zvcm0gW3BoYXNlLCBhbXAg4oaQIHdhdmVdCkZVIOKGkCDil4zCsOKEgsKwZmZ04oqCOuKNnMKw4oSCwq_ihpgx4oeM4oaYMS7ihILiioMow5fiiL8pKMOXQ1Mp4oqZKMOXRlYpICAgICAgIyBGb3VyaWVyIHVudHJhbnNmb3JtIFt3YXZlIOKGkCBwaGFzZSwgYW1wXQpEUyDihpAgfDEg4o2lKOKKgjoiLyIpPjDip7suICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBBZGQgc2xhc2ggdG8gZGlyZWN0b3J5IG5hbWUKQ0cg4oaQIOKNouKKoig9MeKnuyniiaEo4oeM4qyaMeKKnuKJoOKHoUdI4oGFw5ctMUdIIFNVIFUyQynilr09MOKHoeKMisO3R0wgTyAjIENyZWF0ZSBncmFwaApTVCDihpAg4oqC4oavOkAw4oalMC064oqZKOKnuy4kIl8iKSAgICAgICAgICAgICAgICAgICAgICAgICMgTnVtIHRvIHN0cmluZyB3LyB0cmFpbGluZyB6ZXJvZXMKQ0Ug4oaQIOKJjeKsmkAg4oav4oqZOsKk4qe7LuKKmeKHjOKHjCAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBDaGVjayBleHRlbnNpb24gW2V4dCwgbmFtZV0KVHJ1ZSDihpAgMSAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgVHJ1ZSBjb25zdGFudApGYWxzZSDihpAgMCAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBGYWxzZSBjb25zdGFudApS4oKAIOKGkCDimIcwICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgUmVyYW5rIHplcm8KUuKCgSDihpAg4piHMSAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFJlcmFuayBvbmUKViEg4oaQIFLigoHiiaFeIVLigoAKVlYhIOKGkCBS4oKB4omhXiFS4oKA4oqZUuKCgApWVkEhIOKGkCBS4oKB4omhXiFS4oKA4oqZUuKCgOKKmeKKmVLigoEKVkEhIOKGkCBS4oKB4omhXiFS4oKA4oqZUuKCgQpWQUEhIOKGkCBS4oKB4omhXiFS4oKA4oqZUuKCgeKKmeKKmVLigoEKQSEg4oaQIFLigoHiiaFeIVLigoEKQUEhIOKGkCBS4oKB4omhXiFS4oKB4oqZUuKCgQoKIyAtLS0tIENvbnZvbHV0aW9ucwojIElkZW50aXR5IChkZWx0YSkgY29udm9sdXRpb24KQ0lkIOKGkCDCpMKkMQoKIyAtLS0tIFRhYmxlIHZhbHVlcwojIFZhbHVlcyAobGluZWFyKSBbbWluLCBtYXhdClZhbCDihpAgVlLiipniipkoVkIpCiMgVmFsdWVzIChwb3dlciBpbikgW21pbiwgbWF4LCBiYXNlXQpWYWxQSSDihpAgVlLiipniipko4oG_OlZCKQojIFZhbHVlcyAocG93ZXIgb3V0KSBbbWluLCBtYXgsIGJhc2VdClZhbFBPIOKGkCDih4xWYWxQSToKCiMgLS0tLSBIZWxwZXJzCiMgUmFuZG9tIChtaW4tbWF4KSBbTWluLCBNYXhdClJuZCDihpAgK8OX4pqCOuKfnC0KIyBSYW5kb20gKG1pbi1tYXgpIFtNaW4sIE1heF0KUm5kSSDihpAg4oyKUm5kOisxOuKNpTo8LCwKCiMgLS0tLSBXYXZlIE9wZXJhdGlvbnMKIyBTdW0gW3tXYXZlcy9UYWJsZXN9XQpTdW0g4oaQIMKw4pahLyvijZpS4oKBCiMgQXZlcmFnZSBbe1dhdmVzL1RhYmxlc31dCkF2ZyDihpAgU3Vtw7fiirjip7sKIyBBbXBsaWZ5IFtBbW91bnQsIFdhdmUvVGFibGVdCkFtcCDihpAgVkEhw5cKIyBNYXhpbWl6ZSBbV2F2ZS9UYWJsZV0KTWF4IOKGkCBBISjil4zijaXin5zDt-KJoDAu4oal4oipL-KGpeKfnMKvLikKIyBQaGFzZSBzaGlmdCBbQW1vdW50LCBXYXZlL1RhYmxlXQpQaHMg4oaQIFZBISjihrvigYXDl-KnuywpCiMgUGhhc2Ugc2hpZnQgdG8gemVybyBwb2ludCBbV2F2ZS9UYWJsZV0KWmVyIOKGkCBBISjihrviipcv4oan4oy1Li4pCiMgQml0Y3J1c2ggW0Ftb3VudCAoMC0xKSwgV2F2ZS9UYWJsZV0KQml0IOKGkCBWQSEoVVPihqcxw7ctMTrijIrDlyxTVTrigYXigb86Misxw5cxNS06MeKImuKGpzHihqUwKQojIFJldmVyc2UgW1dhdmUvVGFibGVdClJldiDihpAgQSHih4wKIyBJbnZlcnQgW1dhdmUvVGFibGVdCkludiDihpAgQSHCrwojIE1pcnJvciAxc3QgaGFsZiBbV2F2ZS9UYWJsZV0KTWlyIOKGkCBBISjiioLih4wu4oaZw7cyUykKIyBGYWRlIHN0YXJ0ICYgZW5kIFtBbW91bnQgKDAtMSksIFdhdmUvVGFibGVdCkZhZCDihpAgVkEhKOKHjOKsmjHDl-KHjDrirJox4p-cw5fDt-Knuy7ih6HijIrDtzLDl1PihqUwKQojIENyb3NzZmFkZSBbQW1vdW50ICgwLTEpLCBTdGFydCBXYXZlL1RhYmxlLCBFbmQgV2F2ZS9UYWJsZV0KQ3JzIOKGkCBWQUEhKCviipkow5ctOjEpOuKfnMOX4oqZOikKIyBTdHJldGNoIFtUbyAoMC0xKSwgRnJvbSAoMC0xKSwgV2F2ZS9UYWJsZV0KU3RyIOKGkCBWVkEhKOKKguKKmVJJOi06U-KfnFJJ4oqZ4oqD4oaZ4oaY4oipKOKGpy0xU-KGpTHigYXDl1Pihqcx4oalMCkpCiMgU2hpZnQgdXAvZG93biBbQW1vdW50ICjCrzEtMSksIFdhdmUvVGFibGVdClNoZiDihpAgVkEhKCspCiMgU2xpY2UgW0Ftb3VudCAoMC0xKSwgV2F2ZS9UYWJsZV0KU2xjIOKGkCBWQSEoUkkgU-KGr-KZreKBheKGpTHDl1MpCiMgU3BlZWQgdXAvZG93biBbT2N0YXZlcywgV2F2ZS9UYWJsZV0KU3BkIOKGkCBWQSEoU2xj4oG_OjIpCiMgU3BsaWNlIFtBbW91bnQgKDAtMSksIFdhdmUvVGFibGUgMSwgV2F2ZS9UYWJsZSAyXQpTcGwg4oaQIFZBQSEo4oqC4oqT4oaZ4oaY4p-cOuKBhcOXU-KGpzHihqUwKQojIENsaXAKQ2xwIOKGkCBBIShDKQojIFdyYXAgW1dhdmUvVGFibGVdCldycCDihpAgQSEoVVPil78rTUIxU1UpCiMgQW1wbHV0dWRlIG1vZHVsYXRpb24gW0FtcGxpdHVkZSwgTW9kdWxhdG9yLCBDYXJyaWVyXQpBTSDihpAgVkFBISjDlysxw7cyw5ctMTopCiMgUmluZyBtb2R1bGF0aW9uIFtNb2R1bGF0b3IsIENhcnJpZXJdClJNIOKGkCBBQSHDlwojIEZyZXF1ZW5jeSBtb2R1bGF0aW9uIFtBbXBsaXR1ZGUsIE1vZHVsYXRvciwgQ2Fycmllcl0KRk0g4oaQIFZBQSEoU0nDl1MrT1XDlykKIyBDb252b2x2ZSBbSW1wdWxzZSwgV2F2ZS9UYWJsZV0KQ252IOKGkCBBQSEoLyvDl-KNieKXq-KKmSjihq_ima0pOi0x4oq4K-Knuyzip7vin5w6KQojIFBoYXNlIHJhbmRvbWl6ZSBbQW1vdW50ICgwLTEpLCBXYXZlL1RhYmxlXQpQaHIg4oaQIOKJoShGVTriipkrOkZUKTriiaHDl1LigoEtz4DDl8-E4o2lKOKKguKagikrMcO3MlNbXVLigoDiiplS4oKBCiMgSGFybW9uaWMgd2F2ZSBmaWx0ZXIgW0Ftb3VudCAoMC0xKSwgRmlsdGVyIFdhdmUvVGFibGUsIFNvdXJjZSBXYXZlL1RhYmxlXQpId2Yg4oaQIFZBQSEoRlXiipnDlzorMcOX4oqZKC0x4peM4oipRlQpKQojIERvd25zYW1wbGUgW0Ftb3VudCAoMC0xKSwgV2F2ZS9UYWJsZV0KRHduIOKGkCBWQSEoCiAg4oyIw7c6Uy7igYXihqUx4oG_OjIrMcOXLTHigpkyUy06MQogIOKGr8KkU-KNieKGr-KfnMO34p-cKOKJoS8r4oav4oqCOikKKQoKIyAtLS0tIFRhYmxlIG9wZXJhdGlvbnMKIyBDb21iIGZpbHRlciAoZmVlZGJhY2spIFtBbXBsaXR1ZGUsIEhhcm1vbmljLCBUYWJsZV0KVENtYiDihpAg4oavT19TIFNN4oqZKOKsmjDihq_iioLijIjDtzpULuKBhcO3OlPiiplSKQojIENvbWIgZmlsdGVyIChmZWVkZm9yd2FyZCkgW0FtcGxpdHVkZSwgSGFybW9uaWMsIFRhYmxlXQpUQ21mIOKGkCDihq9PX1Mrw5fiipko4oqC4oavOlswXcKk4p-c4oaY4oyKw7c6wq9T4oqZKC5SKSkKCiMgLS0tLSBXYXZlZm9ybXMKIyBTaW5lIHdhdmUgW0hhcm1vbmljXQpTaW4g4oaQIFYhKOKIv8OXOk9UKQojIFNhdyB3YXZlIFtIYXJtb25pY10KU2F3IOKGkCBWIShVU-KXvzHDlzpPVSkKIyBUcmlhbmdsZSB3YXZlIFtIYXJtb25pY10KVHJpIOKGkCBWIShVU-KMtVVT4pe_MSswLjI1wq_DlzpPVSkKIyBQdWxzZSB3YXZlIFtIYXJtb25pYywgRHV0eV0KUHVsIOKGkCBWViEoVVPiiaRVUzpTYXcpCiMgU3F1YXJlIHdhdmUgW0hhcm1vbmljXQpTcXIg4oaQIFYhKFB1bDowLjUpCiMgTm9pc2Ugd2F2ZSBbXQpOc3cg4oaQICjimIcxVVPijaUo4oqC4pqCKVNbXSkKIyBTaWxlbnQgKGVtcHR5KSB3YXZlClNpbCDihpAgUuKCgeKsmjDihq_CpFNbXQoKIyAtLS0tIFRhYmxlcwojIE5vaXNlIHRhYmxlIFtdCk5zdCDihpAgKOKNpSjiioJOc3cpT1tdKQoKIyAtLS0tIFNldHRpbmdzCkRlc2t0b3Ag4oaQIEZhbHNlCkluRGlyQXVkaW8g4oaQICJpbiIKSW5EaXJJbWFnZSDihpAgImluIgpPdXREaXJBdWRpbyDihpAgIm91dCIKT3V0RGlySW1hZ2Ug4oaQICJvdXQiCk91dERpckdyYXBoIOKGkCAib3V0IgpPdXREaXJTcGVjdHJ1bSDihpAgIm91dCIKT3V0U3VmZml4SW1hZ2Ug4oaQICIiCk91dFN1ZmZpeEdyYXBoIOKGkCAiZyIKT3V0U3VmZml4U3BlY3RydW0g4oaQICJzIgoKIyAtLS0tIFV0aWxpdHkKIyBQcmludCBzdHJpbmcgW1N0cmluZ10KUHJpbnQg4oaQICZwZgojIFBsYXkgd2F2ZSBvciB0YWJsZSBbV2F2ZS9UYWJsZV0KRXhwb3J0IOKGkCAmYXAgQ-KZrQojIFBsYXkgdGVzdCB0b25lIG9mIHdhdmUgb3IgdGFibGUgW1dhdmUvVGFibGVdClRlc3Qg4oaQICZhcCBVM1IgQ-KZrQojIERpc3BsYXkgZ3JhcGggW1dhdmUvVGFibGVdCkdyYXBoIOKGkCBJUyBDRwojIERpc3BsYXkgaW1hZ2UgW1dhdmUvVGFibGVdCkltYWdlIOKGkCAmaW1z4oavT19TIFNVIFIgQ-KZrQojIFJlYWQgaW1hZ2UgZnJvbSBmaWxlIFtGaWxlbmFtZV0KSW1wb3J0SW1hZ2Ug4oaQIFJJIE_iiaFSSSBT4o2c4o2J4omhKMO3My8r4oaZMynil4zCsGltZyZmcmFi4o2lKOKKgkRTIEluRGlySW1hZ2UpRGVza3RvcAojIFJlYWQgd2F2ZSBmcm9tIGZpbGUgW0ZpbGVuYW1lXQpJbXBvcnRXYXZlIOKGkCBS4oKBIFJJIFPil4zil4zCsGF1ZGlvJmZyYWLijaUo4oqCRFMgSW5EaXJBdWRpbylEZXNrdG9wCiMgUmVhZCAyNTYtd2F2ZSB0YWJsZSBmcm9tIGZpbGUgW0ZpbGVuYW1lXQpJbXBvcnRUYWJsZSDihpAg4oavT19TIFJJIFTil4zil4zCsGF1ZGlvJmZyYWLijaUo4oqCRFMgSW5EaXJBdWRpbylEZXNrdG9wCiMgVGFibGUgdG8gd2F2ZSBbVGFibGVdClRhYmxlQXZlcmFnZSDihpAgUuKCgcO3Ty8r4oavT19TCiMgVGFibGUgdG8gd2F2ZSBbSW5kZXggKDAtMjU1KSwgVGFibGVdClRhYmxlSW5kZXgg4oaQIFLigoHiiqHihqctMU_ihqUw4oGFOuKGr09fUzoKIyBDb252ZXJ0IGltYWdlIHRvIHRhYmxlIFtJbWFnZV0KSW1hZ2VUb1RhYmxlIOKGkCBVUwojIERpc3BsYXkgZ3JhcGggb2YgaW1wdWxzZSBbV2F2ZS9UYWJsZV0KR3JhcGhDb252IOKGkCBHcmFwaCBBISjirJoww5fihq9TMSkKIyBEaXNwbGF5IHNwZWN0cnVtIGdyYXBoIFtXYXZlL1RhYmxlXQpHcmFwaFNwZWN0cnVtIOKGkCBJU-KNouKKoig9MeKnuyniiaEo4oeM4oqe4omlw7dTSOKHoVNI4oqPU1bDt0ZW4oaZw7cyU-KMtWZmdCnilr09MOKHoeKMisO3R0wgTwojIERpc3BsYXkgZnVsbCBzcGVjdHJ1bSBncmFwaCBbV2F2ZS9UYWJsZV0KR3JhcGhTcGVjdHJ1bUZ1bGwg4oaQIElT4o2i4oqiKD0x4qe7KeKJoSjih4ziip7iiaXDt1NI4oehU0jDt0ZW4oy1ZmZ0KeKWvT0w4oeh4oyKw7dHTCBPCiMgU2F2ZSB3YXZlIG9yIHRhYmxlIHRvIGZpbGUgW0ZpbGVuYW1lLCBXYXZlL1RhYmxlXQpXcml0ZUF1ZGlvIOKGkCAmZndh4o2lKOKKgkRTJmZtZC5PdXREaXJBdWRpbylEZXNrdG9w4oqC4oqZKCIud2F2ImF1ZGlvIndhdiImYXNyQ-KZrSkKIyBTYXZlIGltYWdlIHRvIGZpbGUgW0ZpbGVuYW1lLCBXYXZlL1RhYmxlXQpXcml0ZUltYWdlIOKGkCAoCiAg4oqC4oqZKOKKgk91dFN1ZmZpeEltYWdlIi5wbmciaW1nInBuZyLiiLVbLi46MV3ihq9PX1MgU1UgUiBD4pmtKQogICZmd2HijaUo4oqCRFMmZm1kLk91dERpckltYWdlKURlc2t0b3ApCiMgU2F2ZSBncmFwaCB0byBmaWxlIFtGaWxlbmFtZSwgV2F2ZS9UYWJsZV0KV3JpdGVHcmFwaCDihpAgKAogIOKNpSjiioJEUyZmbWQuT3V0RGlyR3JhcGgpRGVza3RvcOKKgjpPdXRTdWZmaXhHcmFwaOKKmSg-M-Knu-KWsy7iiLVbLi46MV1DRykKICAmZndh4qisKOKKgjoiLnBuZyLiipkoaW1nInBuZyIpfOKKgjoiLmdpZiLiipkoZ2lmMTYpKToKKQojIFNhdmUgc3BlY3RydW0gZ3JhcGggdG8gZmlsZSBbRmlsZW5hbWUsIFdhdmUvVGFibGVdCldyaXRlU3BlY3RydW0g4oaQICgKICDiipko4o2i4oqiKD0x4qe7KeKJoSjih4ziip7iiaXDt1NI4oehU0jiio9TVsO3RlbihpnDtzJT4oy1ZmZ0KeKWvT0w4oeh4oyKw7dHTCBPKQogIOKNpSjiioJEUyZmbWQuT3V0RGlyU3BlY3RydW0pRGVza3RvcOKKgjpPdXRTdWZmaXhTcGVjdHJ1beKKmSg-M-Knu-KWsy7iiLVbLi46MV0pCiAgJmZ3YeKorCjiioI6Ii5wbmci4oqZKGltZyJwbmciKXziioI6Ii5naWYi4oqZKGdpZjE2KSk6CikKIyBTYXZlIGZ1bGwgc3BlY3RydW0gZ3JhcGggdG8gZmlsZSBbRmlsZW5hbWUsIFdhdmUvVGFibGVdCldyaXRlU3BlY3RydW1GdWxsIOKGkCAoCiAg4oqZKOKNouKKoig9MeKnuyniiaEo4oeM4oqe4omlw7dTSOKHoVNIw7dGVuKMtWZmdCnilr09MOKHoeKMisO3R0wgTykKICDijaUo4oqCRFMmZm1kLk91dERpclNwZWN0cnVtKURlc2t0b3DiioI6T3V0U3VmZml4U3BlY3RydW3iipkoPjPip7vilrMu4oi1Wy4uOjFdKQogICZmd2HiqKwo4oqCOiIucG5nIuKKmShpbWcicG5nIil84oqCOiIuZ2lmIuKKmShnaWYxNikpOgopCiMgR2VuZXJhdGVzIHVudXNlZCBuYW1lIGZvciBvdXRwdXQKTmV3TmFtZSDihpAgKAogIOKorCjijZomZmxkey4uLi4uIi4vIn0KICB8IHtPdXREaXJBdWRpbyBPdXREaXJJbWFnZS5PdXREaXJHcmFwaC5PdXREaXJTcGVjdHJ1bX0KICAgIOKNmijijZrihpjip7s64o2lKOKWveKItSZmaWYuKT4x4qe7LiZmbGQu4oqCIi4vIkRTJmZtZC4pCgogIClEZXNrdG9wCiAg4o2iKCsxCiAgfCB74oqCOiIud2F2IuKfnCjiioLiioI6T3V0U3VmZml4SW1hZ2U6Ii5wbmciKQogICAgIOKfnCjiioLiioI6T3V0U3VmZml4R3JhcGg6Ii5naWYiKeKfnCjiioLiioI6T3V0U3VmZml4R3JhcGg6Ii5wbmciKQogICAgIOKfnCjiioLiioI6T3V0U3VmZml4U3BlY3RydW06Ii5naWYiKeKfnCjiioLiioI6T3V0U3VmZml4U3BlY3RydW06Ii5wbmciKX1TVDUsLAogICAgL-KGpeKJoSjiiIjCsOKWoTopCgogICkgMAogIFNUNeKKmeKXjAopCiMgR2V0cyBuYW1lIG9mIHJhbmRvbSAud2F2IGZpbGUgW1N1YmZvbGRlcl0KUmFuZG9tQXVkaW9GaWxlIOKGkCAoCiAg4qisKCZmbGQiLi8ifOKNmuKGmOKnuzrilr3iiLUmZmlmLiZmbGQu4oqCIi4vIkRTJmZtZC7iioJJbkRpckF1ZGlvKURlc2t0b3AKICDilr3iiaEoQ0UiLndhdiLCsOKWoSkuCiAg4qisKCIi4peMfMKw4pah4oqP4oyKw5fimoLip7suKT4w4qe7LgopCiMgR2V0cyBuYW1lIG9mIHJhbmRvbSAucG5nIGZpbGUgW1N1YmZvbGRlcl0KUmFuZG9tSW1hZ2VGaWxlIOKGkCAoCiAg4qisKCZmbGQiLi8ifOKNmuKGmOKnuzrilr3iiLUmZmlmLiZmbGQu4oqCIi4vIkRTJmZtZC7iioJJbkRpckltYWdlKURlc2t0b3AKICDilr3iiaEoQ0UiLnBuZyLCsOKWoSkuCiAg4qisKCIi4peMfMKw4pah4oqP4oyKw5fimoLip7suKT4w4qe7LgopCiMgUmVwZWF0IGZ1bmN0aW9uIFtGdW5jdGlvbiwgTnVtXQpSZXBlYXQhIOKGkCDijaVeIQojIENhbGwgcmFuZG9tIGZ1bmN0aW9uICgyIGNob2ljZXMpCkRvUmFuZG9t4oC8IOKGkCDiqKxeIV4h4oyKw5cy4pqCCiMgQ2FsbCByYW5kb20gZnVuY3Rpb24gKDMgY2hvaWNlcykKRG9SYW5kb23igLwhIOKGkCDiqKwoXiF8XiF8XiEp4oyKw5cz4pqCCiMgQ2FsbCByYW5kb20gZnVuY3Rpb24gKDQgY2hvaWNlcykKRG9SYW5kb23igLzigLwg4oaQIOKorCheIXxeIXxeIXxeISnijIrDlzTimoIKIyBFeGl0ICh1c2UgYXQgZW5kIG9mIHNjcmlwdCkKRXhpdCDihpAg4o2lKCZleGl0MClEZXNrdG9wCgojIC0tLS0gU2FtcGxlIHdhdmVmb3Jtcy93YXZldGFibGVzCldhdmXigoEg4oaQIEF2Z3tTaW4zIEFtcDAuN1NpbjIgU2luMX0KV2F2ZeKCgiDihpAgQU0wLjRTaW43IEFNMC41U2luMiBTaW4xCldhdmXigoIg4oaQIEF2Z3tTYXcxIChQdWwgMiAwLjIpfQpXYXZl4oKEIOKGkCBCaXQwLjcgRk0wLjFTaW43IFNpbjEKVGJs4oKBIOKGkCBGTShWYWwgMCAxKSBTaW4yIFNpbjEKVGJs4oKCIOKGkCBNYXggQXZne0FtcChWYWwgMCAxKVNpbjIgU2luMX0KVGJs4oKDIOKGkCBCaXQoVmFsIDAgMSkgQ3JzKFZhbFBJIDAgMSAyKSBTaW4xIFNpbjMKClByaW50ICJUYmzigoMiClRlc3QgVGJs4oKDCkltYWdlIFRibOKCgwpHcmFwaCBUYmzigoMKR3JhcGhTcGVjdHJ1bSBUYmzigoMKCkV4aXQK

# TODO: high/low pass filters
# TODO: IR importing
# TODO: fix Str (stretch) buzzing
# TODO: Sid (side by side)
# TODO: Shp (waveshape)
# TODO: Error handling
# TODO: Batch processing

S ← 2048                                       # Samples per oscillator
O ← 256                                        # Oscillators per table
T ← ×S O                                       # Samples per table
SU ← ÷2+1                                      # Signed to unsigned
US ← -1×2                                      # Unsigned to signed
OU ← ÷⟜⇡S                                      # Oscillator base, unsigned 0-1
OT ← ×τOU                                      # Oscillator base, trigonometric 0-2π
R ← ↯¤T                                        # Oscillator to table (repeat)
U ← ⍥(⊢⍉↯⊟:⟜÷2⊸⧻)                              # Octave up (for playback only)
GH ← 200                                       # Graph height
C ← ↥¯1↧1                                      # Clamp
SW ← ⊏◿⧻,                                      # Select (wrapped)
SI ← +⊙×⟜-⊃⊃(SW⌊)(SW⌈)(-⊸⌊)                    # Select (interpolated)
RI ← SI×⇡:÷,⧻,                                 # Resize (interpolated)
CS ← ∿+η                                       # Cosine
NR ← ↧⊸↥                                       # NaN replace
SC ← NR1÷⟜∿                                    # Sinc
VB ← ÷-1⟜⇡O                                    # Value base
VR ← +⊙×:-⟜:                                   # Value range [min, max, values]
GL ← 16                                        # GIF length (frames)
SH ← 200                                       # Spectrum graph height
FV ← ÷2√S                                      # Fourier max value
SV ← +1⌊×÷2Sⁿ2÷⟜⇡÷4S                           # Spectrum index values
IS ← ⨬(&gifs15|&ims)=2⧻△.                      # Show image or gif
SM ← ⇌∧(⊂+⊸⊃⋅∘(×⊙⋅⊢))⊙(:¤°⊂)                   # Scan add + multiply
MB ← ÷:1ⁿ16 2                                  # Minimum 16-bit value
FT ← ⊃(∠°ℂ)(÷FV⌵)↙+1÷2S fft                    # Fourier transform [phase, amp ← wave]
FU ← ◌°ℂ°fft⊂:⍜°ℂ¯↘1⇌↘1.ℂ⊃(×∿)(×CS)⊙(×FV)      # Fourier untransform [wave ← phase, amp]
DS ← |1 ⍥(⊂:"/")>0⧻.                           # Add slash to directory name
CG ← ⍢⊢(=1⧻)≡(⇌⬚1⊞≠⇡GH⁅×-1GH SU U2C)▽=0⇡⌊÷GL O # Create graph
ST ← ⊂↯:@0↥0-:⊙(⧻.$"_")                        # Num to string w/ trailing zeroes
CE ← ≍⬚@ ↯⊙:¤⧻.⊙⇌⇌                             # Check extension [ext, name]
True ← 1                                       # True constant
False ← 0                                      # False constant
R₀ ← ☇0                                        # Rerank zero
R₁ ← ☇1                                        # Rerank one
V! ← R₁≡^!R₀
VV! ← R₁≡^!R₀⊙R₀
VVA! ← R₁≡^!R₀⊙R₀⊙⊙R₁
VA! ← R₁≡^!R₀⊙R₁
VAA! ← R₁≡^!R₀⊙R₁⊙⊙R₁
A! ← R₁≡^!R₁
AA! ← R₁≡^!R₁⊙R₁

# ---- Convolutions
# Identity (delta) convolution
CId ← ¤¤1

# ---- Table values
# Values (linear) [min, max]
Val ← VR⊙⊙(VB)
# Values (power in) [min, max, base]
ValPI ← VR⊙⊙(ⁿ:VB)
# Values (power out) [min, max, base]
ValPO ← ⇌ValPI:

# ---- Helpers
# Random (min-max) [Min, Max]
Rnd ← +×⚂:⟜-
# Random (min-max) [Min, Max]
RndI ← ⌊Rnd:+1:⍥:<,,

# ---- Wave Operations
# Sum [{Waves/Tables}]
Sum ← °□/+⍚R₁
# Average [{Waves/Tables}]
Avg ← Sum÷⊸⧻
# Amplify [Amount, Wave/Table]
Amp ← VA!×
# Maximize [Wave/Table]
Max ← A!(◌⍥⟜÷≠0.↥∩/↥⟜¯.)
# Phase shift [Amount, Wave/Table]
Phs ← VA!(↻⁅×⧻,)
# Phase shift to zero point [Wave/Table]
Zer ← A!(↻⊗/↧⌵..)
# Bitcrush [Amount (0-1), Wave/Table]
Bit ← VA!(US↧1÷-1:⌊×,SU:⁅ⁿ:2+1×15-:1√↧1↥0)
# Reverse [Wave/Table]
Rev ← A!⇌
# Invert [Wave/Table]
Inv ← A!¯
# Mirror 1st half [Wave/Table]
Mir ← A!(⊂⇌.↙÷2S)
# Fade start & end [Amount (0-1), Wave/Table]
Fad ← VA!(⇌⬚1×⇌:⬚1⟜×÷⧻.⇡⌊÷2×S↥0)
# Crossfade [Amount (0-1), Start Wave/Table, End Wave/Table]
Crs ← VAA!(+⊙(×-:1):⟜×⊙:)
# Stretch [To (0-1), From (0-1), Wave/Table]
Str ← VVA!(⊂⊙RI:-:S⟜RI⊙⊃↙↘∩(↧-1S↥1⁅×S↧1↥0))
# Shift up/down [Amount (¯1-1), Wave/Table]
Shf ← VA!(+)
# Slice [Amount (0-1), Wave/Table]
Slc ← VA!(RI S↯♭⁅↥1×S)
# Speed up/down [Octaves, Wave/Table]
Spd ← VA!(Slcⁿ:2)
# Splice [Amount (0-1), Wave/Table 1, Wave/Table 2]
Spl ← VAA!(⊂⊓↙↘⟜:⁅×S↧1↥0)
# Clip
Clp ← A!(C)
# Wrap [Wave/Table]
Wrp ← A!(US◿+MB1SU)
# Amplutude modulation [Amplitude, Modulator, Carrier]
AM ← VAA!(×+1÷2×-1:)
# Ring modulation [Modulator, Carrier]
RM ← AA!×
# Frequency modulation [Amplitude, Modulator, Carrier]
FM ← VAA!(SI×S+OU×)
# Convolve [Impulse, Wave/Table]
Cnv ← AA!(/+×⍉◫⊙(↯♭):-1⊸+⧻,⧻⟜:)
# Phase randomize [Amount (0-1), Wave/Table]
Phr ← ≡(FU:⊙+:FT):≡×R₁-π×τ⍥(⊂⚂)+1÷2S[]R₀⊙R₁
# Harmonic wave filter [Amount (0-1), Filter Wave/Table, Source Wave/Table]
Hwf ← VAA!(FU⊙×:+1×⊙(-1◌∩FT))
# Downsample [Amount (0-1), Wave/Table]
Dwn ← VA!(
  ⌈÷:S.⁅↥1ⁿ:2+1×-1ₙ2S-:1
  ↯¤S⍉↯⟜÷⟜(≡/+↯⊂:)
)

# ---- Table operations
# Comb filter (feedback) [Amplitude, Harmonic, Table]
TCmb ← ↯O_S SM⊙(⬚0↯⊂⌈÷:T.⁅÷:S⊙R)
# Comb filter (feedforward) [Amplitude, Harmonic, Table]
TCmf ← ↯O_S+×⊙(⊂↯:[0]¤⟜↘⌊÷:¯S⊙(.R))

# ---- Waveforms
# Sine wave [Harmonic]
Sin ← V!(∿×:OT)
# Saw wave [Harmonic]
Saw ← V!(US◿1×:OU)
# Triangle wave [Harmonic]
Tri ← V!(US⌵US◿1+0.25¯×:OU)
# Pulse wave [Harmonic, Duty]
Pul ← VV!(US≤US:Saw)
# Square wave [Harmonic]
Sqr ← V!(Pul:0.5)
# Noise wave []
Nsw ← (☇1US⍥(⊂⚂)S[])
# Silent (empty) wave
Sil ← R₁⬚0↯¤S[]

# ---- Tables
# Noise table []
Nst ← (⍥(⊂Nsw)O[])

# ---- Settings
Desktop ← False
InDirAudio ← "in"
InDirImage ← "in"
OutDirAudio ← "out"
OutDirImage ← "out"
OutDirGraph ← "out"
OutDirSpectrum ← "out"
OutSuffixImage ← ""
OutSuffixGraph ← "g"
OutSuffixSpectrum ← "s"

# ---- Utility
# Print string [String]
Print ← &pf
# Play wave or table [Wave/Table]
Export ← &ap C♭
# Play test tone of wave or table [Wave/Table]
Test ← &ap U3R C♭
# Display graph [Wave/Table]
Graph ← IS CG
# Display image [Wave/Table]
Image ← &ims↯O_S SU R C♭
# Read image from file [Filename]
ImportImage ← RI O≡RI S⍜⍉≡(÷3/+↙3)◌°img&frab⍥(⊂DS InDirImage)Desktop
# Read wave from file [Filename]
ImportWave ← R₁ RI S◌◌°audio&frab⍥(⊂DS InDirAudio)Desktop
# Read 256-wave table from file [Filename]
ImportTable ← ↯O_S RI T◌◌°audio&frab⍥(⊂DS InDirAudio)Desktop
# Table to wave [Table]
TableAverage ← R₁÷O/+↯O_S
# Table to wave [Index (0-255), Table]
TableIndex ← R₁⊡↧-1O↥0⁅:↯O_S:
# Convert image to table [Image]
ImageToTable ← US
# Display graph of impulse [Wave/Table]
GraphConv ← Graph A!(⬚0×↯S1)
# Display spectrum graph [Wave/Table]
GraphSpectrum ← IS⍢⊢(=1⧻)≡(⇌⊞≥÷SH⇡SH⊏SV÷FV↙÷2S⌵fft)▽=0⇡⌊÷GL O
# Display full spectrum graph [Wave/Table]
GraphSpectrumFull ← IS⍢⊢(=1⧻)≡(⇌⊞≥÷SH⇡SH÷FV⌵fft)▽=0⇡⌊÷GL O
# Save wave or table to file [Filename, Wave/Table]
WriteAudio ← &fwa⍥(⊂DS&fmd.OutDirAudio)Desktop⊂⊙(".wav"audio"wav"&asrC♭)
# Save image to file [Filename, Wave/Table]
WriteImage ← (
  ⊂⊙(⊂OutSuffixImage".png"img"png"∵[..:1]↯O_S SU R C♭)
  &fwa⍥(⊂DS&fmd.OutDirImage)Desktop)
# Save graph to file [Filename, Wave/Table]
WriteGraph ← (
  ⍥(⊂DS&fmd.OutDirGraph)Desktop⊂:OutSuffixGraph⊙(>3⧻△.∵[..:1]CG)
  &fwa⨬(⊂:".png"⊙(img"png")|⊂:".gif"⊙(gif16)):
)
# Save spectrum graph to file [Filename, Wave/Table]
WriteSpectrum ← (
  ⊙(⍢⊢(=1⧻)≡(⇌⊞≥÷SH⇡SH⊏SV÷FV↙÷2S⌵fft)▽=0⇡⌊÷GL O)
  ⍥(⊂DS&fmd.OutDirSpectrum)Desktop⊂:OutSuffixSpectrum⊙(>3⧻△.∵[..:1])
  &fwa⨬(⊂:".png"⊙(img"png")|⊂:".gif"⊙(gif16)):
)
# Save full spectrum graph to file [Filename, Wave/Table]
WriteSpectrumFull ← (
  ⊙(⍢⊢(=1⧻)≡(⇌⊞≥÷SH⇡SH÷FV⌵fft)▽=0⇡⌊÷GL O)
  ⍥(⊂DS&fmd.OutDirSpectrum)Desktop⊂:OutSuffixSpectrum⊙(>3⧻△.∵[..:1])
  &fwa⨬(⊂:".png"⊙(img"png")|⊂:".gif"⊙(gif16)):
)
# Generates unused name for output
NewName ← (
  ⨬(⍚&fld{....."./"}
  | {OutDirAudio OutDirImage.OutDirGraph.OutDirSpectrum}
    ⍚(⍚↘⧻:⍥(▽∵&fif.)>1⧻.&fld.⊂"./"DS&fmd.)

  )Desktop
  ⍢(+1
  | {⊂:".wav"⟜(⊂⊂:OutSuffixImage:".png")
     ⟜(⊂⊂:OutSuffixGraph:".gif")⟜(⊂⊂:OutSuffixGraph:".png")
     ⟜(⊂⊂:OutSuffixSpectrum:".gif")⟜(⊂⊂:OutSuffixSpectrum:".png")}ST5,,
    /↥≡(∈°□:)

  ) 0
  ST5⊙◌
)
# Gets name of random .wav file [Subfolder]
RandomAudioFile ← (
  ⨬(&fld"./"|⍚↘⧻:▽∵&fif.&fld.⊂"./"DS&fmd.⊂InDirAudio)Desktop
  ▽≡(CE".wav"°□).
  ⨬(""◌|°□⊏⌊×⚂⧻.)>0⧻.
)
# Gets name of random .png file [Subfolder]
RandomImageFile ← (
  ⨬(&fld"./"|⍚↘⧻:▽∵&fif.&fld.⊂"./"DS&fmd.⊂InDirImage)Desktop
  ▽≡(CE".png"°□).
  ⨬(""◌|°□⊏⌊×⚂⧻.)>0⧻.
)
# Repeat function [Function, Num]
Repeat! ← ⍥^!
# Call random function (2 choices)
DoRandom‼ ← ⨬^!^!⌊×2⚂
# Call random function (3 choices)
DoRandom‼! ← ⨬(^!|^!|^!)⌊×3⚂
# Call random function (4 choices)
DoRandom‼‼ ← ⨬(^!|^!|^!|^!)⌊×4⚂
# Exit (use at end of script)
Exit ← ⍥(&exit0)Desktop

# ---- Sample waveforms/wavetables
Wave₁ ← Avg{Sin3 Amp0.7Sin2 Sin1}
Wave₂ ← AM0.4Sin7 AM0.5Sin2 Sin1
Wave₂ ← Avg{Saw1 (Pul 2 0.2)}
Wave₄ ← Bit0.7 FM0.1Sin7 Sin1
Tbl₁ ← FM(Val 0 1) Sin2 Sin1
Tbl₂ ← Max Avg{Amp(Val 0 1)Sin2 Sin1}
Tbl₃ ← Bit(Val 0 1) Crs(ValPI 0 1 2) Sin1 Sin3

Print "Tbl₃"
Test Tbl₃
Image Tbl₃
Graph Tbl₃
GraphSpectrum Tbl₃

Exit
