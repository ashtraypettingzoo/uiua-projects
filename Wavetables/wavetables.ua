# Experimental!
# Uiua 0.13.0-dev.2
# https://uiua.org/pad?src=0_13_0-dev_2__IyBFeHBlcmltZW50YWwhCiMgVWl1YSAwLjEzLjAtZGV2LjIKIyBbdXJsXQoKIyBUT0RPOiBoaWdoL2xvdyBwYXNzIGZpbHRlcnMKIyBUT0RPOiBJUiBpbXBvcnRpbmcKIyBUT0RPOiBmaXggU3RyIChzdHJldGNoKSBidXp6aW5nCiMgVE9ETzogU2lkIChzaWRlIGJ5IHNpZGUpCiMgVE9ETzogRHduIChkb3duc2FtcGxlKQojIFRPRE86IFNocCAod2F2ZXNoYXBlKQoKUyDihpAgMjA0OCAgICAgICAgICAgICAgICAgICAgICMgU2FtcGxlcyBwZXIgb3NjaWxsYXRvcgpPIOKGkCAyNTYgICAgICAgICAgICAgICAgICAgICAgIyBPc2NpbGxhdG9ycyBwZXIgdGFibGUKVCDihpAgw5dTIE8gICAgICAgICAgICAgICAgICAgICAjIFNhbXBsZXMgcGVyIHRhYmxlClNVIOKGkCDDtzIrMSAgICAgICAgICAgICAgICAgICAgIyBTaWduZWQgdG8gdW5zaWduZWQKVVMg4oaQIC0xw5cyICAgICAgICAgICAgICAgICAgICAjIFVuc2lnbmVkIHRvIHNpZ25lZApPVSDihpAgw7fin5zih6FTICAgICAgICAgICAgICAgICAgICAjIE9zY2lsbGF0b3IgYmFzZSwgdW5zaWduZWQgMC0xCk9UIOKGkCDDl8-ET1UgICAgICAgICAgICAgICAgICAgICMgT3NjaWxsYXRvciBiYXNlLCB0cmlnb25vbWV0cmljIDAtMs-AClIg4oaQIOKGr8KkVCAgICAgICAgICAgICAgICAgICAgICAjIE9zY2lsbGF0b3IgdG8gdGFibGUgKHJlcGVhdCkKVSDihpAg4o2lKOKKouKNieKGr-KKnzrin5zDtzLiirjip7spICAgICAgICAgICAgIyBPY3RhdmUgdXAgKGZvciBwbGF5YmFjayBvbmx5KQpHSCDihpAgMjAwICAgICAgICAgICAgICAgICAgICAgIyBHcmFwaCBoZWlnaHQKQyDihpAg4oalwq8x4oanMSAgICAgICAgICAgICAgICAgICAgIyBDbGFtcApTVyDihpAg4oqP4pe_4qe7LCAgICAgICAgICAgICAgICAgICAgIyBTZWxlY3QgKHdyYXBwZWQpClNJIOKGkCAr4oqZw5fin5wt4oqD4oqDKFNX4oyKKShTV-KMiCkoLeKKuOKMiikgICMgU2VsZWN0IChpbnRlcnBvbGF0ZWQpClJJIOKGkCBTScOX4oehOsO3LOKnuywgICAgICAgICAgICAgICAjIFJlc2l6ZSAoaW50ZXJwb2xhdGVkKQpDUyDihpAg4oi_K863ICAgICAgICAgICAgICAgICAgICAgIyBDb3NpbmUKTlIg4oaQIOKGp-KKuOKGpSAgICAgICAgICAgICAgICAgICAgICMgTmFOIHJlcGxhY2UKU0Mg4oaQIE5SMcO34p-c4oi_ICAgICAgICAgICAgICAgICAgIyBTaW5jClZCIOKGkCDDty0x4p-c4oehTyAgICAgICAgICAgICAgICAgICMgVmFsdWUgYmFzZQpWUiDihpAgK-KKmcOXOi3in5w6ICAgICAgICAgICAgICAgICAjIFZhbHVlIHJhbmdlIFttaW4sIG1heCwgdmFsdWVzXQpHTCDihpAgMTYgICAgICAgICAgICAgICAgICAgICAgIyBHSUYgbGVuZ3RoIChmcmFtZXMpClNIIOKGkCAyMDAgICAgICAgICAgICAgICAgICAgICAjIFNwZWN0cnVtIGdyYXBoIGhlaWdodApGViDihpAgw7cy4oiaUyAgICAgICAgICAgICAgICAgICAgIyBGb3VyaWVyIG1heCB2YWx1ZQpTViDihpAgKzHijIrDl8O3MlPigb8yw7fin5zih6HDtzRTICAgICAgICAgIyBTcGVjdHJ1bSBpbmRleCB2YWx1ZXMKSVMg4oaQIOKorCgmZ2lmczE1fCZpbXMpPTLip7vilrMuICAgICMgU2hvdyBpbWFnZSBvciBnaWYKU00g4oaQIOKHjOKIpyjiioIr4oq44oqD4ouF4oiYKMOX4oqZ4ouF4oqiKSniipkoOsKkwrDiioIpICMgU2NhbiBhZGQgKyBtdWx0aXBseQpNQiDihpAgw7c6MeKBvzE2IDIgICAgICAgICAgICAgICAgIyBNaW5pbXVtIDE2LWJpdCB2YWx1ZQpS4oKAIOKGkCDimIcwICAgICAgICAgICAgICAgICAgICAgICMgUmVyYW5rIHplcm8KUuKCgSDihpAg4piHMSAgICAgICAgICAgICAgICAgICAgICAjIFJlcmFuayBvbmUKViEg4oaQIFLigoHiiaFeIVLigoAKVlYhIOKGkCBS4oKB4omhXiFS4oKA4oqZUuKCgApWVkEhIOKGkCBS4oKB4omhXiFS4oKA4oqZUuKCgOKKmeKKmVLigoEKVkEhIOKGkCBS4oKB4omhXiFS4oKA4oqZUuKCgQpWQUEhIOKGkCBS4oKB4omhXiFS4oKA4oqZUuKCgeKKmeKKmVLigoEKQSEg4oaQIFLigoHiiaFeIVLigoEKQUEhIOKGkCBS4oKB4omhXiFS4oKB4oqZUuKCgQoKIyBDb252b2x1dGlvbnMKCkNJZCDihpAgwqTCpDEgIyBJZGVudGl0eSAoZGVsdGEpIGNvbnZvbHV0aW9uCgojIFRhYmxlIHZhbHVlcwoKVmFsIOKGkCBWUuKKmeKKmShWQikgICAgICMgVmFsdWVzIChsaW5lYXIpIFttaW4sIG1heF0KVmFsUEkg4oaQIFZS4oqZ4oqZKOKBvzpWQikgIyBWYWx1ZXMgKHBvd2VyIGluKSBbbWluLCBtYXgsIGJhc2VdClZhbFBPIOKGkCDih4xWYWxQSTogICAgIyBWYWx1ZXMgKHBvd2VyIG91dCkgW21pbiwgbWF4LCBiYXNlXQoKIyBIZWxwZXJzCgpSbmRJbnQg4oaQICsx4oyKw5fimoIgIyBSYW5kb20gaW50ZWdlciAxLU4gW01heF0KCiMgV2F2ZSBPcGVyYXRpb25zCgpTdW0g4oaQIMKw4pahLyvijZpS4oKBICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgU3VtIFt7V2F2ZXMvVGFibGVzfV0KQXZnIOKGkCBTdW3Dt-KKuOKnuyAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBBdmVyYWdlIFt7V2F2ZXMvVGFibGVzfV0KQW1wIOKGkCBWQSHDlyAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIEFtcGxpZnkgW0Ftb3VudCwgV2F2ZS9UYWJsZV0KTWF4IOKGkCBBISjil4zijaXin5zDt-KJoDAu4oal4oipL-KGpeKfnMKvLikgICAgICAgICAgICAgICAgICAgICMgTWF4aW1pemUgW1dhdmUvVGFibGVdClBocyDihpAgVkEhKOKGu-KBhcOX4qe7LCkgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBQaGFzZSBzaGlmdCBbQW1vdW50LCBXYXZlL1RhYmxlXQpaZXIg4oaQIEEhKOKGu-KKly_ihqfijLUuLikgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFBoYXNlIHNoaWZ0IHRvIHplcm8gcG9pbnQgW1dhdmUvVGFibGVdCkJpdCDihpAgVkEhKFVT4oanMcO3LTE64oyKw5csU1U64oGF4oG_OjIrMcOXMTUtOjHiiJrihqcx4oalMCkgICMgQml0Y3J1c2ggW0Ftb3VudCAoMC0xKSwgV2F2ZS9UYWJsZV0KUmV2IOKGkCBBIeKHjCAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBSZXZlcnNlIFtXYXZlL1RhYmxlXQpJbnYg4oaQIEEhwq8gICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgSW52ZXJ0IFtXYXZlL1RhYmxlXQpNaXIg4oaQIEEhKOKKguKHjC7ihpnDtzJTKSAgICAgICAgICAgICAgICAgICAgICAgICAgICMgTWlycm9yIDFzdCBoYWxmIFtXYXZlL1RhYmxlXQpGYWQg4oaQIFZBISjih4zirJoxw5fih4w64qyaMeKfnMOXw7fip7su4oeh4oyKw7cyw5dT4oalMCkgICAgICAgICAgICAjIEZhZGUgaW4gJiBvdXQgW0Ftb3VudCAoMC0xKSwgV2F2ZS9UYWJsZV0KQ3JzIOKGkCBWQUEhKCviipkow5ctOjEpOuKfnMOX4oqZOikgICAgICAgICAgICAgICAgICAgIyBDcm9zc2ZhZGUgW0Ftb3VudCAoMC0xKSwgU3RhcnQgV2F2ZS9UYWJsZSwgRW5kIFdhdmUvVGFibGVdClN0ciDihpAgVlZBISjiioLiiplSSTotOlPin5xSSeKKmeKKg-KGmeKGmOKIqSjihqctMVPihqUx4oGFw5dT4oanMeKGpTApKSAjIFN0cmV0Y2ggW1RvICgwLTEpLCBGcm9tICgwLTEpLCBXYXZlL1RhYmxlXQpTaGYg4oaQIFZBISgrKSAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBTaGlmdCB1cC9kb3duIFtBbW91bnQgKMKvMS0xKSwgV2F2ZS9UYWJsZV0KU2xjIOKGkCBWQSEoUkkgU-KGr-KZreKBheKGpTHDl1MpICAgICAgICAgICAgICAgICAgICAgICMgU2xpY2UgW0Ftb3VudCAoMC0xKSwgV2F2ZS9UYWJsZV0KU3BkIOKGkCBWQSEoU2xj4oG_OjIpICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBTcGVlZCB1cC9kb3duIFtPY3RhdmVzLCBXYXZlL1RhYmxlXQpTcGwg4oaQIFZBQSEo4oqC4oqT4oaZ4oaY4p-cOuKBhcOXU-KGpzHihqUwKSAgICAgICAgICAgICAgICAgICAjIFNwbGljZSBbQW1vdW50ICgwLTEpLCBXYXZlL1RhYmxlIDEsIFdhdmUvVGFibGUgMl0KQ2xwIOKGkCBBIShDKSAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgQ2xpcApXcnAg4oaQIEEhKFVT4pe_K01CMVNVKSAgICAgICAgICAgICAgICAgICAgICAgICAjIFdyYXAgW1dhdmUvVGFibGVdCkFNIOKGkCBWQUEhKMOXKzHDtzLDly0xOikgICAgICAgICAgICAgICAgICAgICAgICAjIEFtcGx1dHVkZSBtb2R1bGF0aW9uIFtBbXBsaXR1ZGUsIE1vZHVsYXRvciwgQ2Fycmllcl0KUk0g4oaQIEFBIcOXICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFJpbmcgbW9kdWxhdGlvbiBbTW9kdWxhdG9yLCBDYXJyaWVyXQpGTSDihpAgVkFBIShTScOXUytPVcOXKSAgICAgICAgICAgICAgICAgICAgICAgICAjIEZyZXF1ZW5jeSBtb2R1bGF0aW9uIFtBbXBsaXR1ZGUsIE1vZHVsYXRvciwgQ2Fycmllcl0KQ252IOKGkCBBQSEoLyvDl-KNieKXq-KKmSjihq_ima0pOi0x4oq4K-Knuyzip7vin5w6KSAgICAgICAgICAgICAjIENvbnZvbHZlIFtJbXB1bHNlLCBXYXZlL1RhYmxlXQoKIyBUYWJsZSBvcGVyYXRpb25zCgpUQ21iIOKGkCDihq9PX1MgU03iipko4qyaMOKGr-KKguKMiMO3OlQu4oGFw7c6U-KKmVIpICAgICMgQ29tYiBmaWx0ZXIgKGZlZWRiYWNrKSBbQW1wbGl0dWRlLCBIYXJtb25pYywgVGFibGVdClRDbWYg4oaQIOKGr09fUyvDl-KKmSjiioLihq86WzBdwqTin5zihpjijIrDtzrCr1PiipkoLlIpKSAjIENvbWIgZmlsdGVyIChmZWVkZm9yd2FyZCkgW0FtcGxpdHVkZSwgSGFybW9uaWMsIFRhYmxlXQoKIyBCYXNpYyB3YXZlZm9ybXMKClNpbiDihpAgViEo4oi_w5c6T1QpICAgICAgICAgICAgICMgU2luZSB3YXZlIFtIYXJtb25pY10KU2F3IOKGkCBWIShVU-KXvzHDlzpPVSkgICAgICAgICAgIyBTYXcgd2F2ZSBbSGFybW9uaWNdClRyaSDihpAgViEoVVPijLVVU-KXvzErMC4yNcKvw5c6T1UpICMgVHJpYW5nbGUgd2F2ZSBbSGFybW9uaWNdClB1bCDihpAgVlYhKFVT4omkVVM6U2F3KSAgICAgICAgIyBQdWxzZSB3YXZlIFtIYXJtb25pYywgRHV0eV0KU3FyIOKGkCBWIShQdWw6MC41KSAgICAgICAgICAgIyBTcXVhcmUgd2F2ZSBbSGFybW9uaWNdCk5zdyDihpAgKOKYhzFVU-KNpSjiioLimoIpU1tdKSAgICAgICAgIyBOb2lzZSB3YXZlIFtdCk5zdCDihpAgKOKNpSjiioJOc3cpT1tdKSAgICAgICAgICAjIE5vaXNlIHRhYmxlIFtdCgojIFV0aWxpdHkKClByaW50IOKGkCAmcGYKRXhwb3J0IOKGkCAmYXAgQ-KZrQpUZXN0IOKGkCAmYXAgVTNSIEPima0KR3JhcGgg4oaQIElT4o2i4oqiKD0x4qe7KeKJoSjih4zirJox4oqe4omg4oehR0jigYXDly0xR0ggU1UgVTJDKeKWvT0w4oeh4oyKw7dHTCBPCkltYWdlIOKGkCAmaW1z4oavT19TIFNVIFIgQ-KZrQpJbXBvcnRJbWFnZSDihpAgUkkgT-KJoVJJIFPijZzijYniiaEow7czLyvihpkzKeKXjMKwaW1nJmZyYWIKSW1wb3J0V2F2ZSDihpAgUuKCgCBSSSBT4peM4peMwrBhdWRpbyZmcmFiCkltcG9ydFRhYmxlIOKGkCDihq9PX1MgUkkgVOKXjOKXjMKwYXVkaW8mZnJhYiAjIEltcG9ydCAyNTYtd2F2ZSB0YWJsZQpUYWJsZUF2ZXJhZ2Ug4oaQIFLigoHDt08vK-KGr09fUyAgICAgICAgICAgICMgVGFibGUgdG8gd2F2ZSBbVGFibGVdClRhYmxlSW5kZXgg4oaQIFLigoHiiqHihqctMU_ihqUw4oGFOuKGr09fUzogICAgICAgICMgVGFibGUgdG8gd2F2ZSBbSW5kZXggKDAtMjU1KSwgVGFibGVdCkltYWdlVG9UYWJsZSDihpAgVVMKR3JhcGhDb252IOKGkCBJUyBHcmFwaCBBISjirJoww5fihq9TMSkKR3JhcGhTcGVjdHJ1bSDihpAgSVPijaLiiqIoPTHip7sp4omhKOKHjOKKnuKJpcO3U0jih6FTSOKKj1NWw7dGVuKGmcO3MlPijLVmZnQp4pa9PTDih6HijIrDt0dMIE8KR3JhcGhTcGVjdHJ1bUZ1bGwg4oaQIElT4o2i4oqiKD0x4qe7KeKJoSjih4ziip7iiaXDt1NI4oehU0jDt0ZW4oaZw7cyU-KMtWZmdCnilr09MOKHoeKMisO3R0wgTwoKIyBTYW1wbGUgd2F2ZWZvcm1zL3dhdmV0YWJsZXMKCldhdmXigoEg4oaQIEF2Z3tTaW4zIEFtcDAuN1NpbjIgU2luMX0KV2F2ZeKCgiDihpAgQU0wLjRTaW43IEFNMC41U2luMiBTaW4xCldhdmXigoIg4oaQIEF2Z3tTYXcxIChQdWwgMiAwLjIpfQpXYXZl4oKEIOKGkCBCaXQwLjcgRk0wLjFTaW43IFNpbjEKVGJs4oKBIOKGkCBGTShWYWwgMCAxKSBTaW4yIFNpbjEKVGJs4oKCIOKGkCBNYXggQXZne0FtcChWYWwgMCAxKVNpbjIgU2luMX0KVGJs4oKDIOKGkCBCaXQoVmFsIDAgMSkgQ3JzKFZhbFBJIDAgMSAyKSBTaW4xIFNpbjMKClByaW50ICJUYmzigoMiClRlc3QgVGJs4oKDCkltYWdlIFRibOKCgwpHcmFwaCBUYmzigoMKR3JhcGhTcGVjdHJ1bSBUYmzigoMK

# TODO: high/low pass filters
# TODO: IR importing
# TODO: fix Str (stretch) buzzing
# TODO: Sid (side by side)
# TODO: Dwn (downsample)
# TODO: Shp (waveshape)

S ← 2048                     # Samples per oscillator
O ← 256                      # Oscillators per table
T ← ×S O                     # Samples per table
SU ← ÷2+1                    # Signed to unsigned
US ← -1×2                    # Unsigned to signed
OU ← ÷⟜⇡S                    # Oscillator base, unsigned 0-1
OT ← ×τOU                    # Oscillator base, trigonometric 0-2π
R ← ↯¤T                      # Oscillator to table (repeat)
U ← ⍥(⊢⍉↯⊟:⟜÷2⊸⧻)            # Octave up (for playback only)
GH ← 200                     # Graph height
C ← ↥¯1↧1                    # Clamp
SW ← ⊏◿⧻,                    # Select (wrapped)
SI ← +⊙×⟜-⊃⊃(SW⌊)(SW⌈)(-⊸⌊)  # Select (interpolated)
RI ← SI×⇡:÷,⧻,               # Resize (interpolated)
CS ← ∿+η                     # Cosine
NR ← ↧⊸↥                     # NaN replace
SC ← NR1÷⟜∿                  # Sinc
VB ← ÷-1⟜⇡O                  # Value base
VR ← +⊙×:-⟜:                 # Value range [min, max, values]
GL ← 16                      # GIF length (frames)
SH ← 200                     # Spectrum graph height
FV ← ÷2√S                    # Fourier max value
SV ← +1⌊×÷2Sⁿ2÷⟜⇡÷4S         # Spectrum index values
IS ← ⨬(&gifs15|&ims)=2⧻△.    # Show image or gif
SM ← ⇌∧(⊂+⊸⊃⋅∘(×⊙⋅⊢))⊙(:¤°⊂) # Scan add + multiply
MB ← ÷:1ⁿ16 2                # Minimum 16-bit value
R₀ ← ☇0                      # Rerank zero
R₁ ← ☇1                      # Rerank one
V! ← R₁≡^!R₀
VV! ← R₁≡^!R₀⊙R₀
VVA! ← R₁≡^!R₀⊙R₀⊙⊙R₁
VA! ← R₁≡^!R₀⊙R₁
VAA! ← R₁≡^!R₀⊙R₁⊙⊙R₁
A! ← R₁≡^!R₁
AA! ← R₁≡^!R₁⊙R₁

# Convolutions

CId ← ¤¤1 # Identity (delta) convolution

# Table values

Val ← VR⊙⊙(VB)     # Values (linear) [min, max]
ValPI ← VR⊙⊙(ⁿ:VB) # Values (power in) [min, max, base]
ValPO ← ⇌ValPI:    # Values (power out) [min, max, base]

# Helpers

RndInt ← +1⌊×⚂ # Random integer 1-N [Max]

# Wave Operations

Sum ← °□/+⍚R₁                               # Sum [{Waves/Tables}]
Avg ← Sum÷⊸⧻                                # Average [{Waves/Tables}]
Amp ← VA!×                                  # Amplify [Amount, Wave/Table]
Max ← A!(◌⍥⟜÷≠0.↥∩/↥⟜¯.)                    # Maximize [Wave/Table]
Phs ← VA!(↻⁅×⧻,)                            # Phase shift [Amount, Wave/Table]
Zer ← A!(↻⊗/↧⌵..)                           # Phase shift to zero point [Wave/Table]
Bit ← VA!(US↧1÷-1:⌊×,SU:⁅ⁿ:2+1×15-:1√↧1↥0)  # Bitcrush [Amount (0-1), Wave/Table]
Rev ← A!⇌                                   # Reverse [Wave/Table]
Inv ← A!¯                                   # Invert [Wave/Table]
Mir ← A!(⊂⇌.↙÷2S)                           # Mirror 1st half [Wave/Table]
Fad ← VA!(⇌⬚1×⇌:⬚1⟜×÷⧻.⇡⌊÷2×S↥0)            # Fade in & out [Amount (0-1), Wave/Table]
Crs ← VAA!(+⊙(×-:1):⟜×⊙:)                   # Crossfade [Amount (0-1), Start Wave/Table, End Wave/Table]
Str ← VVA!(⊂⊙RI:-:S⟜RI⊙⊃↙↘∩(↧-1S↥1⁅×S↧1↥0)) # Stretch [To (0-1), From (0-1), Wave/Table]
Shf ← VA!(+)                                # Shift up/down [Amount (¯1-1), Wave/Table]
Slc ← VA!(RI S↯♭⁅↥1×S)                      # Slice [Amount (0-1), Wave/Table]
Spd ← VA!(Slcⁿ:2)                           # Speed up/down [Octaves, Wave/Table]
Spl ← VAA!(⊂⊓↙↘⟜:⁅×S↧1↥0)                   # Splice [Amount (0-1), Wave/Table 1, Wave/Table 2]
Clp ← A!(C)                                 # Clip
Wrp ← A!(US◿+MB1SU)                         # Wrap [Wave/Table]
AM ← VAA!(×+1÷2×-1:)                        # Amplutude modulation [Amplitude, Modulator, Carrier]
RM ← AA!×                                   # Ring modulation [Modulator, Carrier]
FM ← VAA!(SI×S+OU×)                         # Frequency modulation [Amplitude, Modulator, Carrier]
Cnv ← AA!(/+×⍉◫⊙(↯♭):-1⊸+⧻,⧻⟜:)             # Convolve [Impulse, Wave/Table]

# Table operations

TCmb ← ↯O_S SM⊙(⬚0↯⊂⌈÷:T.⁅÷:S⊙R)    # Comb filter (feedback) [Amplitude, Harmonic, Table]
TCmf ← ↯O_S+×⊙(⊂↯:[0]¤⟜↘⌊÷:¯S⊙(.R)) # Comb filter (feedforward) [Amplitude, Harmonic, Table]

# Basic waveforms

Sin ← V!(∿×:OT)             # Sine wave [Harmonic]
Saw ← V!(US◿1×:OU)          # Saw wave [Harmonic]
Tri ← V!(US⌵US◿1+0.25¯×:OU) # Triangle wave [Harmonic]
Pul ← VV!(US≤US:Saw)        # Pulse wave [Harmonic, Duty]
Sqr ← V!(Pul:0.5)           # Square wave [Harmonic]
Nsw ← (☇1US⍥(⊂⚂)S[])        # Noise wave []
Nst ← (⍥(⊂Nsw)O[])          # Noise table []

# Utility

Print ← &pf
Export ← &ap C♭
Test ← &ap U3R C♭
Graph ← IS⍢⊢(=1⧻)≡(⇌⬚1⊞≠⇡GH⁅×-1GH SU U2C)▽=0⇡⌊÷GL O
Image ← &ims↯O_S SU R C♭
ImportImage ← RI O≡RI S⍜⍉≡(÷3/+↙3)◌°img&frab
ImportWave ← R₀ RI S◌◌°audio&frab
ImportTable ← ↯O_S RI T◌◌°audio&frab # Import 256-wave table
TableAverage ← R₁÷O/+↯O_S            # Table to wave [Table]
TableIndex ← R₁⊡↧-1O↥0⁅:↯O_S:        # Table to wave [Index (0-255), Table]
ImageToTable ← US
GraphConv ← IS Graph A!(⬚0×↯S1)
GraphSpectrum ← IS⍢⊢(=1⧻)≡(⇌⊞≥÷SH⇡SH⊏SV÷FV↙÷2S⌵fft)▽=0⇡⌊÷GL O
GraphSpectrumFull ← IS⍢⊢(=1⧻)≡(⇌⊞≥÷SH⇡SH÷FV↙÷2S⌵fft)▽=0⇡⌊÷GL O

# Sample waveforms/wavetables

Wave₁ ← Avg{Sin3 Amp0.7Sin2 Sin1}
Wave₂ ← AM0.4Sin7 AM0.5Sin2 Sin1
Wave₂ ← Avg{Saw1 (Pul 2 0.2)}
Wave₄ ← Bit0.7 FM0.1Sin7 Sin1
Tbl₁ ← FM(Val 0 1) Sin2 Sin1
Tbl₂ ← Max Avg{Amp(Val 0 1)Sin2 Sin1}
Tbl₃ ← Bit(Val 0 1) Crs(ValPI 0 1 2) Sin1 Sin3

Print "Tbl₃"
Test Tbl₃
Image Tbl₃
Graph Tbl₃
GraphSpectrum Tbl₃
