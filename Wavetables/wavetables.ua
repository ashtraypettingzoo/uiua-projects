# Experimental!
# Uiua 0.13.0-dev.2
# https://uiua.org/pad?src=0_13_0-dev_2__IyBFeHBlcmltZW50YWwhCiMgVWl1YSAwLjEzLjAtZGV2LjIKIyBbdXJsXQoKIyBUT0RPOiBoaWdoL2xvdyBwYXNzIGZpbHRlcnMKIyBUT0RPOiBJUiBpbXBvcnRpbmcKIyBUT0RPOiBmaXggU3RyIChzdHJldGNoKSBidXp6aW5nCiMgVE9ETzogU2lkIChzaWRlIGJ5IHNpZGUpCiMgVE9ETzogRHduIChkb3duc2FtcGxlKQojIFRPRE86IFNocCAod2F2ZXNoYXBlKQoKUyDihpAgMjA0OCAgICAgICAgICAgICAgICAgICAgIyBTYW1wbGVzIHBlciBvc2NpbGxhdG9yCk8g4oaQIDI1NiAgICAgICAgICAgICAgICAgICAgICMgT3NjaWxsYXRvcnMgcGVyIHRhYmxlClQg4oaQIMOXUyBPICAgICAgICAgICAgICAgICAgICAjIFNhbXBsZXMgcGVyIHRhYmxlClNVIOKGkCDDtzIrMSAgICAgICAgICAgICAgICAgICAjIFNpZ25lZCB0byB1bnNpZ25lZApVUyDihpAgLTHDlzIgICAgICAgICAgICAgICAgICAgIyBVbnNpZ25lZCB0byBzaWduZWQKT1Ug4oaQIMO34p-c4oehUyAgICAgICAgICAgICAgICAgICAjIE9zY2lsbGF0b3IgYmFzZSwgdW5zaWduZWQgMC0xCk9UIOKGkCDDl8-ET1UgICAgICAgICAgICAgICAgICAgIyBPc2NpbGxhdG9yIGJhc2UsIHRyaWdvbm9tZXRyaWMgMC0yz4AKUiDihpAg4oavwqRUICAgICAgICAgICAgICAgICAgICAgIyBPc2NpbGxhdG9yIHRvIHRhYmxlIChyZXBlYXQpClUg4oaQIOKNpSjiiqLijYnihq_iip864p-cw7cy4oq44qe7KSAgICAgICAgICAgIyBPY3RhdmUgdXAgKGZvciBwbGF5YmFjayBvbmx5KQpHSCDihpAgMjAwICAgICAgICAgICAgICAgICAgICAjIEdyYXBoIGhlaWdodApDIOKGkCDihqXCrzHihqcxICAgICAgICAgICAgICAgICAgICMgQ2xhbXAKU1cg4oaQIOKKj-KXv-KnuywgICAgICAgICAgICAgICAgICAgIyBTZWxlY3QgKHdyYXBwZWQpClNJIOKGkCAr4oqZw5fin5wt4oqD4oqDKFNX4oyKKShTV-KMiCkoLeKKuOKMiikgIyBTZWxlY3QgKGludGVycG9sYXRlZCkKUkkg4oaQIFNJw5fih6E6w7cs4qe7LCAgICAgICAgICAgICAgIyBSZXNpemUgKGludGVycG9sYXRlZCkKQ1Mg4oaQIOKIvyvOtyAgICAgICAgICAgICAgICAgICAgIyBDb3NpbmUKTlIg4oaQIOKGp-KKuOKGpSAgICAgICAgICAgICAgICAgICAgIyBOYU4gcmVwbGFjZQpTQyDihpAgTlIxw7fin5ziiL8gICAgICAgICAgICAgICAgICMgU2luYwpWQiDihpAgw7ctMeKfnOKHoU8gICAgICAgICAgICAgICAgICMgVmFsdWUgYmFzZQpWUiDihpAgK-KKmcOXOi3in5w6ICAgICAgICAgICAgICAgICMgVmFsdWUgcmFuZ2UgW21pbiwgbWF4LCB2YWx1ZXNdCkdMIOKGkCAxNiAgICAgICAgICAgICAgICAgICAgICMgR0lGIGxlbmd0aCAoZnJhbWVzKQpTSCDihpAgMjAwICAgICAgICAgICAgICAgICAgICAjIFNwZWN0cnVtIGdyYXBoIGhlaWdodApGViDihpAgw7cy4oiaUyAgICAgICAgICAgICAgICAgICAjIEZvdXJpZXIgbWF4IHZhbHVlClNWIOKGkCArMeKMisOXw7cyU-KBvzLDt-KfnOKHocO3NFMgICAgICAgICMgU3BlY3RydW0gaW5kZXggdmFsdWVzCklTIOKGkCDiqKwoJmdpZnMxNXwmaW1z4oqiKT0x4qe7LiAgICMgU2hvdyBpbWFnZSBvciBnaWYKUuKCgCDihpAg4piHMCAgICAgICAgICAgICAgICAgICAgICMgUmVyYW5rIHplcm8KUuKCgSDihpAg4piHMSAgICAgICAgICAgICAgICAgICAgICMgUmVyYW5rIG9uZQpWISDihpAgUuKCgeKJoV4hUuKCgApWViEg4oaQIFLigoHiiaFeIVLigoDiiplS4oKAClZWQSEg4oaQIFLigoHiiaFeIVLigoDiiplS4oKA4oqZ4oqZUuKCgQpWQSEg4oaQIFLigoHiiaFeIVLigoDiiplS4oKBClZBQSEg4oaQIFLigoHiiaFeIVLigoDiiplS4oKB4oqZ4oqZUuKCgQpBISDihpAgUuKCgeKJoV4hUuKCgQpBQSEg4oaQIFLigoHiiaFeIVLigoHiiplS4oKBCgojIENvbnZvbHV0aW9ucwoKQ0lkIOKGkCDCpMKkMSAjIElkZW50aXR5IChkZWx0YSkgY29udm9sdXRpb24KCiMgVGFibGUgdmFsdWVzCgpWYWwg4oaQIFZS4oqZ4oqZKFZCKSAgICAgIyBWYWx1ZXMgKGxpbmVhcikgW21pbiwgbWF4XQpWYWxQSSDihpAgVlLiipniipko4oG_OlZCKSAjIFZhbHVlcyAocG93ZXIgaW4pIFttaW4sIG1heCwgYmFzZV0KVmFsUE8g4oaQIOKHjFZhbFBJOiAgICAjIFZhbHVlcyAocG93ZXIgb3V0KSBbbWluLCBtYXgsIGJhc2VdCgojIEhlbHBlcnMKClJuZEludCDihpAgKzHijIrDl-KagiAjIFJhbmRvbSBpbnRlZ2VyIDEtTiBbTWF4XQoKIyBPcGVyYXRpb25zCgpTdW0g4oaQIMKw4pahLyvijZpS4oKBICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgU3VtIFt7V2F2ZXMvVGFibGVzfV0KQXZnIOKGkCBTdW3Dt-KKuOKnuyAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBBdmVyYWdlIFt7V2F2ZXMvVGFibGVzfV0KQW1wIOKGkCBWQSHDlyAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIEFtcGxpZnkgW0Ftb3VudCwgV2F2ZS9UYWJsZV0KTWF4IOKGkCBBISjil4zijaXin5zDt-KJoDAu4oal4oipL-KGpeKfnMKvLikgICAgICAgICAgICAgICAgICAgICMgTWF4aW1pemUgW1dhdmUvVGFibGVdClBocyDihpAgVkEhKOKGu-KBhcOX4qe7LCkgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBQaGFzZSBzaGlmdCBbQW1vdW50LCBXYXZlL1RhYmxlXQpaZXIg4oaQIEEhKOKGu-KKly_ihqfijLUuLikgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFBoYXNlIHNoaWZ0IHRvIHplcm8gcG9pbnQgW1dhdmUvVGFibGVdCkJpdCDihpAgVkEhKFVT4oanMcO3LTE64oyKw5csU1U64oGF4oG_OjIrMcOXMTUtOjHiiJrihqcx4oalMCkgICMgQml0Y3J1c2ggW0Ftb3VudCAoMC0xKSwgV2F2ZS9UYWJsZV0KUmV2IOKGkCBBIeKHjCAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBSZXZlcnNlIFtXYXZlL1RhYmxlXQpJbnYg4oaQIEEhwq8gICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgSW52ZXJ0IFtXYXZlL1RhYmxlXQpNaXIg4oaQIEEhKOKKguKHjC7ihpnDtzJTKSAgICAgICAgICAgICAgICAgICAgICAgICAgICMgTWlycm9yIDFzdCBoYWxmIFtXYXZlL1RhYmxlXQpGYWQg4oaQIFZBISjih4zirJoxw5fih4w64qyaMeKfnMOXw7fip7su4oeh4oyKw7cyw5dT4oalMCkgICAgICAgICAgICAjIEZhZGUgaW4gJiBvdXQgW0Ftb3VudCAoMC0xKSwgV2F2ZS9UYWJsZV0KQ3JzIOKGkCBWQUEhKCviipkow5ctOjEpOuKfnMOX4oqZOikgICAgICAgICAgICAgICAgICAgIyBDcm9zc2ZhZGUgW0Ftb3VudCAoMC0xKSwgU3RhcnQgV2F2ZS9UYWJsZSwgRW5kIFdhdmUvVGFibGVdClN0ciDihpAgVlZBISjiioLiiplSSTotOlPin5xSSeKKmeKKg-KGmeKGmOKIqSjihqctMVPihqUx4oGFw5dT4oanMeKGpTApKSAjIFN0cmV0Y2ggW1RvICgwLTEpLCBGcm9tICgwLTEpLCBXYXZlL1RhYmxlXQpTaGMg4oaQIFZBIShDKykgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBTaGlmdCB1cC9kb3duIChjbGlwKSBbQW1vdW50ICjCrzEtMSksIFdhdmUvVGFibGVdClNodyDihpAgVkEhKOKNpShVU-KXvzFTVSniiaAw4p-cKykgICAgICAgICAgICAgICAgICAgICMgU2hpZnQgdXAvZG93biAod3JhcCkgW0Ftb3VudCAowq8xLTEpLCBXYXZlL1RhYmxlXQpTbGMg4oaQIFZBIShSSSBT4oav4pmt4oGF4oalMcOXUykgICAgICAgICAgICAgICAgICAgICAgIyBTbGljZSBbQW1vdW50ICgwLTEpLCBXYXZlL1RhYmxlXQpTcGQg4oaQIFZBIShTbGPigb86MikgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFNwZWVkIHVwL2Rvd24gW09jdGF2ZXMsIFdhdmUvVGFibGVdClNwbCDihpAgVkFBISjiioLiipPihpnihpjin5w64oGFw5dT4oanMeKGpTApICAgICAgICAgICAgICAgICAgICMgU3BsaWNlIFtBbW91bnQgKDAtMSksIFdhdmUvVGFibGUgMSwgV2F2ZS9UYWJsZSAyXQpBTSDihpAgVkFBISjDlysxw7cyw5ctMTopICAgICAgICAgICAgICAgICAgICAgICAgIyBBbXBsdXR1ZGUgbW9kdWxhdGlvbiBbQW1wbGl0dWRlLCBNb2R1bGF0b3IsIENhcnJpZXJdClJNIOKGkCBBQSHDlyAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBSaW5nIG1vZHVsYXRpb24gW01vZHVsYXRvciwgQ2Fycmllcl0KRk0g4oaQIFZBQSEoU0nDl1MrT1XDlykgICAgICAgICAgICAgICAgICAgICAgICAgIyBGcmVxdWVuY3kgbW9kdWxhdGlvbiBbQW1wbGl0dWRlLCBNb2R1bGF0b3IsIENhcnJpZXJdCkNudiDihpAgQUEhKC8rw5fijYnil6viipko4oav4pmtKTotMeKKuCvip7ss4qe74p-cOikgICAgICAgICAgICAgIyBDb252b2x2ZSBbSW1wdWxzZSwgV2F2ZS9UYWJsZV0KCiMgQmFzaWMgd2F2ZWZvcm1zCgpTaW4g4oaQIFYhKOKIv8OXOk9UKSAgICAgICAgICAgICAjIFNpbmUgd2F2ZSBbSGFybW9uaWNdClNhdyDihpAgViEoVVPil78xw5c6T1UpICAgICAgICAgICMgU2F3IHdhdmUgW0hhcm1vbmljXQpUcmkg4oaQIFYhKFVT4oy1VVPil78xKzAuMjXCr8OXOk9VKSAjIFRyaWFuZ2xlIHdhdmUgW0hhcm1vbmljXQpQdWwg4oaQIFZWIShVU-KJpFVTOlNhdykgICAgICAgICMgUHVsc2Ugd2F2ZSBbSGFybW9uaWMsIER1dHldClNxciDihpAgViEoUHVsOjAuNSkgICAgICAgICAgICMgU3F1YXJlIHdhdmUgW0hhcm1vbmljXQpOc3cg4oaQICjimIcxVVPijaUo4oqC4pqCKVNbXSkgICAgICAgICMgTm9pc2Ugd2F2ZSBbXQpOc3Qg4oaQICjijaUo4oqCTnN3KU9bXSkgICAgICAgICAgIyBOb2lzZSB0YWJsZSBbXQoKIyBVdGlsaXR5CgpQcmludCDihpAgJnBmCkV4cG9ydCDihpAgJmFwIEPima0KVGVzdCDihpAgJmFwIFUzUiBD4pmtCkdyYXBoIOKGkCBJU-KNouKKoig9MeKnuyniiaEo4oeM4qyaMeKKnuKJoOKHoUdI4oGFw5ctMUdIIFNVIFUyQynilr09MOKHoeKMisO3R0wgTwpJbWFnZSDihpAgJmltc-KGr09fUyBTVSBSIEPima0KSW1wb3J0SW1hZ2Ug4oaQIFJJIE_iiaFSSSBT4o2c4o2J4omhKMO3My8r4oaZMynil4zCsGltZyZmcmFiCkltcG9ydFdhdmUg4oaQIFLigoAgUkkgU-KXjOKXjMKwYXVkaW8mZnJhYgpJbXBvcnRUYWJsZSDihpAg4oavT19TIFJJIFTil4zil4zCsGF1ZGlvJmZyYWIgIyBJbXBvcnQgMjU2LXdhdmUgdGFibGUKVGFibGVBdmVyYWdlIOKGkCBS4oKBw7dPLyvihq9PX1MgICAgICAgICAgICAjIFRhYmxlIHRvIHdhdmUgW1RhYmxlXQpUYWJsZUluZGV4IOKGkCBS4oKB4oqh4oanLTFP4oalMOKBhTrihq9PX1M6ICAgICAgICAjIFRhYmxlIHRvIHdhdmUgW0luZGV4ICgwLTI1NSksIFRhYmxlXQpJbWFnZVRvVGFibGUg4oaQIFVTCkdyYXBoQ29udiDihpAgSVMgR3JhcGggQSEo4qyaMMOX4oavUzEpCkdyYXBoU3BlY3RydW0g4oaQIElT4o2i4oqiKD0x4qe7KeKJoSjih4ziip7iiaXDt1NI4oehU0jiio9TVsO3RlbihpnDtzJT4oy1ZmZ0KeKWvT0w4oeh4oyKw7dHTCBPCkdyYXBoU3BlY3RydW1GdWxsIOKGkCBJU-KNouKKoig9MeKnuyniiaEo4oeM4oqe4omlw7dTSOKHoVNIw7dGVuKGmcO3MlPijLVmZnQp4pa9PTDih6HijIrDt0dMIE8KCiMgU2FtcGxlIHdhdmVmb3Jtcy93YXZldGFibGVzCgpXYXZl4oKBIOKGkCBBdmd7U2luMyBBbXAwLjdTaW4yIFNpbjF9CldhdmXigoIg4oaQIEFNMC40U2luNyBBTTAuNVNpbjIgU2luMQpXYXZl4oKCIOKGkCBBdmd7U2F3MSAoUHVsIDIgMC4yKX0KV2F2ZeKChCDihpAgQml0MC43IEZNMC4xU2luNyBTaW4xClRibOKCgSDihpAgRk0oVmFsIDAgMSkgU2luMiBTaW4xClRibOKCgiDihpAgTWF4IEF2Z3tBbXAoVmFsIDAgMSlTaW4yIFNpbjF9ClRibOKCgyDihpAgQml0KFZhbCAwIDEpIENycyhWYWxQSSAwIDEgMikgU2luMSBTaW4zCgpQcmludCAiVGJs4oKDIgpUZXN0IFRibOKCgwpJbWFnZSBUYmzigoMKR3JhcGggVGJs4oKDCkdyYXBoU3BlY3RydW0gVGJs4oKDCg==

# TODO: high/low pass filters
# TODO: IR importing
# TODO: fix Str (stretch) buzzing
# TODO: Sid (side by side)
# TODO: Dwn (downsample)
# TODO: Shp (waveshape)

S ← 2048                    # Samples per oscillator
O ← 256                     # Oscillators per table
T ← ×S O                    # Samples per table
SU ← ÷2+1                   # Signed to unsigned
US ← -1×2                   # Unsigned to signed
OU ← ÷⟜⇡S                   # Oscillator base, unsigned 0-1
OT ← ×τOU                   # Oscillator base, trigonometric 0-2π
R ← ↯¤T                     # Oscillator to table (repeat)
U ← ⍥(⊢⍉↯⊟:⟜÷2⊸⧻)           # Octave up (for playback only)
GH ← 200                    # Graph height
C ← ↥¯1↧1                   # Clamp
SW ← ⊏◿⧻,                   # Select (wrapped)
SI ← +⊙×⟜-⊃⊃(SW⌊)(SW⌈)(-⊸⌊) # Select (interpolated)
RI ← SI×⇡:÷,⧻,              # Resize (interpolated)
CS ← ∿+η                    # Cosine
NR ← ↧⊸↥                    # NaN replace
SC ← NR1÷⟜∿                 # Sinc
VB ← ÷-1⟜⇡O                 # Value base
VR ← +⊙×:-⟜:                # Value range [min, max, values]
GL ← 16                     # GIF length (frames)
SH ← 200                    # Spectrum graph height
FV ← ÷2√S                   # Fourier max value
SV ← +1⌊×÷2Sⁿ2÷⟜⇡÷4S        # Spectrum index values
IS ← ⨬(&gifs15|&ims⊢)=1⧻.   # Show image or gif
R₀ ← ☇0                     # Rerank zero
R₁ ← ☇1                     # Rerank one
V! ← R₁≡^!R₀
VV! ← R₁≡^!R₀⊙R₀
VVA! ← R₁≡^!R₀⊙R₀⊙⊙R₁
VA! ← R₁≡^!R₀⊙R₁
VAA! ← R₁≡^!R₀⊙R₁⊙⊙R₁
A! ← R₁≡^!R₁
AA! ← R₁≡^!R₁⊙R₁

# Convolutions

CId ← ¤¤1 # Identity (delta) convolution

# Table values

Val ← VR⊙⊙(VB)     # Values (linear) [min, max]
ValPI ← VR⊙⊙(ⁿ:VB) # Values (power in) [min, max, base]
ValPO ← ⇌ValPI:    # Values (power out) [min, max, base]

# Helpers

RndInt ← +1⌊×⚂ # Random integer 1-N [Max]

# Operations

Sum ← °□/+⍚R₁                               # Sum [{Waves/Tables}]
Avg ← Sum÷⊸⧻                                # Average [{Waves/Tables}]
Amp ← VA!×                                  # Amplify [Amount, Wave/Table]
Max ← A!(◌⍥⟜÷≠0.↥∩/↥⟜¯.)                    # Maximize [Wave/Table]
Phs ← VA!(↻⁅×⧻,)                            # Phase shift [Amount, Wave/Table]
Zer ← A!(↻⊗/↧⌵..)                           # Phase shift to zero point [Wave/Table]
Bit ← VA!(US↧1÷-1:⌊×,SU:⁅ⁿ:2+1×15-:1√↧1↥0)  # Bitcrush [Amount (0-1), Wave/Table]
Rev ← A!⇌                                   # Reverse [Wave/Table]
Inv ← A!¯                                   # Invert [Wave/Table]
Mir ← A!(⊂⇌.↙÷2S)                           # Mirror 1st half [Wave/Table]
Fad ← VA!(⇌⬚1×⇌:⬚1⟜×÷⧻.⇡⌊÷2×S↥0)            # Fade in & out [Amount (0-1), Wave/Table]
Crs ← VAA!(+⊙(×-:1):⟜×⊙:)                   # Crossfade [Amount (0-1), Start Wave/Table, End Wave/Table]
Str ← VVA!(⊂⊙RI:-:S⟜RI⊙⊃↙↘∩(↧-1S↥1⁅×S↧1↥0)) # Stretch [To (0-1), From (0-1), Wave/Table]
Shc ← VA!(C+)                               # Shift up/down (clip) [Amount (¯1-1), Wave/Table]
Shw ← VA!(⍥(US◿1SU)≠0⟜+)                    # Shift up/down (wrap) [Amount (¯1-1), Wave/Table]
Slc ← VA!(RI S↯♭⁅↥1×S)                      # Slice [Amount (0-1), Wave/Table]
Spd ← VA!(Slcⁿ:2)                           # Speed up/down [Octaves, Wave/Table]
Spl ← VAA!(⊂⊓↙↘⟜:⁅×S↧1↥0)                   # Splice [Amount (0-1), Wave/Table 1, Wave/Table 2]
AM ← VAA!(×+1÷2×-1:)                        # Amplutude modulation [Amplitude, Modulator, Carrier]
RM ← AA!×                                   # Ring modulation [Modulator, Carrier]
FM ← VAA!(SI×S+OU×)                         # Frequency modulation [Amplitude, Modulator, Carrier]
Cnv ← AA!(/+×⍉◫⊙(↯♭):-1⊸+⧻,⧻⟜:)             # Convolve [Impulse, Wave/Table]

# Basic waveforms

Sin ← V!(∿×:OT)             # Sine wave [Harmonic]
Saw ← V!(US◿1×:OU)          # Saw wave [Harmonic]
Tri ← V!(US⌵US◿1+0.25¯×:OU) # Triangle wave [Harmonic]
Pul ← VV!(US≤US:Saw)        # Pulse wave [Harmonic, Duty]
Sqr ← V!(Pul:0.5)           # Square wave [Harmonic]
Nsw ← (☇1US⍥(⊂⚂)S[])        # Noise wave []
Nst ← (⍥(⊂Nsw)O[])          # Noise table []

# Utility

Print ← &pf
Export ← &ap C♭
Test ← &ap U3R C♭
Graph ← IS⍢⊢(=1⧻)≡(⇌⬚1⊞≠⇡GH⁅×-1GH SU U2C)▽=0⇡⌊÷GL O
Image ← &ims↯O_S SU R C♭
ImportImage ← RI O≡RI S⍜⍉≡(÷3/+↙3)◌°img&frab
ImportWave ← R₀ RI S◌◌°audio&frab
ImportTable ← ↯O_S RI T◌◌°audio&frab # Import 256-wave table
TableAverage ← R₁÷O/+↯O_S            # Table to wave [Table]
TableIndex ← R₁⊡↧-1O↥0⁅:↯O_S:        # Table to wave [Index (0-255), Table]
ImageToTable ← US
GraphConv ← IS Graph A!(⬚0×↯S1)
GraphSpectrum ← IS⍢⊢(=1⧻)≡(⇌⊞≥÷SH⇡SH⊏SV÷FV↙÷2S⌵fft)▽=0⇡⌊÷GL O
GraphSpectrumFull ← IS⍢⊢(=1⧻)≡(⇌⊞≥÷SH⇡SH÷FV↙÷2S⌵fft)▽=0⇡⌊÷GL O

# Sample waveforms/wavetables

Wave₁ ← Avg{Sin3 Amp0.7Sin2 Sin1}
Wave₂ ← AM0.4Sin7 AM0.5Sin2 Sin1
Wave₂ ← Avg{Saw1 (Pul 2 0.2)}
Wave₄ ← Bit0.7 FM0.1Sin7 Sin1
Tbl₁ ← FM(Val 0 1) Sin2 Sin1
Tbl₂ ← Max Avg{Amp(Val 0 1)Sin2 Sin1}
Tbl₃ ← Bit(Val 0 1) Crs(ValPI 0 1 2) Sin1 Sin3

Print "Tbl₃"
Test Tbl₃
Image Tbl₃
Graph Tbl₃
GraphSpectrum Tbl₃
