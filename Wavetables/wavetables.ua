# Experimental!
# Uiua 0.13.0-dev.2
# https://github.com/ashtraypettingzoo/uiua-projects/blob/main/Wavetables/wavetables.ua
# https://uiua.org/pad?src=0_13_0-dev_2__IyBFeHBlcmltZW50YWwhCiMgVWl1YSAwLjEzLjAtZGV2LjIKIyBodHRwczovL2dpdGh1Yi5jb20vYXNodHJheXBldHRpbmd6b28vdWl1YS1wcm9qZWN0cy9ibG9iL21haW4vV2F2ZXRhYmxlcy93YXZldGFibGVzLnVhCiMgW3VybF0KCiMgVE9ETzogSVIgaW1wb3J0aW5nCiMgVE9ETzogZml4IFN0ciAoc3RyZXRjaCkgYnV6emluZwojIFRPRE86IFNpZCAoc2lkZSBieSBzaWRlKQojIFRPRE86IFNocCAod2F2ZXNoYXBlKQojIFRPRE86IEVycm9yIGhhbmRsaW5nCgpTIOKGkCAyMDQ4ICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBTYW1wbGVzIHBlciBvc2NpbGxhdG9yCk8g4oaQIDI1NiAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIE9zY2lsbGF0b3JzIHBlciB0YWJsZQpUIOKGkCDDl1MgTyAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgU2FtcGxlcyBwZXIgdGFibGUKU1Ug4oaQIMO3MisxICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFNpZ25lZCB0byB1bnNpZ25lZApVUyDihpAgLTHDlzIgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgVW5zaWduZWQgdG8gc2lnbmVkCk9VIOKGkCDDt-KfnOKHoVMgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgT3NjaWxsYXRvciBiYXNlLCB1bnNpZ25lZCAwLTEKT1Qg4oaQIMOXz4RPVSAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBPc2NpbGxhdG9yIGJhc2UsIHRyaWdvbm9tZXRyaWMgMC0yz4AKUiDihpAg4oavwqRUICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgT3NjaWxsYXRvciB0byB0YWJsZSAocmVwZWF0KQpVIOKGkCDijaUo4oqi4o2J4oav4oqfOuKfnMO3MuKKuOKnuykgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIE9jdGF2ZSB1cCAoZm9yIHBsYXliYWNrIG9ubHkpCkdIIOKGkCAyMDAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIEdyYXBoIGhlaWdodApDIOKGkCDihqXCrzHihqcxICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIENsYW1wClNXIOKGkCDiio_il7_ip7ssICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFNlbGVjdCAod3JhcHBlZCkKU0kg4oaQICviipnDl-KfnC3iioPiioMoU1fijIopKFNX4oyIKSgt4oq44oyKKSAgICAgICAgICAgICAgICAgICAgIyBTZWxlY3QgKGludGVycG9sYXRlZCkKUkkg4oaQIFNJw5fih6E6w7cs4qe7LCAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgUmVzaXplIChpbnRlcnBvbGF0ZWQpCkNTIOKGkCDiiL8rzrcgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIENvc2luZQpOUiDihpAg4oan4oq44oalICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBOYU4gcmVwbGFjZQpTQyDihpAgTlIxw7fin5ziiL8gICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFNpbmMKVkIg4oaQIMO3LTHin5zih6FPICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBWYWx1ZSBiYXNlClZSIOKGkCAr4oqZw5c6LeKfnDogICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgVmFsdWUgcmFuZ2UgW21pbiwgbWF4LCB2YWx1ZXNdCkdMIOKGkCAxNiAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIEdJRiBsZW5ndGggKGZyYW1lcykKU0gg4oaQIDIwMCAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgU3BlY3RydW0gZ3JhcGggaGVpZ2h0CkZWIOKGkCDDtzLiiJpTICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIEZvdXJpZXIgbWF4IHZhbHVlClNWIOKGkCArMeKMisOXw7cyU-KBvzLDt-KfnOKHocO3NFMgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFNwZWN0cnVtIGluZGV4IHZhbHVlcwpJUyDihpAg4qisKCZnaWZzMTV8Jmltcyk9MuKnu-KWsy4gICAgICAgICAgICAgICAgICAgICAgIyBTaG93IGltYWdlIG9yIGdpZgpTTSDihpAg4oeM4oinKOKKgiviirjiioPii4XiiJgow5fiipnii4XiiqIpKeKKmSg6wqTCsOKKgikgICAgICAgICAgICAgICAgICAgIyBTY2FuIGFkZCArIG11bHRpcGx5Ck1CIOKGkCDDtzox4oG_MTYgMiAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIE1pbmltdW0gMTYtYml0IHZhbHVlCkZUIOKGkCDiioMo4oigwrDihIIpKMO3RlbijLUp4oaZKzHDtzJTIGZmdCAgICAgICAgICAgICAgICAgICAgIyBGb3VyaWVyIHRyYW5zZm9ybSBbcGhhc2UsIGFtcCDihpAgd2F2ZV0KRlUg4oaQIOKXjMKw4oSCwrBmZnTiioI64o2cwrDihILCr-KGmDHih4zihpgxLuKEguKKgyjDl-KIvykow5dDUyniipkow5dGVikgICAgICAjIEZvdXJpZXIgdW50cmFuc2Zvcm0gW3dhdmUg4oaQIHBoYXNlLCBhbXBdCkRTIOKGkCB8MSDijaUo4oqCOiIvIik-MOKnuy4gICAgICAgICAgICAgICAgICAgICAgICAgICAjIEFkZCBzbGFzaCB0byBkaXJlY3RvcnkgbmFtZQpDRyDihpAg4o2i4oqiKD0x4qe7KeKJoSjih4zirJox4oqe4omg4oehR0jigYXDly0xR0ggU1UgVTJDKeKWvT0w4oeh4oyKw7dHTCBPICMgQ3JlYXRlIGdyYXBoClNUIOKGkCDiioLihq86QDDihqUwLTriipko4qe7LiQiXyIpICAgICAgICAgICAgICAgICAgICAgICAgIyBOdW0gdG8gc3RyaW5nIHcvIHRyYWlsaW5nIHplcm9lcwpDRSDihpAg4omN4qyaQCDihq_iipk6wqTip7su4oqZ4oeM4oeMICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIENoZWNrIGV4dGVuc2lvbiBbZXh0LCBuYW1lXQpUcnVlIOKGkCAxICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBUcnVlIGNvbnN0YW50CkZhbHNlIOKGkCAwICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIEZhbHNlIGNvbnN0YW50ClLigoAg4oaQIOKYhzAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBSZXJhbmsgemVybwpS4oKBIOKGkCDimIcxICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgUmVyYW5rIG9uZQpWISDihpAgUuKCgeKJoV4hUuKCgApWViEg4oaQIFLigoHiiaFeIVLigoDiiplS4oKAClZWQSEg4oaQIFLigoHiiaFeIVLigoDiiplS4oKA4oqZ4oqZUuKCgQpWQSEg4oaQIFLigoHiiaFeIVLigoDiiplS4oKBClZBQSEg4oaQIFLigoHiiaFeIVLigoDiiplS4oKB4oqZ4oqZUuKCgQpBISDihpAgUuKCgeKJoV4hUuKCgQpBQSEg4oaQIFLigoHiiaFeIVLigoHiiplS4oKBCgojIC0tLS0gSW1wdWxzZXMKIyBJZGVudGl0eSAoZGVsdGEpIGltcHVsc2UKSUlkIOKGkCDCpMKkMQojIFBoYXNlIHNoaWZ0ZWQgZGVsdGEgaW1wdWxzZSBbUGhhc2UgKDAtMSldCklQaHMg4oaQIFYhKOKGuzrirJow4oavwqRTIElJZOKBhcOXUykKIyBMb3ctcGFzcyAoc2luYykgaW1wdWxzZSBbSGFybW9uaWNdCklMUFMg4oaQIFYhKMO3LysuU0PDly3PgE9UKQojIEhpZ2gtcGFzcyAoc2luYykgaW1wdWxzZSBbSGFybW9uaWNdCklIUFMg4oaQIFYhKC06SVBoczAuNUlMUFMpCiMgTG93LXBhc3MgKHdpbmRvd2VkKSBpbXB1bHNlIFtIYXJtb25pYywgV2luZG93XQpJTFBXIOKGkCBWViEoCiAgPjAsT1Q6U0PDly3PgE9UCiAg4qisKCsxw5cwfMOXw5fiioMoPjB8PM-EKeKfnCgrKzAuNDLiipMow5fCrzAuNXzDlzAuMDgp4oipQ1Pin5wow5cyKSkrz4DDly3PgCkKICDDty8rLsOXKQojIEhpZ2gtcGFzcyAod2luZG93ZWQpIGltcHVsc2UgW0hhcm1vbmljLCBXaW5kb3ddCklIUFcg4oaQIFZWISgtOklQaHMwLjVJTFBXKQojIExvdy1wYXNzIChwdWxzZSkgaW1wdWxzZSBbSGFybW9uaWNdCklMUFAg4oaQIFYhKOKGu-KBhcO3MitTOuKsmjDihq_CpFPihq_CpCzDtzoxLuKBheKGp1PDtzpT4oalMcOXMikKIyBIaWdoLXBhc3MgKHB1bHNlKSBpbXB1bHNlIFtIYXJtb25pY10KSUhQUCDihpAgViEoLTpJUGhzMC41SUxQUCkKIyBMb3ctcGFzcyAoZXhwb25lbnRpYWwpIGltcHVsc2UgW0hhcm1vbmljXQpJTFBFIOKGkCBWISjDty8rLuKBvzplwq_Dl09Vw5figpll4oG_MyAxMCkKIyBIaWdoLXBhc3MgKGV4cG9uZW50aWFsKSBpbXB1bHNlIFtIYXJtb25pY10KSUhQRSDihpAgViEoLTpJUGhzMElMUEUpCgojIC0tLS0gVGFibGUgdmFsdWVzCiMgVmFsdWVzIChsaW5lYXIpIFtNaW4sIE1heF0KVmFsIOKGkCBWUuKKmeKKmShWQikKIyBWYWx1ZXMgKHBvd2VyIGluKSBbTWluLCBNYXgsIEJhc2VdClZhbFBJIOKGkCBWUuKKmeKKmSjigb86VkIpCiMgVmFsdWVzIChwb3dlciBvdXQpIFtNaW4sIE1heCwgQmFzZV0KVmFsUE8g4oaQIOKHjFZhbFBJOgoKIyAtLS0tIEhlbHBlcnMKIyBSYW5kb20gKG1pbi1tYXgpIFtNaW4sIE1heF0KUm5kIOKGkCArw5fimoI64p-cLQojIFJhbmRvbSAobWluLW1heCkgW01pbiwgTWF4XQpSbmRJIOKGkCDijIpSbmQ6KzE64o2lOjwsLAoKIyAtLS0tIFdhdmUgT3BlcmF0aW9ucwojIFN1bSBbe1dhdmVzL1RhYmxlc31dClN1bSDihpAgwrDilqEvK-KNmlLigoEKIyBBdmVyYWdlIFt7V2F2ZXMvVGFibGVzfV0KQXZnIOKGkCBTdW3Dt-KKuOKnuwojIEFtcGxpZnkgW0Ftb3VudCwgV2F2ZS9UYWJsZV0KQW1wIOKGkCBWQSHDlwojIE1heGltaXplIFtXYXZlL1RhYmxlXQpNYXgg4oaQIEEhKOKXjOKNpeKfnMO34omgMC7ihqXiiKkv4oal4p-cwq8uKQojIENlbnRlciAoYnkgcGVha3MpIFtXYXZlL1RhYmxlXQpDdHAg4oaQIEEhKC3DtzIrL-KGp-KfnC_ihqUuKQojIENlbnRlciAoYnkgbWVhbikgW1dhdmUvVGFibGVdCkN0bSDihpAgQSEoLcO3Uy8rLikKIyBQaGFzZSBzaGlmdCBbQW1vdW50LCBXYXZlL1RhYmxlXQpQaHMg4oaQIFZBISjihrvigYXDl-KnuywpCiMgUGhhc2Ugc2hpZnQgdG8gemVybyBwb2ludCBbV2F2ZS9UYWJsZV0KWmVyIOKGkCBBISjihrviipcv4oan4oy1Li4pCiMgQml0Y3J1c2ggW0Ftb3VudCAoMC0xKSwgV2F2ZS9UYWJsZV0KQml0IOKGkCBWQSEoVVPihqcxw7ctMTrijIrDlyxTVTrigYXigb86Misxw5cxNS06MeKImuKGpzHihqUwKQojIFJldmVyc2UgW1dhdmUvVGFibGVdClJldiDihpAgQSHih4wKIyBJbnZlcnQgW1dhdmUvVGFibGVdCkludiDihpAgQSHCrwojIE1pcnJvciAxc3QgaGFsZiBbV2F2ZS9UYWJsZV0KTWlyIOKGkCBBISjiioLih4wu4oaZw7cyUykKIyBGYWRlIHN0YXJ0ICYgZW5kIFtBbW91bnQgKDAtMSksIFdhdmUvVGFibGVdCkZhZCDihpAgVkEhKOKHjOKsmjHDl-KHjDrirJox4p-cw5fDt-Knuy7ih6HijIrDtzLDl1PihqUwKQojIENyb3NzZmFkZSBbQW1vdW50ICgwLTEpLCBTdGFydCBXYXZlL1RhYmxlLCBFbmQgV2F2ZS9UYWJsZV0KQ3JzIOKGkCBWQUEhKCviipkow5ctOjEpOuKfnMOX4oqZOikKIyBTdHJldGNoIFtUbyAoMC0xKSwgRnJvbSAoMC0xKSwgV2F2ZS9UYWJsZV0KU3RyIOKGkCBWVkEhKOKKguKKmVJJOi06U-KfnFJJ4oqZ4oqD4oaZ4oaY4oipKOKGpy0xU-KGpTHigYXDl1Pihqcx4oalMCkpCiMgU2hpZnQgdXAvZG93biBbQW1vdW50ICjCrzEtMSksIFdhdmUvVGFibGVdClNoZiDihpAgVkEhKCspCiMgU2xpY2UgW0Ftb3VudCAoMC0xKSwgV2F2ZS9UYWJsZV0KU2xjIOKGkCBWQSEoUkkgU-KGr-KZreKBheKGpTHDl1MpCiMgU3BlZWQgdXAvZG93biBbT2N0YXZlcywgV2F2ZS9UYWJsZV0KU3BkIOKGkCBWQSEoU2xj4oG_OjIpCiMgU3BsaWNlIFtBbW91bnQgKDAtMSksIFdhdmUvVGFibGUgMSwgV2F2ZS9UYWJsZSAyXQpTcGwg4oaQIFZBQSEo4oqC4oqT4oaZ4oaY4p-cOuKBhcOXU-KGpzHihqUwKQojIENsaXAgKGhhcmQpIFtXYXZlL1RhYmxlXQpDbHAg4oaQIEEhKEMpCiMgV3JhcCBbV2F2ZS9UYWJsZV0KV3JwIOKGkCBBIShVU-KXvytNQjFTVSkKIyBDbGlwIChzb2Z0KSBbRXhwb25lbnQgKOKJpTIpLCBXYXZlL1RhYmxlXQpDbHMg4oaQIFZBISjijZzijLUow7ctMeKKmS3in5ziioPigb_DlzopQzrihqUyKQojIFNhdHVyYXRlICh0YW5oKSBbR2FpbiwgV2F2ZS9UYWJsZV0KU2F0IOKGkCBWQSEow7fiioMrLTHigb86ZcOXMsOXKQojIEZ1enogKHRhbmgpIFtHYWluLCBTaGlmdCAowq8xLTEpLCBXYXZlL1RhYmxlXQpGdXog4oaQIFNhdOKKmVNoZgojIFJlY3RpZnkgW1dhdmUvVGFibGVdClJjdCDihpAgQSEo4oy1KQojIEFtcGx1dHVkZSBtb2R1bGF0aW9uIFtBbXBsaXR1ZGUsIE1vZHVsYXRvciwgQ2Fycmllcl0KQU0g4oaQIFZBQSEow5crMcO3MsOXLTE6KQojIFJpbmcgbW9kdWxhdGlvbiBbTW9kdWxhdG9yLCBDYXJyaWVyXQpSTSDihpAgQUEhw5cKIyBGcmVxdWVuY3kgbW9kdWxhdGlvbiBbQW1wbGl0dWRlLCBNb2R1bGF0b3IsIENhcnJpZXJdCkZNIOKGkCBWQUEhKFNJw5dTK09Vw5cpCiMgQ29udm9sdmUgW0ltcHVsc2UsIFdhdmUvVGFibGVdCkNudiDihpAgQUEhKOKXjMKw4oSCwrBmZnTDl-KIqWZmdMOX4oiaU-KsmjDihq_CpFMpCiMgUGhhc2UgcmFuZG9taXplIFtBbW91bnQgKDAtMSksIFdhdmUvVGFibGVdClBociDihpAg4omhKEZVOuKKmSs6RlQpOuKJocOXUuKCgS3PgMOXz4TijaUo4oqC4pqCKSsxw7cyU1tdUuKCgOKKmVLigoEKIyBIYXJtb25pYyB3YXZlIGZpbHRlciBbQW1vdW50ICgwLTEpLCBGaWx0ZXIgV2F2ZS9UYWJsZSwgU291cmNlIFdhdmUvVGFibGVdCkh3ZiDihpAgVkFBIShGVeKKmcOXOisxw5fiipkoLTHil4ziiKlGVCkpCiMgRG93bnNhbXBsZSBbQW1vdW50ICgwLTEpLCBXYXZlL1RhYmxlXQpEd24g4oaQIFZBISgKICDijIjDtzpTLuKBheKGpTHigb86Misxw5ctMeKCmTJTLToxCiAg4oavwqRT4o2J4oav4p-cw7fin5wo4omhLyvihq_iioI6KQopCiMgTG93LXBhc3MgKHNpbmMpIFtIYXJtb25pYywgV2F2ZS9UYWJsZV0KTFBTIOKGkCBQaHMwLjVDbnYgSUxQUwojIEhpZ2gtcGFzcyAoc2luYykgW0hhcm1vbmljLCBXYXZlL1RhYmxlXQpIUFMg4oaQIFBoczAuNUNudiBJSFBTCiMgTG93LXBhc3MgKHdpbmRvd2VkKSBbSGFybW9uaWMsIFdpbmRvdywgV2F2ZS9UYWJsZV0KTFBXIOKGkCBQaHMwLjVDbnYgSUxQVwojIEhpZ2gtcGFzcyAod2luZG93ZWQpIFtIYXJtb25pYywgV2luZG93LCBXYXZlL1RhYmxlXQpIUFcg4oaQIFBoczAuNUNudiBJSFBXCiMgTG93LXBhc3MgKHB1bHNlKSBbSGFybW9uaWMsIFdhdmUvVGFibGVdCkxQUCDihpAgUGhzMC41Q252IElMUFAKIyBIaWdoLXBhc3MgKHB1bHNlKSBbSGFybW9uaWMsIFdhdmUvVGFibGVdCkhQUCDihpAgUGhzMC41Q252IElIUFAKIyBMb3ctcGFzcyAoZXhwb25lbnRpYWwpIFtIYXJtb25pYywgV2F2ZS9UYWJsZV0KTFBFIOKGkCBDbnYgSUxQRQojIEhpZ2gtcGFzcyAoZXhwb25lbnRpYWwpIFtIYXJtb25pYywgV2F2ZS9UYWJsZV0KSFBFIOKGkCBDbnYgSUhQRQojIExvdy1wYXNzIChkZWZhdWx0KSBbSGFybW9uaWMsIFdhdmUvVGFibGVdCkxQIOKGkCBMUFcuCiMgTG93LXBhc3MgKGRlZmF1bHQpIFtIYXJtb25pYywgV2F2ZS9UYWJsZV0KSFAg4oaQIEhQVy4KCiMgLS0tLSBUYWJsZSBvcGVyYXRpb25zCiMgQ29tYiBmaWx0ZXIgKGZlZWRiYWNrKSBbQW1wbGl0dWRlLCBIYXJtb25pYywgV2F2ZS9UYWJsZV0KVENtYiDihpAg4oavT19TIFNN4oqZKOKsmjDihq_iioLijIjDtzpULuKBhcO3OlPiiplSKQojIENvbWIgZmlsdGVyIChmZWVkZm9yd2FyZCkgW0FtcGxpdHVkZSwgSGFybW9uaWMsIFdhdmUvVGFibGVdClRDbWYg4oaQIOKGr09fUyvDl-KKmSjiioLihq86WzBdwqTin5zihpjijIrDtzrCr1PiipkoLlIpKQojIE1heGltaXplIHRhYmxlIFtXYXZlL1RhYmxlXQpUTWF4IOKGkCDihq9PX1Pil4zijaXin5zDt-KJoDAu4oal4oipL-KGpeKfnMKvLlIKIyBDZW50ZXIgdGFibGUgKGJ5IHBlYWtzKSBbV2F2ZS9UYWJsZV0KVEN0cCDihpAg4oavT19TLcO3Misv4oan4p-cL-KGpS5SCiMgQ2VudGVyIHRhYmxlIChieSBtZWFuKSBbV2F2ZS9UYWJsZV0KVEN0bSDihpAg4oavT19TLcO3VC8rLlIKCiMgLS0tLSBXYXZlZm9ybXMKIyBTaW5lIHdhdmUgW0hhcm1vbmljXQpTaW4g4oaQIFYhKOKIv8OXOk9UKQojIFNhdyB3YXZlIFtIYXJtb25pY10KU2F3IOKGkCBWIShVU-KXvzHDlzpPVSkKIyBUcmlhbmdsZSB3YXZlIFtIYXJtb25pY10KVHJpIOKGkCBWIShVU-KMtVVT4pe_MSswLjI1wq_DlzpPVSkKIyBQdWxzZSB3YXZlIFtIYXJtb25pYywgRHV0eV0KUHVsIOKGkCBWViEoVVPiiaRVUzpTYXcpCiMgU3F1YXJlIHdhdmUgW0hhcm1vbmljXQpTcXIg4oaQIFYhKFB1bDowLjUpCiMgTm9pc2Ugd2F2ZSBbXQpOc3cg4oaQICjimIcxVVPijaUo4oqC4pqCKVNbXSkKIyBTaWxlbnQgKGVtcHR5KSB3YXZlClNpbCDihpAgUuKCgeKsmjDihq_CpFNbXQoKIyAtLS0tIFRhYmxlcwojIE5vaXNlIHRhYmxlIFtdCk5zdCDihpAgKOKNpSjiioJOc3cpT1tdKQoKIyAtLS0tIFNldHRpbmdzCkRlc2t0b3Ag4oaQIEZhbHNlCkluRGlyQXVkaW8g4oaQICJpbiIKSW5EaXJJbWFnZSDihpAgImluIgpPdXREaXJBdWRpbyDihpAgIm91dCIKT3V0RGlySW1hZ2Ug4oaQICJvdXQiCk91dERpckdyYXBoIOKGkCAib3V0IgpPdXREaXJTcGVjdHJ1bSDihpAgIm91dCIKT3V0U3VmZml4SW1hZ2Ug4oaQICIiCk91dFN1ZmZpeEdyYXBoIOKGkCAiZyIKT3V0U3VmZml4U3BlY3RydW0g4oaQICJzIgoKIyAtLS0tIFV0aWxpdHkKIyBQcmludCBzdHJpbmcgW1N0cmluZ10KUHJpbnQg4oaQICZwZgojIFBsYXkgd2F2ZSBvciB0YWJsZSBbV2F2ZS9UYWJsZV0KRXhwb3J0IOKGkCAmYXAgQ-KZrQojIFBsYXkgdGVzdCB0b25lIG9mIHdhdmUgb3IgdGFibGUgW1dhdmUvVGFibGVdClRlc3Qg4oaQICZhcCBVM1IgQ-KZrQojIERpc3BsYXkgZ3JhcGggW1dhdmUvVGFibGVdCkdyYXBoIOKGkCBJUyBDRwojIERpc3BsYXkgaW1hZ2UgW1dhdmUvVGFibGVdCkltYWdlIOKGkCAmaW1z4oavT19TIFNVIFIgQ-KZrQojIFJlYWQgaW1hZ2UgZnJvbSBmaWxlIFtGaWxlbmFtZV0KSW1wb3J0SW1hZ2Ug4oaQIFJJIE_iiaFSSSBT4o2c4o2J4omhKMO3My8r4oaZMynil4zCsGltZyZmcmFi4o2lKOKKgkRTIEluRGlySW1hZ2UpRGVza3RvcAojIFJlYWQgd2F2ZSBmcm9tIGZpbGUgW0ZpbGVuYW1lXQpJbXBvcnRXYXZlIOKGkCBS4oKBIFJJIFPil4zil4zCsGF1ZGlvJmZyYWLijaUo4oqCRFMgSW5EaXJBdWRpbylEZXNrdG9wCiMgUmVhZCAyNTYtd2F2ZSB0YWJsZSBmcm9tIGZpbGUgW0ZpbGVuYW1lXQpJbXBvcnRUYWJsZSDihpAg4oavT19TIFJJIFTil4zil4zCsGF1ZGlvJmZyYWLijaUo4oqCRFMgSW5EaXJBdWRpbylEZXNrdG9wCiMgVGFibGUgdG8gd2F2ZSBbVGFibGVdClRhYmxlQXZlcmFnZSDihpAgUuKCgcO3Ty8r4oavT19TCiMgVGFibGUgdG8gd2F2ZSBbSW5kZXggKDAtMjU1KSwgVGFibGVdClRhYmxlSW5kZXgg4oaQIFLigoHiiqHihqctMU_ihqUw4oGFOuKGr09fUzoKIyBDb252ZXJ0IGltYWdlIHRvIHRhYmxlIFtJbWFnZV0KSW1hZ2VUb1RhYmxlIOKGkCBVUwojIERpc3BsYXkgZ3JhcGggb2YgaW1wdWxzZSBbV2F2ZS9UYWJsZV0KR3JhcGhJbXB1bHNlIOKGkCBHcmFwaCBBISjirJoww5fihq9TMSkKIyBEaXNwbGF5IHNwZWN0cnVtIGdyYXBoIFtXYXZlL1RhYmxlXQpHcmFwaFNwZWN0cnVtIOKGkCBJU-KNouKKoig9MeKnuyniiaEo4oeM4oqe4omlw7dTSOKHoVNI4oqPU1bDt0ZW4oaZw7cyU-KMtWZmdCnilr09MOKHoeKMisO3R0wgTwojIERpc3BsYXkgZnVsbCBzcGVjdHJ1bSBncmFwaCBbV2F2ZS9UYWJsZV0KR3JhcGhTcGVjdHJ1bUZ1bGwg4oaQIElT4o2i4oqiKD0x4qe7KeKJoSjih4ziip7iiaXDt1NI4oehU0jDt0ZW4oy1ZmZ0KeKWvT0w4oeh4oyKw7dHTCBPCiMgU2F2ZSB3YXZlIG9yIHRhYmxlIHRvIGZpbGUgW0ZpbGVuYW1lLCBXYXZlL1RhYmxlXQpXcml0ZUF1ZGlvIOKGkCAmZndh4o2lKOKKgkRTJmZtZC5PdXREaXJBdWRpbylEZXNrdG9w4oqC4oqZKCIud2F2ImF1ZGlvIndhdiImYXNyQ-KZrSkKIyBTYXZlIGltYWdlIHRvIGZpbGUgW0ZpbGVuYW1lLCBXYXZlL1RhYmxlXQpXcml0ZUltYWdlIOKGkCAoCiAg4oqC4oqZKOKKgk91dFN1ZmZpeEltYWdlIi5wbmciaW1nInBuZyLiiLVbLi46MV3ihq9PX1MgU1UgUiBD4pmtKQogICZmd2HijaUo4oqCRFMmZm1kLk91dERpckltYWdlKURlc2t0b3ApCiMgU2F2ZSBncmFwaCB0byBmaWxlIFtGaWxlbmFtZSwgV2F2ZS9UYWJsZV0KV3JpdGVHcmFwaCDihpAgKAogIOKNpSjiioJEUyZmbWQuT3V0RGlyR3JhcGgpRGVza3RvcOKKgjpPdXRTdWZmaXhHcmFwaOKKmSg-M-Knu-KWsy7iiLVbLi46MV1DRykKICAmZndh4qisKOKKgjoiLnBuZyLiipkoaW1nInBuZyIpfOKKgjoiLmdpZiLiipkoZ2lmMTYpKToKKQojIFNhdmUgc3BlY3RydW0gZ3JhcGggdG8gZmlsZSBbRmlsZW5hbWUsIFdhdmUvVGFibGVdCldyaXRlU3BlY3RydW0g4oaQICgKICDiipko4o2i4oqiKD0x4qe7KeKJoSjih4ziip7iiaXDt1NI4oehU0jiio9TVsO3RlbihpnDtzJT4oy1ZmZ0KeKWvT0w4oeh4oyKw7dHTCBPKQogIOKNpSjiioJEUyZmbWQuT3V0RGlyU3BlY3RydW0pRGVza3RvcOKKgjpPdXRTdWZmaXhTcGVjdHJ1beKKmSg-M-Knu-KWsy7iiLVbLi46MV0pCiAgJmZ3YeKorCjiioI6Ii5wbmci4oqZKGltZyJwbmciKXziioI6Ii5naWYi4oqZKGdpZjE2KSk6CikKIyBTYXZlIGZ1bGwgc3BlY3RydW0gZ3JhcGggdG8gZmlsZSBbRmlsZW5hbWUsIFdhdmUvVGFibGVdCldyaXRlU3BlY3RydW1GdWxsIOKGkCAoCiAg4oqZKOKNouKKoig9MeKnuyniiaEo4oeM4oqe4omlw7dTSOKHoVNIw7dGVuKMtWZmdCnilr09MOKHoeKMisO3R0wgTykKICDijaUo4oqCRFMmZm1kLk91dERpclNwZWN0cnVtKURlc2t0b3DiioI6T3V0U3VmZml4U3BlY3RydW3iipkoPjPip7vilrMu4oi1Wy4uOjFdKQogICZmd2HiqKwo4oqCOiIucG5nIuKKmShpbWcicG5nIil84oqCOiIuZ2lmIuKKmShnaWYxNikpOgopCiMgR2VuZXJhdGVzIHVudXNlZCBuYW1lIGZvciBvdXRwdXQKTmV3TmFtZSDihpAgKAogIOKorCjijZomZmxkey4uLi4uIi4vIn0KICB8IHtPdXREaXJBdWRpbyBPdXREaXJJbWFnZS5PdXREaXJHcmFwaC5PdXREaXJTcGVjdHJ1bX0KICAgIOKNmijijZrihpjip7s64o2lKOKWveKItSZmaWYuKT4x4qe7LiZmbGQu4oqCIi4vIkRTJmZtZC4pCiAgKURlc2t0b3AKICDijaIoKzEKICB8IHviioI6Ii53YXYi4p-cKOKKguKKgjpPdXRTdWZmaXhJbWFnZToiLnBuZyIpCiAgICAg4p-cKOKKguKKgjpPdXRTdWZmaXhHcmFwaDoiLmdpZiIp4p-cKOKKguKKgjpPdXRTdWZmaXhHcmFwaDoiLnBuZyIpCiAgICAg4p-cKOKKguKKgjpPdXRTdWZmaXhTcGVjdHJ1bToiLmdpZiIp4p-cKOKKguKKgjpPdXRTdWZmaXhTcGVjdHJ1bToiLnBuZyIpfVNUNSwsCiAgICAv4oal4omhKOKIiMKw4pahOikKICApIDAKICBTVDXiipnil4wKKQojIEdldHMgbmFtZSBvZiByYW5kb20gLndhdiBmaWxlIFtTdWJmb2xkZXJdClJhbmRvbUF1ZGlvRmlsZSDihpAgKAogIOKorCgmZmxkIi4vInzijZrihpjip7s64pa94oi1JmZpZi4mZmxkLuKKgiIuLyJEUyZmbWQu4oqCSW5EaXJBdWRpbylEZXNrdG9wCiAg4pa94omhKENFIi53YXYiwrDilqEpLgogIOKorCgiIuKXjHzCsOKWoeKKj-KMisOX4pqC4qe7Lik-MOKnuy4KKQojIEdldHMgbmFtZSBvZiByYW5kb20gLnBuZyBmaWxlIFtTdWJmb2xkZXJdClJhbmRvbUltYWdlRmlsZSDihpAgKAogIOKorCgmZmxkIi4vInzijZrihpjip7s64pa94oi1JmZpZi4mZmxkLuKKgiIuLyJEUyZmbWQu4oqCSW5EaXJJbWFnZSlEZXNrdG9wCiAg4pa94omhKENFIi5wbmciwrDilqEpLgogIOKorCgiIuKXjHzCsOKWoeKKj-KMisOX4pqC4qe7Lik-MOKnuy4KKQojIFJlcGVhdCBmdW5jdGlvbiBbRnVuY3Rpb24sIE51bV0KUmVwZWF0ISDihpAg4o2lXiEKIyBDYWxsIHJhbmRvbSBmdW5jdGlvbiAoMiBjaG9pY2VzKQpEb1JhbmRvbeKAvCDihpAg4qisXiFeIeKMisOXMuKaggojIENhbGwgcmFuZG9tIGZ1bmN0aW9uICgzIGNob2ljZXMpCkRvUmFuZG9t4oC8ISDihpAg4qisKF4hfF4hfF4hKeKMisOXM-KaggojIENhbGwgcmFuZG9tIGZ1bmN0aW9uICg0IGNob2ljZXMpCkRvUmFuZG9t4oC84oC8IOKGkCDiqKwoXiF8XiF8XiF8XiEp4oyKw5c04pqCCiMgRXhpdCAodXNlIGF0IGVuZCBvZiBzY3JpcHQpCkV4aXQg4oaQIOKNpSgmZXhpdDApRGVza3RvcAoKIyAtLS0tIFNhbXBsZSB3YXZlZm9ybXMvd2F2ZXRhYmxlcwpXYXZl4oKBIOKGkCBBdmd7U2luMyBBbXAwLjdTaW4yIFNpbjF9CldhdmXigoIg4oaQIEFNMC40U2luNyBBTTAuNVNpbjIgU2luMQpXYXZl4oKCIOKGkCBBdmd7U2F3MSAoUHVsIDIgMC4yKX0KV2F2ZeKChCDihpAgQml0MC43IEZNMC4xU2luNyBTaW4xClRibOKCgSDihpAgRk0oVmFsIDAgMSkgU2luMiBTaW4xClRibOKCgiDihpAgTWF4IEF2Z3tBbXAoVmFsIDAgMSlTaW4yIFNpbjF9ClRibOKCgyDihpAgQml0KFZhbCAwIDEpIENycyhWYWxQSSAwIDEgMikgU2luMSBTaW4zCgpQcmludCAiVGJs4oKDIgpUZXN0IFRibOKCgwpJbWFnZSBUYmzigoMKR3JhcGggVGJs4oKDCkdyYXBoU3BlY3RydW0gVGJs4oKDCgpFeGl0Cg==

# TODO: IR importing
# TODO: fix Str (stretch) buzzing
# TODO: Sid (side by side)
# TODO: Shp (waveshape)
# TODO: Error handling

S ← 2048                                       # Samples per oscillator
O ← 256                                        # Oscillators per table
T ← ×S O                                       # Samples per table
SU ← ÷2+1                                      # Signed to unsigned
US ← -1×2                                      # Unsigned to signed
OU ← ÷⟜⇡S                                      # Oscillator base, unsigned 0-1
OT ← ×τOU                                      # Oscillator base, trigonometric 0-2π
R ← ↯¤T                                        # Oscillator to table (repeat)
U ← ⍥(⊢⍉↯⊟:⟜÷2⊸⧻)                              # Octave up (for playback only)
GH ← 200                                       # Graph height
C ← ↥¯1↧1                                      # Clamp
SW ← ⊏◿⧻,                                      # Select (wrapped)
SI ← +⊙×⟜-⊃⊃(SW⌊)(SW⌈)(-⊸⌊)                    # Select (interpolated)
RI ← SI×⇡:÷,⧻,                                 # Resize (interpolated)
CS ← ∿+η                                       # Cosine
NR ← ↧⊸↥                                       # NaN replace
SC ← NR1÷⟜∿                                    # Sinc
VB ← ÷-1⟜⇡O                                    # Value base
VR ← +⊙×:-⟜:                                   # Value range [min, max, values]
GL ← 16                                        # GIF length (frames)
SH ← 200                                       # Spectrum graph height
FV ← ÷2√S                                      # Fourier max value
SV ← +1⌊×÷2Sⁿ2÷⟜⇡÷4S                           # Spectrum index values
IS ← ⨬(&gifs15|&ims)=2⧻△.                      # Show image or gif
SM ← ⇌∧(⊂+⊸⊃⋅∘(×⊙⋅⊢))⊙(:¤°⊂)                   # Scan add + multiply
MB ← ÷:1ⁿ16 2                                  # Minimum 16-bit value
FT ← ⊃(∠°ℂ)(÷FV⌵)↙+1÷2S fft                    # Fourier transform [phase, amp ← wave]
FU ← ◌°ℂ°fft⊂:⍜°ℂ¯↘1⇌↘1.ℂ⊃(×∿)(×CS)⊙(×FV)      # Fourier untransform [wave ← phase, amp]
DS ← |1 ⍥(⊂:"/")>0⧻.                           # Add slash to directory name
CG ← ⍢⊢(=1⧻)≡(⇌⬚1⊞≠⇡GH⁅×-1GH SU U2C)▽=0⇡⌊÷GL O # Create graph
ST ← ⊂↯:@0↥0-:⊙(⧻.$"_")                        # Num to string w/ trailing zeroes
CE ← ≍⬚@ ↯⊙:¤⧻.⊙⇌⇌                             # Check extension [ext, name]
True ← 1                                       # True constant
False ← 0                                      # False constant
R₀ ← ☇0                                        # Rerank zero
R₁ ← ☇1                                        # Rerank one
V! ← R₁≡^!R₀
VV! ← R₁≡^!R₀⊙R₀
VVA! ← R₁≡^!R₀⊙R₀⊙⊙R₁
VA! ← R₁≡^!R₀⊙R₁
VAA! ← R₁≡^!R₀⊙R₁⊙⊙R₁
A! ← R₁≡^!R₁
AA! ← R₁≡^!R₁⊙R₁

# ---- Impulses
# Identity (delta) impulse
IId ← ¤¤1
# Phase shifted delta impulse [Phase (0-1)]
IPhs ← V!(↻:⬚0↯¤S IId⁅×S)
# Low-pass (sinc) impulse [Harmonic]
ILPS ← V!(÷/+.SC×-πOT)
# High-pass (sinc) impulse [Harmonic]
IHPS ← V!(-:IPhs0.5ILPS)
# Low-pass (windowed) impulse [Harmonic, Window]
ILPW ← VV!(
  >0,OT:SC×-πOT
  ⨬(+1×0|××⊃(>0|<τ)⟜(++0.42⊓(×¯0.5|×0.08)∩CS⟜(×2))+π×-π)
  ÷/+.×)
# High-pass (windowed) impulse [Harmonic, Window]
IHPW ← VV!(-:IPhs0.5ILPW)
# Low-pass (pulse) impulse [Harmonic]
ILPP ← V!(↻⁅÷2+S:⬚0↯¤S↯¤,÷:1.⁅↧S÷:S↥1×2)
# High-pass (pulse) impulse [Harmonic]
IHPP ← V!(-:IPhs0.5ILPP)
# Low-pass (exponential) impulse [Harmonic]
ILPE ← V!(÷/+.ⁿ:e¯×OU×ₙeⁿ3 10)
# High-pass (exponential) impulse [Harmonic]
IHPE ← V!(-:IPhs0ILPE)

# ---- Table values
# Values (linear) [Min, Max]
Val ← VR⊙⊙(VB)
# Values (power in) [Min, Max, Base]
ValPI ← VR⊙⊙(ⁿ:VB)
# Values (power out) [Min, Max, Base]
ValPO ← ⇌ValPI:

# ---- Helpers
# Random (min-max) [Min, Max]
Rnd ← +×⚂:⟜-
# Random (min-max) [Min, Max]
RndI ← ⌊Rnd:+1:⍥:<,,

# ---- Wave Operations
# Sum [{Waves/Tables}]
Sum ← °□/+⍚R₁
# Average [{Waves/Tables}]
Avg ← Sum÷⊸⧻
# Amplify [Amount, Wave/Table]
Amp ← VA!×
# Maximize [Wave/Table]
Max ← A!(◌⍥⟜÷≠0.↥∩/↥⟜¯.)
# Center (by peaks) [Wave/Table]
Ctp ← A!(-÷2+/↧⟜/↥.)
# Center (by mean) [Wave/Table]
Ctm ← A!(-÷S/+.)
# Phase shift [Amount, Wave/Table]
Phs ← VA!(↻⁅×⧻,)
# Phase shift to zero point [Wave/Table]
Zer ← A!(↻⊗/↧⌵..)
# Bitcrush [Amount (0-1), Wave/Table]
Bit ← VA!(US↧1÷-1:⌊×,SU:⁅ⁿ:2+1×15-:1√↧1↥0)
# Reverse [Wave/Table]
Rev ← A!⇌
# Invert [Wave/Table]
Inv ← A!¯
# Mirror 1st half [Wave/Table]
Mir ← A!(⊂⇌.↙÷2S)
# Fade start & end [Amount (0-1), Wave/Table]
Fad ← VA!(⇌⬚1×⇌:⬚1⟜×÷⧻.⇡⌊÷2×S↥0)
# Crossfade [Amount (0-1), Start Wave/Table, End Wave/Table]
Crs ← VAA!(+⊙(×-:1):⟜×⊙:)
# Stretch [To (0-1), From (0-1), Wave/Table]
Str ← VVA!(⊂⊙RI:-:S⟜RI⊙⊃↙↘∩(↧-1S↥1⁅×S↧1↥0))
# Shift up/down [Amount (¯1-1), Wave/Table]
Shf ← VA!(+)
# Slice [Amount (0-1), Wave/Table]
Slc ← VA!(RI S↯♭⁅↥1×S)
# Speed up/down [Octaves, Wave/Table]
Spd ← VA!(Slcⁿ:2)
# Splice [Amount (0-1), Wave/Table 1, Wave/Table 2]
Spl ← VAA!(⊂⊓↙↘⟜:⁅×S↧1↥0)
# Clip (hard) [Wave/Table]
Clp ← A!(C)
# Wrap [Wave/Table]
Wrp ← A!(US◿+MB1SU)
# Clip (soft) [Exponent (≥2), Wave/Table]
Cls ← VA!(⍜⌵(÷-1⊙-⟜⊃ⁿ×:)C:↥2)
# Saturate (tanh) [Gain, Wave/Table]
Sat ← VA!(÷⊃+-1ⁿ:e×2×)
# Fuzz (tanh) [Gain, Shift (¯1-1), Wave/Table]
Fuz ← Sat⊙Shf
# Rectify [Wave/Table]
Rct ← A!(⌵)
# Amplutude modulation [Amplitude, Modulator, Carrier]
AM ← VAA!(×+1÷2×-1:)
# Ring modulation [Modulator, Carrier]
RM ← AA!×
# Frequency modulation [Amplitude, Modulator, Carrier]
FM ← VAA!(SI×S+OU×)
# Convolve [Impulse, Wave/Table]
Cnv ← AA!(◌°ℂ°fft×∩fft×√S⬚0↯¤S)
# Phase randomize [Amount (0-1), Wave/Table]
Phr ← ≡(FU:⊙+:FT):≡×R₁-π×τ⍥(⊂⚂)+1÷2S[]R₀⊙R₁
# Harmonic wave filter [Amount (0-1), Filter Wave/Table, Source Wave/Table]
Hwf ← VAA!(FU⊙×:+1×⊙(-1◌∩FT))
# Downsample [Amount (0-1), Wave/Table]
Dwn ← VA!(
  ⌈÷:S.⁅↥1ⁿ:2+1×-1ₙ2S-:1
  ↯¤S⍉↯⟜÷⟜(≡/+↯⊂:)
)
# Low-pass (sinc) [Harmonic, Wave/Table]
LPS ← Phs0.5Cnv ILPS
# High-pass (sinc) [Harmonic, Wave/Table]
HPS ← Phs0.5Cnv IHPS
# Low-pass (windowed) [Harmonic, Window, Wave/Table]
LPW ← Phs0.5Cnv ILPW
# High-pass (windowed) [Harmonic, Window, Wave/Table]
HPW ← Phs0.5Cnv IHPW
# Low-pass (pulse) [Harmonic, Wave/Table]
LPP ← Phs0.5Cnv ILPP
# High-pass (pulse) [Harmonic, Wave/Table]
HPP ← Phs0.5Cnv IHPP
# Low-pass (exponential) [Harmonic, Wave/Table]
LPE ← Cnv ILPE
# High-pass (exponential) [Harmonic, Wave/Table]
HPE ← Cnv IHPE
# Low-pass (default) [Harmonic, Wave/Table]
LP ← LPW.
# Low-pass (default) [Harmonic, Wave/Table]
HP ← HPW.

# ---- Table operations
# Comb filter (feedback) [Amplitude, Harmonic, Wave/Table]
TCmb ← ↯O_S SM⊙(⬚0↯⊂⌈÷:T.⁅÷:S⊙R)
# Comb filter (feedforward) [Amplitude, Harmonic, Wave/Table]
TCmf ← ↯O_S+×⊙(⊂↯:[0]¤⟜↘⌊÷:¯S⊙(.R))
# Maximize table [Wave/Table]
TMax ← ↯O_S◌⍥⟜÷≠0.↥∩/↥⟜¯.R
# Center table (by peaks) [Wave/Table]
TCtp ← ↯O_S-÷2+/↧⟜/↥.R
# Center table (by mean) [Wave/Table]
TCtm ← ↯O_S-÷T/+.R

# ---- Waveforms
# Sine wave [Harmonic]
Sin ← V!(∿×:OT)
# Saw wave [Harmonic]
Saw ← V!(US◿1×:OU)
# Triangle wave [Harmonic]
Tri ← V!(US⌵US◿1+0.25¯×:OU)
# Pulse wave [Harmonic, Duty]
Pul ← VV!(US≤US:Saw)
# Square wave [Harmonic]
Sqr ← V!(Pul:0.5)
# Noise wave []
Nsw ← (☇1US⍥(⊂⚂)S[])
# Silent (empty) wave
Sil ← R₁⬚0↯¤S[]

# ---- Tables
# Noise table []
Nst ← (⍥(⊂Nsw)O[])

# ---- Settings
Desktop ← False
InDirAudio ← "in"
InDirImage ← "in"
OutDirAudio ← "out"
OutDirImage ← "out"
OutDirGraph ← "out"
OutDirSpectrum ← "out"
OutSuffixImage ← ""
OutSuffixGraph ← "g"
OutSuffixSpectrum ← "s"

# ---- Utility
# Print string [String]
Print ← &pf
# Play wave or table [Wave/Table]
Export ← &ap C♭
# Play test tone of wave or table [Wave/Table]
Test ← &ap U3R C♭
# Display graph [Wave/Table]
Graph ← IS CG
# Display image [Wave/Table]
Image ← &ims↯O_S SU R C♭
# Read image from file [Filename]
ImportImage ← RI O≡RI S⍜⍉≡(÷3/+↙3)◌°img&frab⍥(⊂DS InDirImage)Desktop
# Read wave from file [Filename]
ImportWave ← R₁ RI S◌◌°audio&frab⍥(⊂DS InDirAudio)Desktop
# Read 256-wave table from file [Filename]
ImportTable ← ↯O_S RI T◌◌°audio&frab⍥(⊂DS InDirAudio)Desktop
# Table to wave [Table]
TableAverage ← R₁÷O/+↯O_S
# Table to wave [Index (0-255), Table]
TableIndex ← R₁⊡↧-1O↥0⁅:↯O_S:
# Convert image to table [Image]
ImageToTable ← US
# Display graph of impulse [Wave/Table]
GraphImpulse ← Graph A!(⬚0×↯S1)
# Display spectrum graph [Wave/Table]
GraphSpectrum ← IS⍢⊢(=1⧻)≡(⇌⊞≥÷SH⇡SH⊏SV÷FV↙÷2S⌵fft)▽=0⇡⌊÷GL O
# Display full spectrum graph [Wave/Table]
GraphSpectrumFull ← IS⍢⊢(=1⧻)≡(⇌⊞≥÷SH⇡SH÷FV⌵fft)▽=0⇡⌊÷GL O
# Save wave or table to file [Filename, Wave/Table]
WriteAudio ← &fwa⍥(⊂DS&fmd.OutDirAudio)Desktop⊂⊙(".wav"audio"wav"&asrC♭)
# Save image to file [Filename, Wave/Table]
WriteImage ← (
  ⊂⊙(⊂OutSuffixImage".png"img"png"∵[..:1]↯O_S SU R C♭)
  &fwa⍥(⊂DS&fmd.OutDirImage)Desktop)
# Save graph to file [Filename, Wave/Table]
WriteGraph ← (
  ⍥(⊂DS&fmd.OutDirGraph)Desktop⊂:OutSuffixGraph⊙(>3⧻△.∵[..:1]CG)
  &fwa⨬(⊂:".png"⊙(img"png")|⊂:".gif"⊙(gif16)):
)
# Save spectrum graph to file [Filename, Wave/Table]
WriteSpectrum ← (
  ⊙(⍢⊢(=1⧻)≡(⇌⊞≥÷SH⇡SH⊏SV÷FV↙÷2S⌵fft)▽=0⇡⌊÷GL O)
  ⍥(⊂DS&fmd.OutDirSpectrum)Desktop⊂:OutSuffixSpectrum⊙(>3⧻△.∵[..:1])
  &fwa⨬(⊂:".png"⊙(img"png")|⊂:".gif"⊙(gif16)):
)
# Save full spectrum graph to file [Filename, Wave/Table]
WriteSpectrumFull ← (
  ⊙(⍢⊢(=1⧻)≡(⇌⊞≥÷SH⇡SH÷FV⌵fft)▽=0⇡⌊÷GL O)
  ⍥(⊂DS&fmd.OutDirSpectrum)Desktop⊂:OutSuffixSpectrum⊙(>3⧻△.∵[..:1])
  &fwa⨬(⊂:".png"⊙(img"png")|⊂:".gif"⊙(gif16)):
)
# Generates unused name for output
NewName ← (
  ⨬(⍚&fld{....."./"}
  | {OutDirAudio OutDirImage.OutDirGraph.OutDirSpectrum}
    ⍚(⍚↘⧻:⍥(▽∵&fif.)>1⧻.&fld.⊂"./"DS&fmd.)
  )Desktop
  ⍢(+1
  | {⊂:".wav"⟜(⊂⊂:OutSuffixImage:".png")
     ⟜(⊂⊂:OutSuffixGraph:".gif")⟜(⊂⊂:OutSuffixGraph:".png")
     ⟜(⊂⊂:OutSuffixSpectrum:".gif")⟜(⊂⊂:OutSuffixSpectrum:".png")}ST5,,
    /↥≡(∈°□:)
  ) 0
  ST5⊙◌
)
# Gets name of random .wav file [Subfolder]
RandomAudioFile ← (
  ⨬(&fld"./"|⍚↘⧻:▽∵&fif.&fld.⊂"./"DS&fmd.⊂InDirAudio)Desktop
  ▽≡(CE".wav"°□).
  ⨬(""◌|°□⊏⌊×⚂⧻.)>0⧻.
)
# Gets name of random .png file [Subfolder]
RandomImageFile ← (
  ⨬(&fld"./"|⍚↘⧻:▽∵&fif.&fld.⊂"./"DS&fmd.⊂InDirImage)Desktop
  ▽≡(CE".png"°□).
  ⨬(""◌|°□⊏⌊×⚂⧻.)>0⧻.
)
# Repeat function [Function, Num]
Repeat! ← ⍥^!
# Call random function (2 choices)
DoRandom‼ ← ⨬^!^!⌊×2⚂
# Call random function (3 choices)
DoRandom‼! ← ⨬(^!|^!|^!)⌊×3⚂
# Call random function (4 choices)
DoRandom‼‼ ← ⨬(^!|^!|^!|^!)⌊×4⚂
# Exit (use at end of script)
Exit ← ⍥(&exit0)Desktop

# ---- Sample waveforms/wavetables
Wave₁ ← Avg{Sin3 Amp0.7Sin2 Sin1}
Wave₂ ← AM0.4Sin7 AM0.5Sin2 Sin1
Wave₂ ← Avg{Saw1 (Pul 2 0.2)}
Wave₄ ← Bit0.7 FM0.1Sin7 Sin1
Tbl₁ ← FM(Val 0 1) Sin2 Sin1
Tbl₂ ← Max Avg{Amp(Val 0 1)Sin2 Sin1}
Tbl₃ ← Bit(Val 0 1) Crs(ValPI 0 1 2) Sin1 Sin3

Print "Tbl₃"
Test Tbl₃
Image Tbl₃
Graph Tbl₃
GraphSpectrum Tbl₃

Exit
