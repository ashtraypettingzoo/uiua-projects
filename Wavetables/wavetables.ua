# Experimental!
# Uiua 0.13.0-dev.2
# https://github.com/ashtraypettingzoo/uiua-projects/blob/main/Wavetables/wavetables.ua
# https://uiua.org/pad?src=0_13_0-dev_2__IyBFeHBlcmltZW50YWwhCiMgVWl1YSAwLjEzLjAtZGV2LjIKIyBodHRwczovL2dpdGh1Yi5jb20vYXNodHJheXBldHRpbmd6b28vdWl1YS1wcm9qZWN0cy9ibG9iL21haW4vV2F2ZXRhYmxlcy93YXZldGFibGVzLnVhCiMgW3VybF0KCiMgVE9ETzogaGlnaC9sb3cgcGFzcyBmaWx0ZXJzCiMgVE9ETzogSVIgaW1wb3J0aW5nCiMgVE9ETzogZml4IFN0ciAoc3RyZXRjaCkgYnV6emluZwojIFRPRE86IFNpZCAoc2lkZSBieSBzaWRlKQojIFRPRE86IER3biAoZG93bnNhbXBsZSkKIyBUT0RPOiBTaHAgKHdhdmVzaGFwZSkKClMg4oaQIDIwNDggICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBTYW1wbGVzIHBlciBvc2NpbGxhdG9yCk8g4oaQIDI1NiAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBPc2NpbGxhdG9ycyBwZXIgdGFibGUKVCDihpAgw5dTIE8gICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBTYW1wbGVzIHBlciB0YWJsZQpTVSDihpAgw7cyKzEgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFNpZ25lZCB0byB1bnNpZ25lZApVUyDihpAgLTHDlzIgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFVuc2lnbmVkIHRvIHNpZ25lZApPVSDihpAgw7fin5zih6FTICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBPc2NpbGxhdG9yIGJhc2UsIHVuc2lnbmVkIDAtMQpPVCDihpAgw5fPhE9VICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBPc2NpbGxhdG9yIGJhc2UsIHRyaWdvbm9tZXRyaWMgMC0yz4AKUiDihpAg4oavwqRUICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIE9zY2lsbGF0b3IgdG8gdGFibGUgKHJlcGVhdCkKVSDihpAg4o2lKOKKouKNieKGr-KKnzrin5zDtzLiirjip7spICAgICAgICAgICAgICAgICAgICAgICAgICMgT2N0YXZlIHVwIChmb3IgcGxheWJhY2sgb25seSkKR0gg4oaQIDIwMCAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIEdyYXBoIGhlaWdodApDIOKGkCDihqXCrzHihqcxICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBDbGFtcApTVyDihpAg4oqP4pe_4qe7LCAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgU2VsZWN0ICh3cmFwcGVkKQpTSSDihpAgK-KKmcOX4p-cLeKKg-KKgyhTV-KMiikoU1fijIgpKC3iirjijIopICAgICAgICAgICAgICAgIyBTZWxlY3QgKGludGVycG9sYXRlZCkKUkkg4oaQIFNJw5fih6E6w7cs4qe7LCAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFJlc2l6ZSAoaW50ZXJwb2xhdGVkKQpDUyDihpAg4oi_K863ICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgQ29zaW5lCk5SIOKGkCDihqfiirjihqUgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBOYU4gcmVwbGFjZQpTQyDihpAgTlIxw7fin5ziiL8gICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBTaW5jClZCIOKGkCDDty0x4p-c4oehTyAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFZhbHVlIGJhc2UKVlIg4oaQICviipnDlzot4p-cOiAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgVmFsdWUgcmFuZ2UgW21pbiwgbWF4LCB2YWx1ZXNdCkdMIOKGkCAxNiAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBHSUYgbGVuZ3RoIChmcmFtZXMpClNIIOKGkCAyMDAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBTcGVjdHJ1bSBncmFwaCBoZWlnaHQKRlYg4oaQIMO3MuKImlMgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIEZvdXJpZXIgbWF4IHZhbHVlClNWIOKGkCArMeKMisOXw7cyU-KBvzLDt-KfnOKHocO3NFMgICAgICAgICAgICAgICAgICAgICAgIyBTcGVjdHJ1bSBpbmRleCB2YWx1ZXMKSVMg4oaQIOKorCgmZ2lmczE1fCZpbXMpPTLip7vilrMuICAgICAgICAgICAgICAgICAjIFNob3cgaW1hZ2Ugb3IgZ2lmClNNIOKGkCDih4ziiKco4oqCK-KKuOKKg-KLheKImCjDl-KKmeKLheKKoikp4oqZKDrCpMKw4oqCKSAgICAgICAgICAgICAgIyBTY2FuIGFkZCArIG11bHRpcGx5Ck1CIOKGkCDDtzox4oG_MTYgMiAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBNaW5pbXVtIDE2LWJpdCB2YWx1ZQpGVCDihpAg4oqDKOKIoMKw4oSCKSjDt0ZW4oy1KeKGmSsxw7cyUyBmZnQgICAgICAgICAgICAgICAjIEZvdXJpZXIgdHJhbnNmb3JtIFtwaGFzZSwgYW1wIOKGkCB3YXZlXQpGVSDihpAg4peMwrDihILCsGZmdOKKgjrijZzCsOKEgsKv4oaYMeKHjOKGmDEu4oSC4oqDKMOX4oi_KSjDl0NTKeKKmSjDl0ZWKSAjIEZvdXJpZXIgdW50cmFuc2Zvcm0gW3dhdmUg4oaQIHBoYXNlLCBhbXBdClLigoAg4oaQIOKYhzAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgUmVyYW5rIHplcm8KUuKCgSDihpAg4piHMSAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBSZXJhbmsgb25lClYhIOKGkCBS4oKB4omhXiFS4oKAClZWISDihpAgUuKCgeKJoV4hUuKCgOKKmVLigoAKVlZBISDihpAgUuKCgeKJoV4hUuKCgOKKmVLigoDiipniiplS4oKBClZBISDihpAgUuKCgeKJoV4hUuKCgOKKmVLigoEKVkFBISDihpAgUuKCgeKJoV4hUuKCgOKKmVLigoHiipniiplS4oKBCkEhIOKGkCBS4oKB4omhXiFS4oKBCkFBISDihpAgUuKCgeKJoV4hUuKCgeKKmVLigoEKCiMgQ29udm9sdXRpb25zCgpDSWQg4oaQIMKkwqQxICMgSWRlbnRpdHkgKGRlbHRhKSBjb252b2x1dGlvbgoKIyBUYWJsZSB2YWx1ZXMKClZhbCDihpAgVlLiipniipkoVkIpICAgICAjIFZhbHVlcyAobGluZWFyKSBbbWluLCBtYXhdClZhbFBJIOKGkCBWUuKKmeKKmSjigb86VkIpICMgVmFsdWVzIChwb3dlciBpbikgW21pbiwgbWF4LCBiYXNlXQpWYWxQTyDihpAg4oeMVmFsUEk6ICAgICMgVmFsdWVzIChwb3dlciBvdXQpIFttaW4sIG1heCwgYmFzZV0KCiMgSGVscGVycwoKUm5kSW50IOKGkCArMeKMisOX4pqCICMgUmFuZG9tIGludGVnZXIgMS1OIFtNYXhdCgojIFdhdmUgT3BlcmF0aW9ucwoKU3VtIOKGkCDCsOKWoS8r4o2aUuKCgSAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFN1bSBbe1dhdmVzL1RhYmxlc31dCkF2ZyDihpAgU3Vtw7fiirjip7sgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgQXZlcmFnZSBbe1dhdmVzL1RhYmxlc31dCkFtcCDihpAgVkEhw5cgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBBbXBsaWZ5IFtBbW91bnQsIFdhdmUvVGFibGVdCk1heCDihpAgQSEo4peM4o2l4p-cw7fiiaAwLuKGpeKIqS_ihqXin5zCry4pICAgICAgICAgICAgICAgICAgICAjIE1heGltaXplIFtXYXZlL1RhYmxlXQpQaHMg4oaQIFZBISjihrvigYXDl-KnuywpICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgUGhhc2Ugc2hpZnQgW0Ftb3VudCwgV2F2ZS9UYWJsZV0KWmVyIOKGkCBBISjihrviipcv4oan4oy1Li4pICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBQaGFzZSBzaGlmdCB0byB6ZXJvIHBvaW50IFtXYXZlL1RhYmxlXQpCaXQg4oaQIFZBIShVU-KGpzHDty0xOuKMisOXLFNVOuKBheKBvzoyKzHDlzE1LTox4oia4oanMeKGpTApICAjIEJpdGNydXNoIFtBbW91bnQgKDAtMSksIFdhdmUvVGFibGVdClJldiDihpAgQSHih4wgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgUmV2ZXJzZSBbV2F2ZS9UYWJsZV0KSW52IOKGkCBBIcKvICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIEludmVydCBbV2F2ZS9UYWJsZV0KTWlyIOKGkCBBISjiioLih4wu4oaZw7cyUykgICAgICAgICAgICAgICAgICAgICAgICAgICAjIE1pcnJvciAxc3QgaGFsZiBbV2F2ZS9UYWJsZV0KRmFkIOKGkCBWQSEo4oeM4qyaMcOX4oeMOuKsmjHin5zDl8O34qe7LuKHoeKMisO3MsOXU-KGpTApICAgICAgICAgICAgIyBGYWRlIGluICYgb3V0IFtBbW91bnQgKDAtMSksIFdhdmUvVGFibGVdCkNycyDihpAgVkFBISgr4oqZKMOXLToxKTrin5zDl-KKmTopICAgICAgICAgICAgICAgICAgICMgQ3Jvc3NmYWRlIFtBbW91bnQgKDAtMSksIFN0YXJ0IFdhdmUvVGFibGUsIEVuZCBXYXZlL1RhYmxlXQpTdHIg4oaQIFZWQSEo4oqC4oqZUkk6LTpT4p-cUkniipniioPihpnihpjiiKko4oanLTFT4oalMeKBhcOXU-KGpzHihqUwKSkgIyBTdHJldGNoIFtUbyAoMC0xKSwgRnJvbSAoMC0xKSwgV2F2ZS9UYWJsZV0KU2hmIOKGkCBWQSEoKykgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgU2hpZnQgdXAvZG93biBbQW1vdW50ICjCrzEtMSksIFdhdmUvVGFibGVdClNsYyDihpAgVkEhKFJJIFPihq_ima3igYXihqUxw5dTKSAgICAgICAgICAgICAgICAgICAgICAjIFNsaWNlIFtBbW91bnQgKDAtMSksIFdhdmUvVGFibGVdClNwZCDihpAgVkEhKFNsY-KBvzoyKSAgICAgICAgICAgICAgICAgICAgICAgICAgICMgU3BlZWQgdXAvZG93biBbT2N0YXZlcywgV2F2ZS9UYWJsZV0KU3BsIOKGkCBWQUEhKOKKguKKk-KGmeKGmOKfnDrigYXDl1Pihqcx4oalMCkgICAgICAgICAgICAgICAgICAgIyBTcGxpY2UgW0Ftb3VudCAoMC0xKSwgV2F2ZS9UYWJsZSAxLCBXYXZlL1RhYmxlIDJdCkNscCDihpAgQSEoQykgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIENsaXAKV3JwIOKGkCBBIShVU-KXvytNQjFTVSkgICAgICAgICAgICAgICAgICAgICAgICAgIyBXcmFwIFtXYXZlL1RhYmxlXQpBTSDihpAgVkFBISjDlysxw7cyw5ctMTopICAgICAgICAgICAgICAgICAgICAgICAgIyBBbXBsdXR1ZGUgbW9kdWxhdGlvbiBbQW1wbGl0dWRlLCBNb2R1bGF0b3IsIENhcnJpZXJdClJNIOKGkCBBQSHDlyAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBSaW5nIG1vZHVsYXRpb24gW01vZHVsYXRvciwgQ2Fycmllcl0KRk0g4oaQIFZBQSEoU0nDl1MrT1XDlykgICAgICAgICAgICAgICAgICAgICAgICAgIyBGcmVxdWVuY3kgbW9kdWxhdGlvbiBbQW1wbGl0dWRlLCBNb2R1bGF0b3IsIENhcnJpZXJdCkNudiDihpAgQUEhKC8rw5fijYnil6viipko4oav4pmtKTotMeKKuCvip7ss4qe74p-cOikgICAgICAgICAgICAgIyBDb252b2x2ZSBbSW1wdWxzZSwgV2F2ZS9UYWJsZV0KUGhyIOKGkCDiiaEoRlU64oqZKzpGVCk64omhw5dS4oKBLc-Aw5fPhOKNpSjiioLimoIpKzHDtzJTW11S4oKA4oqZUuKCgSAjIFBoYXNlIHJhbmRvbWl6ZSBbQW1vdW50ICgwLTEpLCBXYXZlL1RhYmxlXQoKIyBUYWJsZSBvcGVyYXRpb25zCgpUQ21iIOKGkCDihq9PX1MgU03iipko4qyaMOKGr-KKguKMiMO3OlQu4oGFw7c6U-KKmVIpICAgICMgQ29tYiBmaWx0ZXIgKGZlZWRiYWNrKSBbQW1wbGl0dWRlLCBIYXJtb25pYywgVGFibGVdClRDbWYg4oaQIOKGr09fUyvDl-KKmSjiioLihq86WzBdwqTin5zihpjijIrDtzrCr1PiipkoLlIpKSAjIENvbWIgZmlsdGVyIChmZWVkZm9yd2FyZCkgW0FtcGxpdHVkZSwgSGFybW9uaWMsIFRhYmxlXQoKIyBCYXNpYyB3YXZlZm9ybXMKClNpbiDihpAgViEo4oi_w5c6T1QpICAgICAgICAgICAgICMgU2luZSB3YXZlIFtIYXJtb25pY10KU2F3IOKGkCBWIShVU-KXvzHDlzpPVSkgICAgICAgICAgIyBTYXcgd2F2ZSBbSGFybW9uaWNdClRyaSDihpAgViEoVVPijLVVU-KXvzErMC4yNcKvw5c6T1UpICMgVHJpYW5nbGUgd2F2ZSBbSGFybW9uaWNdClB1bCDihpAgVlYhKFVT4omkVVM6U2F3KSAgICAgICAgIyBQdWxzZSB3YXZlIFtIYXJtb25pYywgRHV0eV0KU3FyIOKGkCBWIShQdWw6MC41KSAgICAgICAgICAgIyBTcXVhcmUgd2F2ZSBbSGFybW9uaWNdCk5zdyDihpAgKOKYhzFVU-KNpSjiioLimoIpU1tdKSAgICAgICAgIyBOb2lzZSB3YXZlIFtdCk5zdCDihpAgKOKNpSjiioJOc3cpT1tdKSAgICAgICAgICAjIE5vaXNlIHRhYmxlIFtdCgojIFV0aWxpdHkKClByaW50IOKGkCAmcGYKRXhwb3J0IOKGkCAmYXAgQ-KZrQpUZXN0IOKGkCAmYXAgVTNSIEPima0KR3JhcGgg4oaQIElT4o2i4oqiKD0x4qe7KeKJoSjih4zirJox4oqe4omg4oehR0jigYXDly0xR0ggU1UgVTJDKeKWvT0w4oeh4oyKw7dHTCBPCkltYWdlIOKGkCAmaW1z4oavT19TIFNVIFIgQ-KZrQpJbXBvcnRJbWFnZSDihpAgUkkgT-KJoVJJIFPijZzijYniiaEow7czLyvihpkzKeKXjMKwaW1nJmZyYWIKSW1wb3J0V2F2ZSDihpAgUuKCgCBSSSBT4peM4peMwrBhdWRpbyZmcmFiCkltcG9ydFRhYmxlIOKGkCDihq9PX1MgUkkgVOKXjOKXjMKwYXVkaW8mZnJhYiAjIEltcG9ydCAyNTYtd2F2ZSB0YWJsZQpUYWJsZUF2ZXJhZ2Ug4oaQIFLigoHDt08vK-KGr09fUyAgICAgICAgICAgICMgVGFibGUgdG8gd2F2ZSBbVGFibGVdClRhYmxlSW5kZXgg4oaQIFLigoHiiqHihqctMU_ihqUw4oGFOuKGr09fUzogICAgICAgICMgVGFibGUgdG8gd2F2ZSBbSW5kZXggKDAtMjU1KSwgVGFibGVdCkltYWdlVG9UYWJsZSDihpAgVVMKR3JhcGhDb252IOKGkCBJUyBHcmFwaCBBISjirJoww5fihq9TMSkKR3JhcGhTcGVjdHJ1bSDihpAgSVPijaLiiqIoPTHip7sp4omhKOKHjOKKnuKJpcO3U0jih6FTSOKKj1NWw7dGVuKGmcO3MlPijLVmZnQp4pa9PTDih6HijIrDt0dMIE8KR3JhcGhTcGVjdHJ1bUZ1bGwg4oaQIElT4o2i4oqiKD0x4qe7KeKJoSjih4ziip7iiaXDt1NI4oehU0jDt0ZW4oy1ZmZ0KeKWvT0w4oeh4oyKw7dHTCBPCgojIFNhbXBsZSB3YXZlZm9ybXMvd2F2ZXRhYmxlcwoKV2F2ZeKCgSDihpAgQXZne1NpbjMgQW1wMC43U2luMiBTaW4xfQpXYXZl4oKCIOKGkCBBTTAuNFNpbjcgQU0wLjVTaW4yIFNpbjEKV2F2ZeKCgiDihpAgQXZne1NhdzEgKFB1bCAyIDAuMil9CldhdmXigoQg4oaQIEJpdDAuNyBGTTAuMVNpbjcgU2luMQpUYmzigoEg4oaQIEZNKFZhbCAwIDEpIFNpbjIgU2luMQpUYmzigoIg4oaQIE1heCBBdmd7QW1wKFZhbCAwIDEpU2luMiBTaW4xfQpUYmzigoMg4oaQIEJpdChWYWwgMCAxKSBDcnMoVmFsUEkgMCAxIDIpIFNpbjEgU2luMwoKUHJpbnQgIlRibOKCgyIKVGVzdCBUYmzigoMKSW1hZ2UgVGJs4oKDCkdyYXBoIFRibOKCgwpHcmFwaFNwZWN0cnVtIFRibOKCgwo=

# TODO: high/low pass filters
# TODO: IR importing
# TODO: fix Str (stretch) buzzing
# TODO: Sid (side by side)
# TODO: Dwn (downsample)
# TODO: Shp (waveshape)

S ← 2048                                  # Samples per oscillator
O ← 256                                   # Oscillators per table
T ← ×S O                                  # Samples per table
SU ← ÷2+1                                 # Signed to unsigned
US ← -1×2                                 # Unsigned to signed
OU ← ÷⟜⇡S                                 # Oscillator base, unsigned 0-1
OT ← ×τOU                                 # Oscillator base, trigonometric 0-2π
R ← ↯¤T                                   # Oscillator to table (repeat)
U ← ⍥(⊢⍉↯⊟:⟜÷2⊸⧻)                         # Octave up (for playback only)
GH ← 200                                  # Graph height
C ← ↥¯1↧1                                 # Clamp
SW ← ⊏◿⧻,                                 # Select (wrapped)
SI ← +⊙×⟜-⊃⊃(SW⌊)(SW⌈)(-⊸⌊)               # Select (interpolated)
RI ← SI×⇡:÷,⧻,                            # Resize (interpolated)
CS ← ∿+η                                  # Cosine
NR ← ↧⊸↥                                  # NaN replace
SC ← NR1÷⟜∿                               # Sinc
VB ← ÷-1⟜⇡O                               # Value base
VR ← +⊙×:-⟜:                              # Value range [min, max, values]
GL ← 16                                   # GIF length (frames)
SH ← 200                                  # Spectrum graph height
FV ← ÷2√S                                 # Fourier max value
SV ← +1⌊×÷2Sⁿ2÷⟜⇡÷4S                      # Spectrum index values
IS ← ⨬(&gifs15|&ims)=2⧻△.                 # Show image or gif
SM ← ⇌∧(⊂+⊸⊃⋅∘(×⊙⋅⊢))⊙(:¤°⊂)              # Scan add + multiply
MB ← ÷:1ⁿ16 2                             # Minimum 16-bit value
FT ← ⊃(∠°ℂ)(÷FV⌵)↙+1÷2S fft               # Fourier transform [phase, amp ← wave]
FU ← ◌°ℂ°fft⊂:⍜°ℂ¯↘1⇌↘1.ℂ⊃(×∿)(×CS)⊙(×FV) # Fourier untransform [wave ← phase, amp]
R₀ ← ☇0                                   # Rerank zero
R₁ ← ☇1                                   # Rerank one
V! ← R₁≡^!R₀
VV! ← R₁≡^!R₀⊙R₀
VVA! ← R₁≡^!R₀⊙R₀⊙⊙R₁
VA! ← R₁≡^!R₀⊙R₁
VAA! ← R₁≡^!R₀⊙R₁⊙⊙R₁
A! ← R₁≡^!R₁
AA! ← R₁≡^!R₁⊙R₁

# Convolutions

CId ← ¤¤1 # Identity (delta) convolution

# Table values

Val ← VR⊙⊙(VB)     # Values (linear) [min, max]
ValPI ← VR⊙⊙(ⁿ:VB) # Values (power in) [min, max, base]
ValPO ← ⇌ValPI:    # Values (power out) [min, max, base]

# Helpers

RndInt ← +1⌊×⚂ # Random integer 1-N [Max]

# Wave Operations

Sum ← °□/+⍚R₁                               # Sum [{Waves/Tables}]
Avg ← Sum÷⊸⧻                                # Average [{Waves/Tables}]
Amp ← VA!×                                  # Amplify [Amount, Wave/Table]
Max ← A!(◌⍥⟜÷≠0.↥∩/↥⟜¯.)                    # Maximize [Wave/Table]
Phs ← VA!(↻⁅×⧻,)                            # Phase shift [Amount, Wave/Table]
Zer ← A!(↻⊗/↧⌵..)                           # Phase shift to zero point [Wave/Table]
Bit ← VA!(US↧1÷-1:⌊×,SU:⁅ⁿ:2+1×15-:1√↧1↥0)  # Bitcrush [Amount (0-1), Wave/Table]
Rev ← A!⇌                                   # Reverse [Wave/Table]
Inv ← A!¯                                   # Invert [Wave/Table]
Mir ← A!(⊂⇌.↙÷2S)                           # Mirror 1st half [Wave/Table]
Fad ← VA!(⇌⬚1×⇌:⬚1⟜×÷⧻.⇡⌊÷2×S↥0)            # Fade in & out [Amount (0-1), Wave/Table]
Crs ← VAA!(+⊙(×-:1):⟜×⊙:)                   # Crossfade [Amount (0-1), Start Wave/Table, End Wave/Table]
Str ← VVA!(⊂⊙RI:-:S⟜RI⊙⊃↙↘∩(↧-1S↥1⁅×S↧1↥0)) # Stretch [To (0-1), From (0-1), Wave/Table]
Shf ← VA!(+)                                # Shift up/down [Amount (¯1-1), Wave/Table]
Slc ← VA!(RI S↯♭⁅↥1×S)                      # Slice [Amount (0-1), Wave/Table]
Spd ← VA!(Slcⁿ:2)                           # Speed up/down [Octaves, Wave/Table]
Spl ← VAA!(⊂⊓↙↘⟜:⁅×S↧1↥0)                   # Splice [Amount (0-1), Wave/Table 1, Wave/Table 2]
Clp ← A!(C)                                 # Clip
Wrp ← A!(US◿+MB1SU)                         # Wrap [Wave/Table]
AM ← VAA!(×+1÷2×-1:)                        # Amplutude modulation [Amplitude, Modulator, Carrier]
RM ← AA!×                                   # Ring modulation [Modulator, Carrier]
FM ← VAA!(SI×S+OU×)                         # Frequency modulation [Amplitude, Modulator, Carrier]
Cnv ← AA!(/+×⍉◫⊙(↯♭):-1⊸+⧻,⧻⟜:)             # Convolve [Impulse, Wave/Table]
Phr ← ≡(FU:⊙+:FT):≡×R₁-π×τ⍥(⊂⚂)+1÷2S[]R₀⊙R₁ # Phase randomize [Amount (0-1), Wave/Table]

# Table operations

TCmb ← ↯O_S SM⊙(⬚0↯⊂⌈÷:T.⁅÷:S⊙R)    # Comb filter (feedback) [Amplitude, Harmonic, Table]
TCmf ← ↯O_S+×⊙(⊂↯:[0]¤⟜↘⌊÷:¯S⊙(.R)) # Comb filter (feedforward) [Amplitude, Harmonic, Table]

# Basic waveforms

Sin ← V!(∿×:OT)             # Sine wave [Harmonic]
Saw ← V!(US◿1×:OU)          # Saw wave [Harmonic]
Tri ← V!(US⌵US◿1+0.25¯×:OU) # Triangle wave [Harmonic]
Pul ← VV!(US≤US:Saw)        # Pulse wave [Harmonic, Duty]
Sqr ← V!(Pul:0.5)           # Square wave [Harmonic]
Nsw ← (☇1US⍥(⊂⚂)S[])        # Noise wave []
Nst ← (⍥(⊂Nsw)O[])          # Noise table []

# Utility

Print ← &pf
Export ← &ap C♭
Test ← &ap U3R C♭
Graph ← IS⍢⊢(=1⧻)≡(⇌⬚1⊞≠⇡GH⁅×-1GH SU U2C)▽=0⇡⌊÷GL O
Image ← &ims↯O_S SU R C♭
ImportImage ← RI O≡RI S⍜⍉≡(÷3/+↙3)◌°img&frab
ImportWave ← R₀ RI S◌◌°audio&frab
ImportTable ← ↯O_S RI T◌◌°audio&frab # Import 256-wave table
TableAverage ← R₁÷O/+↯O_S            # Table to wave [Table]
TableIndex ← R₁⊡↧-1O↥0⁅:↯O_S:        # Table to wave [Index (0-255), Table]
ImageToTable ← US
GraphConv ← IS Graph A!(⬚0×↯S1)
GraphSpectrum ← IS⍢⊢(=1⧻)≡(⇌⊞≥÷SH⇡SH⊏SV÷FV↙÷2S⌵fft)▽=0⇡⌊÷GL O
GraphSpectrumFull ← IS⍢⊢(=1⧻)≡(⇌⊞≥÷SH⇡SH÷FV⌵fft)▽=0⇡⌊÷GL O

# Sample waveforms/wavetables

Wave₁ ← Avg{Sin3 Amp0.7Sin2 Sin1}
Wave₂ ← AM0.4Sin7 AM0.5Sin2 Sin1
Wave₂ ← Avg{Saw1 (Pul 2 0.2)}
Wave₄ ← Bit0.7 FM0.1Sin7 Sin1
Tbl₁ ← FM(Val 0 1) Sin2 Sin1
Tbl₂ ← Max Avg{Amp(Val 0 1)Sin2 Sin1}
Tbl₃ ← Bit(Val 0 1) Crs(ValPI 0 1 2) Sin1 Sin3

Print "Tbl₃"
Test Tbl₃
Image Tbl₃
Graph Tbl₃
GraphSpectrum Tbl₃
