# Experimental!
# Uiua 0.13.0-dev.2
# https://github.com/ashtraypettingzoo/uiua-projects/blob/main/Wavetables/wavetables.ua
# https://uiua.org/pad?src=0_13_0-dev_2__IyBFeHBlcmltZW50YWwhCiMgVWl1YSAwLjEzLjAtZGV2LjIKIyBodHRwczovL2dpdGh1Yi5jb20vYXNodHJheXBldHRpbmd6b28vdWl1YS1wcm9qZWN0cy9ibG9iL21haW4vV2F2ZXRhYmxlcy93YXZldGFibGVzLnVhCiMgW3VybF0KCiMgVE9ETzogaGlnaC9sb3cgcGFzcyBmaWx0ZXJzCiMgVE9ETzogSVIgaW1wb3J0aW5nCiMgVE9ETzogZml4IFN0ciAoc3RyZXRjaCkgYnV6emluZwojIFRPRE86IFNpZCAoc2lkZSBieSBzaWRlKQojIFRPRE86IFNocCAod2F2ZXNoYXBlKQoKUyDihpAgMjA0OCAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFNhbXBsZXMgcGVyIG9zY2lsbGF0b3IKTyDihpAgMjU2ICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIE9zY2lsbGF0b3JzIHBlciB0YWJsZQpUIOKGkCDDl1MgTyAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFNhbXBsZXMgcGVyIHRhYmxlClNVIOKGkCDDtzIrMSAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgU2lnbmVkIHRvIHVuc2lnbmVkClVTIOKGkCAtMcOXMiAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgVW5zaWduZWQgdG8gc2lnbmVkCk9VIOKGkCDDt-KfnOKHoVMgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIE9zY2lsbGF0b3IgYmFzZSwgdW5zaWduZWQgMC0xCk9UIOKGkCDDl8-ET1UgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIE9zY2lsbGF0b3IgYmFzZSwgdHJpZ29ub21ldHJpYyAwLTLPgApSIOKGkCDihq_CpFQgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgT3NjaWxsYXRvciB0byB0YWJsZSAocmVwZWF0KQpVIOKGkCDijaUo4oqi4o2J4oav4oqfOuKfnMO3MuKKuOKnuykgICAgICAgICAgICAgICAgICAgICAgICAgIyBPY3RhdmUgdXAgKGZvciBwbGF5YmFjayBvbmx5KQpHSCDihpAgMjAwICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgR3JhcGggaGVpZ2h0CkMg4oaQIOKGpcKvMeKGpzEgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIENsYW1wClNXIOKGkCDiio_il7_ip7ssICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBTZWxlY3QgKHdyYXBwZWQpClNJIOKGkCAr4oqZw5fin5wt4oqD4oqDKFNX4oyKKShTV-KMiCkoLeKKuOKMiikgICAgICAgICAgICAgICAjIFNlbGVjdCAoaW50ZXJwb2xhdGVkKQpSSSDihpAgU0nDl-KHoTrDtyzip7ssICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgUmVzaXplIChpbnRlcnBvbGF0ZWQpCkNTIOKGkCDiiL8rzrcgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBDb3NpbmUKTlIg4oaQIOKGp-KKuOKGpSAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIE5hTiByZXBsYWNlClNDIOKGkCBOUjHDt-KfnOKIvyAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFNpbmMKVkIg4oaQIMO3LTHin5zih6FPICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgVmFsdWUgYmFzZQpWUiDihpAgK-KKmcOXOi3in5w6ICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBWYWx1ZSByYW5nZSBbbWluLCBtYXgsIHZhbHVlc10KR0wg4oaQIDE2ICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIEdJRiBsZW5ndGggKGZyYW1lcykKU0gg4oaQIDIwMCAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFNwZWN0cnVtIGdyYXBoIGhlaWdodApGViDihpAgw7cy4oiaUyAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgRm91cmllciBtYXggdmFsdWUKU1Yg4oaQICsx4oyKw5fDtzJT4oG_MsO34p-c4oehw7c0UyAgICAgICAgICAgICAgICAgICAgICAjIFNwZWN0cnVtIGluZGV4IHZhbHVlcwpJUyDihpAg4qisKCZnaWZzMTV8Jmltcyk9MuKnu-KWsy4gICAgICAgICAgICAgICAgICMgU2hvdyBpbWFnZSBvciBnaWYKU00g4oaQIOKHjOKIpyjiioIr4oq44oqD4ouF4oiYKMOX4oqZ4ouF4oqiKSniipkoOsKkwrDiioIpICAgICAgICAgICAgICAjIFNjYW4gYWRkICsgbXVsdGlwbHkKTUIg4oaQIMO3OjHigb8xNiAyICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIE1pbmltdW0gMTYtYml0IHZhbHVlCkZUIOKGkCDiioMo4oigwrDihIIpKMO3RlbijLUp4oaZKzHDtzJTIGZmdCAgICAgICAgICAgICAgICMgRm91cmllciB0cmFuc2Zvcm0gW3BoYXNlLCBhbXAg4oaQIHdhdmVdCkZVIOKGkCDil4zCsOKEgsKwZmZ04oqCOuKNnMKw4oSCwq_ihpgx4oeM4oaYMS7ihILiioMow5fiiL8pKMOXQ1Mp4oqZKMOXRlYpICMgRm91cmllciB1bnRyYW5zZm9ybSBbd2F2ZSDihpAgcGhhc2UsIGFtcF0KUuKCgCDihpAg4piHMCAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBSZXJhbmsgemVybwpS4oKBIOKGkCDimIcxICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFJlcmFuayBvbmUKViEg4oaQIFLigoHiiaFeIVLigoAKVlYhIOKGkCBS4oKB4omhXiFS4oKA4oqZUuKCgApWVkEhIOKGkCBS4oKB4omhXiFS4oKA4oqZUuKCgOKKmeKKmVLigoEKVkEhIOKGkCBS4oKB4omhXiFS4oKA4oqZUuKCgQpWQUEhIOKGkCBS4oKB4omhXiFS4oKA4oqZUuKCgeKKmeKKmVLigoEKQSEg4oaQIFLigoHiiaFeIVLigoEKQUEhIOKGkCBS4oKB4omhXiFS4oKB4oqZUuKCgQoKIyAtLS0tIENvbnZvbHV0aW9ucwojIElkZW50aXR5IChkZWx0YSkgY29udm9sdXRpb24KQ0lkIOKGkCDCpMKkMQoKIyAtLS0tIFRhYmxlIHZhbHVlcwojIFZhbHVlcyAobGluZWFyKSBbbWluLCBtYXhdClZhbCDihpAgVlLiipniipkoVkIpCiMgVmFsdWVzIChwb3dlciBpbikgW21pbiwgbWF4LCBiYXNlXQpWYWxQSSDihpAgVlLiipniipko4oG_OlZCKQojIFZhbHVlcyAocG93ZXIgb3V0KSBbbWluLCBtYXgsIGJhc2VdClZhbFBPIOKGkCDih4xWYWxQSToKCiMgLS0tLSBIZWxwZXJzCiMgUmFuZG9tIGludGVnZXIgMS1OIFtNYXhdClJuZEludCDihpAgKzHijIrDl-KaggoKIyAtLS0tIFdhdmUgT3BlcmF0aW9ucwojIFN1bSBbe1dhdmVzL1RhYmxlc31dClN1bSDihpAgwrDilqEvK-KNmlLigoEKIyBBdmVyYWdlIFt7V2F2ZXMvVGFibGVzfV0KQXZnIOKGkCBTdW3Dt-KKuOKnuwojIEFtcGxpZnkgW0Ftb3VudCwgV2F2ZS9UYWJsZV0KQW1wIOKGkCBWQSHDlwojIE1heGltaXplIFtXYXZlL1RhYmxlXQpNYXgg4oaQIEEhKOKXjOKNpeKfnMO34omgMC7ihqXiiKkv4oal4p-cwq8uKQojIFBoYXNlIHNoaWZ0IFtBbW91bnQsIFdhdmUvVGFibGVdClBocyDihpAgVkEhKOKGu-KBhcOX4qe7LCkKIyBQaGFzZSBzaGlmdCB0byB6ZXJvIHBvaW50IFtXYXZlL1RhYmxlXQpaZXIg4oaQIEEhKOKGu-KKly_ihqfijLUuLikKIyBCaXRjcnVzaCBbQW1vdW50ICgwLTEpLCBXYXZlL1RhYmxlXQpCaXQg4oaQIFZBIShVU-KGpzHDty0xOuKMisOXLFNVOuKBheKBvzoyKzHDlzE1LTox4oia4oanMeKGpTApCiMgUmV2ZXJzZSBbV2F2ZS9UYWJsZV0KUmV2IOKGkCBBIeKHjAojIEludmVydCBbV2F2ZS9UYWJsZV0KSW52IOKGkCBBIcKvCiMgTWlycm9yIDFzdCBoYWxmIFtXYXZlL1RhYmxlXQpNaXIg4oaQIEEhKOKKguKHjC7ihpnDtzJTKQojIEZhZGUgc3RhcnQgJiBlbmQgW0Ftb3VudCAoMC0xKSwgV2F2ZS9UYWJsZV0KRmFkIOKGkCBWQSEo4oeM4qyaMcOX4oeMOuKsmjHin5zDl8O34qe7LuKHoeKMisO3MsOXU-KGpTApCiMgQ3Jvc3NmYWRlIFtBbW91bnQgKDAtMSksIFN0YXJ0IFdhdmUvVGFibGUsIEVuZCBXYXZlL1RhYmxlXQpDcnMg4oaQIFZBQSEoK-KKmSjDly06MSk64p-cw5fiipk6KQojIFN0cmV0Y2ggW1RvICgwLTEpLCBGcm9tICgwLTEpLCBXYXZlL1RhYmxlXQpTdHIg4oaQIFZWQSEo4oqC4oqZUkk6LTpT4p-cUkniipniioPihpnihpjiiKko4oanLTFT4oalMeKBhcOXU-KGpzHihqUwKSkKIyBTaGlmdCB1cC9kb3duIFtBbW91bnQgKMKvMS0xKSwgV2F2ZS9UYWJsZV0KU2hmIOKGkCBWQSEoKykKIyBTbGljZSBbQW1vdW50ICgwLTEpLCBXYXZlL1RhYmxlXQpTbGMg4oaQIFZBIShSSSBT4oav4pmt4oGF4oalMcOXUykKIyBTcGVlZCB1cC9kb3duIFtPY3RhdmVzLCBXYXZlL1RhYmxlXQpTcGQg4oaQIFZBIShTbGPigb86MikKIyBTcGxpY2UgW0Ftb3VudCAoMC0xKSwgV2F2ZS9UYWJsZSAxLCBXYXZlL1RhYmxlIDJdClNwbCDihpAgVkFBISjiioLiipPihpnihpjin5w64oGFw5dT4oanMeKGpTApCiMgQ2xpcApDbHAg4oaQIEEhKEMpCiMgV3JhcCBbV2F2ZS9UYWJsZV0KV3JwIOKGkCBBIShVU-KXvytNQjFTVSkKIyBBbXBsdXR1ZGUgbW9kdWxhdGlvbiBbQW1wbGl0dWRlLCBNb2R1bGF0b3IsIENhcnJpZXJdCkFNIOKGkCBWQUEhKMOXKzHDtzLDly0xOikKIyBSaW5nIG1vZHVsYXRpb24gW01vZHVsYXRvciwgQ2Fycmllcl0KUk0g4oaQIEFBIcOXCiMgRnJlcXVlbmN5IG1vZHVsYXRpb24gW0FtcGxpdHVkZSwgTW9kdWxhdG9yLCBDYXJyaWVyXQpGTSDihpAgVkFBIShTScOXUytPVcOXKQojIENvbnZvbHZlIFtJbXB1bHNlLCBXYXZlL1RhYmxlXQpDbnYg4oaQIEFBISgvK8OX4o2J4per4oqZKOKGr-KZrSk6LTHiirgr4qe7LOKnu-KfnDopCiMgUGhhc2UgcmFuZG9taXplIFtBbW91bnQgKDAtMSksIFdhdmUvVGFibGVdClBociDihpAg4omhKEZVOuKKmSs6RlQpOuKJocOXUuKCgS3PgMOXz4TijaUo4oqC4pqCKSsxw7cyU1tdUuKCgOKKmVLigoEKIyBIYXJtb25pYyB3YXZlIGZpbHRlciBbQW1vdW50ICgwLTEpLCBGaWx0ZXIgV2F2ZS9UYWJsZSwgU291cmNlIFdhdmUvVGFibGVdCkh3ZiDihpAgVkFBIShGVeKKmcOXOisxw5fiipkoLTHil4ziiKlGVCkpCiMgRG93bnNhbXBsZSBbQW1vdW50ICgwLTEpLCBXYXZlL1RhYmxlXQpEd24g4oaQIFZBISgKICDijIjDtzpTLuKBheKGpTHigb86Misxw5ctMeKCmTJTLToxCiAg4oavwqRT4o2J4oav4p-cw7fin5wo4omhLyvihq_iioI6KQopCgojIC0tLS0gVGFibGUgb3BlcmF0aW9ucwojIENvbWIgZmlsdGVyIChmZWVkYmFjaykgW0FtcGxpdHVkZSwgSGFybW9uaWMsIFRhYmxlXQpUQ21iIOKGkCDihq9PX1MgU03iipko4qyaMOKGr-KKguKMiMO3OlQu4oGFw7c6U-KKmVIpCiMgQ29tYiBmaWx0ZXIgKGZlZWRmb3J3YXJkKSBbQW1wbGl0dWRlLCBIYXJtb25pYywgVGFibGVdClRDbWYg4oaQIOKGr09fUyvDl-KKmSjiioLihq86WzBdwqTin5zihpjijIrDtzrCr1PiipkoLlIpKQoKIyAtLS0tIFdhdmVmb3JtcwojIFNpbmUgd2F2ZSBbSGFybW9uaWNdClNpbiDihpAgViEo4oi_w5c6T1QpCiMgU2F3IHdhdmUgW0hhcm1vbmljXQpTYXcg4oaQIFYhKFVT4pe_McOXOk9VKQojIFRyaWFuZ2xlIHdhdmUgW0hhcm1vbmljXQpUcmkg4oaQIFYhKFVT4oy1VVPil78xKzAuMjXCr8OXOk9VKQojIFB1bHNlIHdhdmUgW0hhcm1vbmljLCBEdXR5XQpQdWwg4oaQIFZWIShVU-KJpFVTOlNhdykKIyBTcXVhcmUgd2F2ZSBbSGFybW9uaWNdClNxciDihpAgViEoUHVsOjAuNSkKIyBOb2lzZSB3YXZlIFtdCk5zdyDihpAgKOKYhzFVU-KNpSjiioLimoIpU1tdKQoKIyAtLS0tIFRhYmxlcwojIE5vaXNlIHRhYmxlIFtdCk5zdCDihpAgKOKNpSjiioJOc3cpT1tdKQoKIyAtLS0tIFV0aWxpdHkKUHJpbnQg4oaQICZwZgpFeHBvcnQg4oaQICZhcCBD4pmtClRlc3Qg4oaQICZhcCBVM1IgQ-KZrQpHcmFwaCDihpAgSVPijaLiiqIoPTHip7sp4omhKOKHjOKsmjHiip7iiaDih6FHSOKBhcOXLTFHSCBTVSBVMkMp4pa9PTDih6HijIrDt0dMIE8KSW1hZ2Ug4oaQICZpbXPihq9PX1MgU1UgUiBD4pmtCkltcG9ydEltYWdlIOKGkCBSSSBP4omhUkkgU-KNnOKNieKJoSjDtzMvK-KGmTMp4peMwrBpbWcmZnJhYgpJbXBvcnRXYXZlIOKGkCBS4oKAIFJJIFPil4zil4zCsGF1ZGlvJmZyYWIKSW1wb3J0VGFibGUg4oaQIOKGr09fUyBSSSBU4peM4peMwrBhdWRpbyZmcmFiICMgSW1wb3J0IDI1Ni13YXZlIHRhYmxlClRhYmxlQXZlcmFnZSDihpAgUuKCgcO3Ty8r4oavT19TICAgICAgICAgICAgIyBUYWJsZSB0byB3YXZlIFtUYWJsZV0KVGFibGVJbmRleCDihpAgUuKCgeKKoeKGpy0xT-KGpTDigYU64oavT19TOiAgICAgICAgIyBUYWJsZSB0byB3YXZlIFtJbmRleCAoMC0yNTUpLCBUYWJsZV0KSW1hZ2VUb1RhYmxlIOKGkCBVUwpHcmFwaENvbnYg4oaQIElTIEdyYXBoIEEhKOKsmjDDl-KGr1MxKQpHcmFwaFNwZWN0cnVtIOKGkCBJU-KNouKKoig9MeKnuyniiaEo4oeM4oqe4omlw7dTSOKHoVNI4oqPU1bDt0ZW4oaZw7cyU-KMtWZmdCnilr09MOKHoeKMisO3R0wgTwpHcmFwaFNwZWN0cnVtRnVsbCDihpAgSVPijaLiiqIoPTHip7sp4omhKOKHjOKKnuKJpcO3U0jih6FTSMO3RlbijLVmZnQp4pa9PTDih6HijIrDt0dMIE8KCiMgLS0tLSBTYW1wbGUgd2F2ZWZvcm1zL3dhdmV0YWJsZXMKV2F2ZeKCgSDihpAgQXZne1NpbjMgQW1wMC43U2luMiBTaW4xfQpXYXZl4oKCIOKGkCBBTTAuNFNpbjcgQU0wLjVTaW4yIFNpbjEKV2F2ZeKCgiDihpAgQXZne1NhdzEgKFB1bCAyIDAuMil9CldhdmXigoQg4oaQIEJpdDAuNyBGTTAuMVNpbjcgU2luMQpUYmzigoEg4oaQIEZNKFZhbCAwIDEpIFNpbjIgU2luMQpUYmzigoIg4oaQIE1heCBBdmd7QW1wKFZhbCAwIDEpU2luMiBTaW4xfQpUYmzigoMg4oaQIEJpdChWYWwgMCAxKSBDcnMoVmFsUEkgMCAxIDIpIFNpbjEgU2luMwoKUHJpbnQgIlRibOKCgyIKVGVzdCBEd24gKFZhbCAwIDEpIFNhdzEKSW1hZ2UgVGJs4oKDCkdyYXBoIFRibOKCgwpHcmFwaFNwZWN0cnVtIFRibOKCgwo=

# TODO: high/low pass filters
# TODO: IR importing
# TODO: fix Str (stretch) buzzing
# TODO: Sid (side by side)
# TODO: Shp (waveshape)

S ← 2048                                  # Samples per oscillator
O ← 256                                   # Oscillators per table
T ← ×S O                                  # Samples per table
SU ← ÷2+1                                 # Signed to unsigned
US ← -1×2                                 # Unsigned to signed
OU ← ÷⟜⇡S                                 # Oscillator base, unsigned 0-1
OT ← ×τOU                                 # Oscillator base, trigonometric 0-2π
R ← ↯¤T                                   # Oscillator to table (repeat)
U ← ⍥(⊢⍉↯⊟:⟜÷2⊸⧻)                         # Octave up (for playback only)
GH ← 200                                  # Graph height
C ← ↥¯1↧1                                 # Clamp
SW ← ⊏◿⧻,                                 # Select (wrapped)
SI ← +⊙×⟜-⊃⊃(SW⌊)(SW⌈)(-⊸⌊)               # Select (interpolated)
RI ← SI×⇡:÷,⧻,                            # Resize (interpolated)
CS ← ∿+η                                  # Cosine
NR ← ↧⊸↥                                  # NaN replace
SC ← NR1÷⟜∿                               # Sinc
VB ← ÷-1⟜⇡O                               # Value base
VR ← +⊙×:-⟜:                              # Value range [min, max, values]
GL ← 16                                   # GIF length (frames)
SH ← 200                                  # Spectrum graph height
FV ← ÷2√S                                 # Fourier max value
SV ← +1⌊×÷2Sⁿ2÷⟜⇡÷4S                      # Spectrum index values
IS ← ⨬(&gifs15|&ims)=2⧻△.                 # Show image or gif
SM ← ⇌∧(⊂+⊸⊃⋅∘(×⊙⋅⊢))⊙(:¤°⊂)              # Scan add + multiply
MB ← ÷:1ⁿ16 2                             # Minimum 16-bit value
FT ← ⊃(∠°ℂ)(÷FV⌵)↙+1÷2S fft               # Fourier transform [phase, amp ← wave]
FU ← ◌°ℂ°fft⊂:⍜°ℂ¯↘1⇌↘1.ℂ⊃(×∿)(×CS)⊙(×FV) # Fourier untransform [wave ← phase, amp]
R₀ ← ☇0                                   # Rerank zero
R₁ ← ☇1                                   # Rerank one
V! ← R₁≡^!R₀
VV! ← R₁≡^!R₀⊙R₀
VVA! ← R₁≡^!R₀⊙R₀⊙⊙R₁
VA! ← R₁≡^!R₀⊙R₁
VAA! ← R₁≡^!R₀⊙R₁⊙⊙R₁
A! ← R₁≡^!R₁
AA! ← R₁≡^!R₁⊙R₁

# ---- Convolutions
# Identity (delta) convolution
CId ← ¤¤1

# ---- Table values
# Values (linear) [min, max]
Val ← VR⊙⊙(VB)
# Values (power in) [min, max, base]
ValPI ← VR⊙⊙(ⁿ:VB)
# Values (power out) [min, max, base]
ValPO ← ⇌ValPI:

# ---- Helpers
# Random integer 1-N [Max]
RndInt ← +1⌊×⚂

# ---- Wave Operations
# Sum [{Waves/Tables}]
Sum ← °□/+⍚R₁
# Average [{Waves/Tables}]
Avg ← Sum÷⊸⧻
# Amplify [Amount, Wave/Table]
Amp ← VA!×
# Maximize [Wave/Table]
Max ← A!(◌⍥⟜÷≠0.↥∩/↥⟜¯.)
# Phase shift [Amount, Wave/Table]
Phs ← VA!(↻⁅×⧻,)
# Phase shift to zero point [Wave/Table]
Zer ← A!(↻⊗/↧⌵..)
# Bitcrush [Amount (0-1), Wave/Table]
Bit ← VA!(US↧1÷-1:⌊×,SU:⁅ⁿ:2+1×15-:1√↧1↥0)
# Reverse [Wave/Table]
Rev ← A!⇌
# Invert [Wave/Table]
Inv ← A!¯
# Mirror 1st half [Wave/Table]
Mir ← A!(⊂⇌.↙÷2S)
# Fade start & end [Amount (0-1), Wave/Table]
Fad ← VA!(⇌⬚1×⇌:⬚1⟜×÷⧻.⇡⌊÷2×S↥0)
# Crossfade [Amount (0-1), Start Wave/Table, End Wave/Table]
Crs ← VAA!(+⊙(×-:1):⟜×⊙:)
# Stretch [To (0-1), From (0-1), Wave/Table]
Str ← VVA!(⊂⊙RI:-:S⟜RI⊙⊃↙↘∩(↧-1S↥1⁅×S↧1↥0))
# Shift up/down [Amount (¯1-1), Wave/Table]
Shf ← VA!(+)
# Slice [Amount (0-1), Wave/Table]
Slc ← VA!(RI S↯♭⁅↥1×S)
# Speed up/down [Octaves, Wave/Table]
Spd ← VA!(Slcⁿ:2)
# Splice [Amount (0-1), Wave/Table 1, Wave/Table 2]
Spl ← VAA!(⊂⊓↙↘⟜:⁅×S↧1↥0)
# Clip
Clp ← A!(C)
# Wrap [Wave/Table]
Wrp ← A!(US◿+MB1SU)
# Amplutude modulation [Amplitude, Modulator, Carrier]
AM ← VAA!(×+1÷2×-1:)
# Ring modulation [Modulator, Carrier]
RM ← AA!×
# Frequency modulation [Amplitude, Modulator, Carrier]
FM ← VAA!(SI×S+OU×)
# Convolve [Impulse, Wave/Table]
Cnv ← AA!(/+×⍉◫⊙(↯♭):-1⊸+⧻,⧻⟜:)
# Phase randomize [Amount (0-1), Wave/Table]
Phr ← ≡(FU:⊙+:FT):≡×R₁-π×τ⍥(⊂⚂)+1÷2S[]R₀⊙R₁
# Harmonic wave filter [Amount (0-1), Filter Wave/Table, Source Wave/Table]
Hwf ← VAA!(FU⊙×:+1×⊙(-1◌∩FT))
# Downsample [Amount (0-1), Wave/Table]
Dwn ← VA!(
  ⌈÷:S.⁅↥1ⁿ:2+1×-1ₙ2S-:1
  ↯¤S⍉↯⟜÷⟜(≡/+↯⊂:)
)

# ---- Table operations
# Comb filter (feedback) [Amplitude, Harmonic, Table]
TCmb ← ↯O_S SM⊙(⬚0↯⊂⌈÷:T.⁅÷:S⊙R)
# Comb filter (feedforward) [Amplitude, Harmonic, Table]
TCmf ← ↯O_S+×⊙(⊂↯:[0]¤⟜↘⌊÷:¯S⊙(.R))

# ---- Waveforms
# Sine wave [Harmonic]
Sin ← V!(∿×:OT)
# Saw wave [Harmonic]
Saw ← V!(US◿1×:OU)
# Triangle wave [Harmonic]
Tri ← V!(US⌵US◿1+0.25¯×:OU)
# Pulse wave [Harmonic, Duty]
Pul ← VV!(US≤US:Saw)
# Square wave [Harmonic]
Sqr ← V!(Pul:0.5)
# Noise wave []
Nsw ← (☇1US⍥(⊂⚂)S[])

# ---- Tables
# Noise table []
Nst ← (⍥(⊂Nsw)O[])

# ---- Utility
Print ← &pf
Export ← &ap C♭
Test ← &ap U3R C♭
Graph ← IS⍢⊢(=1⧻)≡(⇌⬚1⊞≠⇡GH⁅×-1GH SU U2C)▽=0⇡⌊÷GL O
Image ← &ims↯O_S SU R C♭
ImportImage ← RI O≡RI S⍜⍉≡(÷3/+↙3)◌°img&frab
ImportWave ← R₀ RI S◌◌°audio&frab
ImportTable ← ↯O_S RI T◌◌°audio&frab # Import 256-wave table
TableAverage ← R₁÷O/+↯O_S            # Table to wave [Table]
TableIndex ← R₁⊡↧-1O↥0⁅:↯O_S:        # Table to wave [Index (0-255), Table]
ImageToTable ← US
GraphConv ← IS Graph A!(⬚0×↯S1)
GraphSpectrum ← IS⍢⊢(=1⧻)≡(⇌⊞≥÷SH⇡SH⊏SV÷FV↙÷2S⌵fft)▽=0⇡⌊÷GL O
GraphSpectrumFull ← IS⍢⊢(=1⧻)≡(⇌⊞≥÷SH⇡SH÷FV⌵fft)▽=0⇡⌊÷GL O

# ---- Sample waveforms/wavetables
Wave₁ ← Avg{Sin3 Amp0.7Sin2 Sin1}
Wave₂ ← AM0.4Sin7 AM0.5Sin2 Sin1
Wave₂ ← Avg{Saw1 (Pul 2 0.2)}
Wave₄ ← Bit0.7 FM0.1Sin7 Sin1
Tbl₁ ← FM(Val 0 1) Sin2 Sin1
Tbl₂ ← Max Avg{Amp(Val 0 1)Sin2 Sin1}
Tbl₃ ← Bit(Val 0 1) Crs(ValPI 0 1 2) Sin1 Sin3

Print "Tbl₃"
Test Dwn (Val 0 1) Saw1
Image Tbl₃
Graph Tbl₃
GraphSpectrum Tbl₃
