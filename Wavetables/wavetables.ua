# Uiua 0.13.0-dev.2
# https://uiua.org/pad?src=0_13_0-dev_2__IyBVaXVhIDAuMTMuMC1kZXYuMgojIFt1cmxdCgpTIOKGkCAyMDQ4ICAgICAgICAgICAgICAgICAgICAjIFNhbXBsZXMgcGVyIG9zY2lsbGF0b3IKTyDihpAgMjU2ICAgICAgICAgICAgICAgICAgICAgIyBPc2NpbGxhdG9ycyBwZXIgdGFibGUKVCDihpAgw5dTIE8gICAgICAgICAgICAgICAgICAgICMgU2FtcGxlcyBwZXIgdGFibGUKU1Ug4oaQIMO3MisxICAgICAgICAgICAgICAgICAgICMgU2lnbmVkIHRvIHVuc2lnbmVkClVTIOKGkCAtMcOXMiAgICAgICAgICAgICAgICAgICAjIFVuc2lnbmVkIHRvIHNpZ25lZApPVSDihpAgw7fin5zih6FTICAgICAgICAgICAgICAgICAgICMgT3NjaWxsYXRvciBiYXNlLCB1bnNpZ25lZCAwLTEKT1Qg4oaQIMOXz4RPVSAgICAgICAgICAgICAgICAgICAjIE9zY2lsbGF0b3IgYmFzZSwgdHJpZ29ub21ldHJpYyAwLTLPgApSIOKGkCDihq_CpFQgICAgICAgICAgICAgICAgICAgICAjIE9zY2lsbGF0b3IgdG8gdGFibGUgKHJlcGVhdCkKVSDihpAg4o2lKOKKouKNieKGr-KKnzrin5zDtzLiirjip7spICAgICAgICAgICAjIE9jdGF2ZSB1cCAoZm9yIHBsYXliYWNrIG9ubHkpCk0g4oaQIOKXjOKNpeKfnMO34omgMC7ihqXiiKkv4oal4p-cwq8uICAgICAgICAgICMgTWF4aW1pemUKR0gg4oaQIDIwMCAgICAgICAgICAgICAgICAgICAgIyBHcmFwaCBoZWlnaHQKQyDihpAg4oalwq8x4oanMSAgICAgICAgICAgICAgICAgICAjIENsYW1wClNXIOKGkCDiio_il7_ip7ssICAgICAgICAgICAgICAgICAgICMgU2VsZWN0ICh3cmFwcGVkKQpTSSDihpAgK-KKmcOX4p-cLeKKg-KKgyhTV-KMiikoU1fijIgpKC3iirjijIopICMgU2VsZWN0IChpbnRlcnBvbGF0ZWQpClJJIOKGkCBTScOX4oehOsO3LOKnuywgICAgICAgICAgICAgICMgUmVzaXplIChpbnRlcnBvbGF0ZWQpCgojIEhlbHBlcnMKUm5kSW50IOKGkCArMeKMisOX4pqCICMgUmFuZG9tIGludGVnZXIgMS1OIFtNYXhdCgojIE9wZXJhdGlvbnMKUGhzIOKGkCDihrvigYXDl-KnuywgICAgIyBQaGFzZSBzaGlmdCBbQW1vdW50LCBXYXZlXQpaZXIg4oaQIOKGu-KKly_ihqfijLUuLiAgIyBQaGFzZSBzaGlmdCB0byB6ZXJvIHBvaW50IFtXYXZlXQpBdmcg4oaQIMO3Oi8r4p-c4qe7ICAgIyBBdmVyYWdlIFtXYXZlQXJyYXldCkFNIOKGkCDDlysxw7cyw5ctMTogIyBBbXBsdXR1ZGUgbW9kdWxhdGlvbiBbQW1wbGl0dWRlLCBNb2R1bGF0b3IsIENhcnJpZXJdCkZNIOKGkCBTScOXUytPVcOXICAjIEZyZXF1ZW5jeSBtb2R1bGF0aW9uIFtBbXBsaXR1ZGUsIE1vZHVsYXRvciwgQ2Fycmllcl0KCiMgQmFzaWMgd2F2ZXMKU2luIOKGkCDiiL_DlzpPVCAgICAgIyBTaW5lIHdhdmUgW0hhcm1vbmljXQpTYXcg4oaQIFVT4pe_McOXOk9VICAjIFNhdyB3YXZlIFtIYXJtb25pY10KUHVsIOKGkCBVU-KJpFVTOlNhdyAjIFB1bHNlIHdhdmUgW0hhcm1vbmljLCBEdXR5XQpTcXIg4oaQIFB1bDowLjUgICAjIFNxdWFyZSB3YXZlIFtIYXJtb25pY10KCiMgVXRpbGl0eQpQcmludCDihpAgJnBmCkV4cG9ydCDihpAgJmFwIEMKVGVzdCDihpAgJmFwIFUzUiBNIEMKR3JhcGgg4oaQICZpbXPih4zirJox4oqe4omg4oehR0jigYXDly0xR0ggU1UgVTJDCkltYWdlIOKGkCAmaW1z4oavT19TIFNVIFIgQwpJbXBvcnRXYXZlIOKGkCBSSSBT4peM4peMwrBhdWRpbyZmcmFiCkltcG9ydEltYWdlIOKGkCBSSSBP4omhUkkgU-KNnOKNieKJoSjDtzMvK-KGmTMpIOKXjMKwaW1nICZmcmFiCkltYWdlVG9UYWJsZSDihpAg4pmtVVMKVGFibGVBdmVyYWdlIOKGkCDDt08vK-KGr09fUyAgICAgIyBUYWJsZSB0byB3YXZlClRhYmxlSW5kZXgg4oaQIOKKoeKGpy0xT-KGpTDigYU64oavT19TOiAjIFRhYmxlIHRvIHdhdmUgW0luZGV4ICgwLTI1NSksIFRhYmxlXQoKIyBTYW1wbGUgd2F2ZWZvcm1zCldhdmXigoEg4oaQIEF2Z1tTaW4zIMOXMC43U2luMiBTaW4xXQpXYXZl4oKCIOKGkCBBTTAuNFNpbjcgQU0wLjVTaW4yIFNpbjEKV2F2ZeKCgyDihpAgQXZnW1NhdzEgUHVsMuKImDAuMl0KV2F2ZeKChCDihpAgRk0wLjFTaW43IFNpbjEKClByaW50ICJXYXZl4oKEIgpFeHBvcnQgV2F2ZeKChApUZXN0IFdhdmXigoQKR3JhcGggV2F2ZeKChApJbWFnZSBXYXZl4oKECg==

S ← 2048                    # Samples per oscillator
O ← 256                     # Oscillators per table
T ← ×S O                    # Samples per table
SU ← ÷2+1                   # Signed to unsigned
US ← -1×2                   # Unsigned to signed
OU ← ÷⟜⇡S                   # Oscillator base, unsigned 0-1
OT ← ×τOU                   # Oscillator base, trigonometric 0-2π
R ← ↯¤T                     # Oscillator to table (repeat)
U ← ⍥(⊢⍉↯⊟:⟜÷2⊸⧻)           # Octave up (for playback only)
M ← ◌⍥⟜÷≠0.↥∩/↥⟜¯.          # Maximize
GH ← 200                    # Graph height
C ← ↥¯1↧1                   # Clamp
SW ← ⊏◿⧻,                   # Select (wrapped)
SI ← +⊙×⟜-⊃⊃(SW⌊)(SW⌈)(-⊸⌊) # Select (interpolated)
RI ← SI×⇡:÷,⧻,              # Resize (interpolated)

# Helpers
RndInt ← +1⌊×⚂ # Random integer 1-N [Max]

# Operations
Phs ← ↻⁅×⧻,    # Phase shift [Amount, Wave]
Zer ← ↻⊗/↧⌵..  # Phase shift to zero point [Wave]
Avg ← ÷:/+⟜⧻   # Average [WaveArray]
AM ← ×+1÷2×-1: # Amplutude modulation [Amplitude, Modulator, Carrier]
FM ← SI×S+OU×  # Frequency modulation [Amplitude, Modulator, Carrier]

# Basic waves
Sin ← ∿×:OT     # Sine wave [Harmonic]
Saw ← US◿1×:OU  # Saw wave [Harmonic]
Pul ← US≤US:Saw # Pulse wave [Harmonic, Duty]
Sqr ← Pul:0.5   # Square wave [Harmonic]

# Utility
Print ← &pf
Export ← &ap C
Test ← &ap U3R M C
Graph ← &ims⇌⬚1⊞≠⇡GH⁅×-1GH SU U2C
Image ← &ims↯O_S SU R C
ImportWave ← RI S◌◌°audio&frab
ImportImage ← RI O≡RI S⍜⍉≡(÷3/+↙3) ◌°img &frab
ImageToTable ← ♭US
TableAverage ← ÷O/+↯O_S     # Table to wave
TableIndex ← ⊡↧-1O↥0⁅:↯O_S: # Table to wave [Index (0-255), Table]

# Sample waveforms
Wave₁ ← Avg[Sin3 ×0.7Sin2 Sin1]
Wave₂ ← AM0.4Sin7 AM0.5Sin2 Sin1
Wave₃ ← Avg[Saw1 Pul2∘0.2]
Wave₄ ← FM0.1Sin7 Sin1

Print "Wave₄"
Export Wave₄
Test Wave₄
Graph Wave₄
Image Wave₄
