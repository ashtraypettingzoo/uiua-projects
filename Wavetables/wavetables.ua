# Uiua 0.13.0-dev.2
# https://uiua.org/pad?src=0_13_0-dev_2__IyBVaXVhIDAuMTMuMC1kZXYuMgojIFt1cmxdCgpTIOKGkCAyMDQ4ICAgICAgICAgICAgICAgICAgICAjIFNhbXBsZXMgcGVyIG9zY2lsbGF0b3IKTyDihpAgMjU2ICAgICAgICAgICAgICAgICAgICAgIyBPc2NpbGxhdG9ycyBwZXIgdGFibGUKVCDihpAgw5dTIE8gICAgICAgICAgICAgICAgICAgICMgU2FtcGxlcyBwZXIgdGFibGUKU1Ug4oaQIMO3MisxICAgICAgICAgICAgICAgICAgICMgU2lnbmVkIHRvIHVuc2lnbmVkClVTIOKGkCAtMcOXMiAgICAgICAgICAgICAgICAgICAjIFVuc2lnbmVkIHRvIHNpZ25lZApPVSDihpAgw7fin5zih6FTICAgICAgICAgICAgICAgICAgICMgT3NjaWxsYXRvciBiYXNlLCB1bnNpZ25lZCAwLTEKT1Qg4oaQIMOXz4RPVSAgICAgICAgICAgICAgICAgICAjIE9zY2lsbGF0b3IgYmFzZSwgdHJpZ29ub21ldHJpYyAwLTLPgApSIOKGkCDihq_CpFQgICAgICAgICAgICAgICAgICAgICAjIE9zY2lsbGF0b3IgdG8gdGFibGUgKHJlcGVhdCkKVSDihpAg4o2lKOKKouKNieKGr-KKnzrin5zDtzLiirjip7spICAgICAgICAgICAjIE9jdGF2ZSB1cCAoZm9yIHBsYXliYWNrIG9ubHkpCk0g4oaQIOKXjOKNpeKfnMO34omgMC7ihqXiiKkv4oal4p-cwq8uICAgICAgICAgICMgTWF4aW1pemUKR0gg4oaQIDIwMCAgICAgICAgICAgICAgICAgICAgIyBHcmFwaCBoZWlnaHQKQyDihpAg4oalwq8x4oanMSAgICAgICAgICAgICAgICAgICAjIENsYW1wClNXIOKGkCDiio_il7_ip7ssICAgICAgICAgICAgICAgICAgICMgU2VsZWN0ICh3cmFwcGVkKQpTSSDihpAgK-KKmcOX4p-cLeKKg-KKgyhTV-KMiikoU1fijIgpKC3iirjijIopICMgU2VsZWN0IChpbnRlcnBvbGF0ZWQpClJJIOKGkCBTScOX4oehOsO3LOKnuywgICAgICAgICAgICAgICMgUmVzaXplIChpbnRlcnBvbGF0ZWQpCgojIEhlbHBlcnMKUm5kSW50IOKGkCArMeKMisOX4pqCICMgUmFuZG9tIGludGVnZXIgMS1OIFtNYXhdCgojIE9wZXJhdGlvbnMKUGhzIOKGkCDihrvigYXDl-KnuywgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFBoYXNlIHNoaWZ0IFtBbW91bnQsIFdhdmVdClplciDihpAg4oa74oqXL-KGp-KMtS4uICAgICAgICAgICAgICAgICAgICAgICAgICMgUGhhc2Ugc2hpZnQgdG8gemVybyBwb2ludCBbV2F2ZV0KQXZnIOKGkCDDtzovK-KfnOKnuyAgICAgICAgICAgICAgICAgICAgICAgICAgIyBBdmVyYWdlIFtXYXZlQXJyYXldCkJpdCDihpAgVVPihqcxw7ctMTrijIrDlyxTVTrigYXigb86Misxw5cxNS06MeKImuKGpzHihqUwICMgQml0Y3J1c2ggW0Ftb3VudCAoMC0xKSwgV2F2ZV0KQU0g4oaQIMOXKzHDtzLDly0xOiAgICAgICAgICAgICAgICAgICAgICAgICMgQW1wbHV0dWRlIG1vZHVsYXRpb24gW0FtcGxpdHVkZSwgTW9kdWxhdG9yLCBDYXJyaWVyXQpGTSDihpAgU0nDl1MrT1XDlyAgICAgICAgICAgICAgICAgICAgICAgICAjIEZyZXF1ZW5jeSBtb2R1bGF0aW9uIFtBbXBsaXR1ZGUsIE1vZHVsYXRvciwgQ2Fycmllcl0KQ252IOKGkCAvK8OX4o2J4per4oqZKOKGr-KZrSk6LTHiirgr4qe7LOKnu-KfnDogICAgICAgICAgICAjIENvbnZvbHZlIFtDb252b2x1dGlvbiwgV2F2ZV0KUmV2IOKGkCDih4wgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBSZXZlcnNlIFtXYXZlXQpJbnYg4oaQIMKvICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgSW52ZXJ0IFtXYXZlXQpNaXIg4oaQIOKKgi7ihpnDtzJTICAgICAgICAgICAgICAgICAgICAgICAgICAjIE1pcnJvciAxc3QgaGFsZiBbV2F2ZV0KRmFkIOKGkCDih4zirJoxw5fih4w64qyaMeKfnMOXw7fip7su4oeh4oyKw7cyw5dT4oanMeKGpTAgICAgICAgICAjIEZhZGUgaW4gJiBvdXQgW0Ftb3VudCAoMC0xKSwgV2F2ZV0KCiMgQmFzaWMgd2F2ZXMKU2luIOKGkCDiiL_DlzpPVCAgICAgIyBTaW5lIHdhdmUgW0hhcm1vbmljXQpTYXcg4oaQIFVT4pe_McOXOk9VICAjIFNhdyB3YXZlIFtIYXJtb25pY10KUHVsIOKGkCBVU-KJpFVTOlNhdyAjIFB1bHNlIHdhdmUgW0hhcm1vbmljLCBEdXR5XQpTcXIg4oaQIFB1bDowLjUgICAjIFNxdWFyZSB3YXZlIFtIYXJtb25pY10KCiMgVXRpbGl0eQpQcmludCDihpAgJnBmCkV4cG9ydCDihpAgJmFwIEMKVGVzdCDihpAgJmFwIFUzUiBNIEMKR3JhcGgg4oaQICZpbXPih4zirJox4oqe4omg4oehR0jigYXDly0xR0ggU1UgVTJDCkltYWdlIOKGkCAmaW1z4oavT19TIFNVIFIgQwpJbXBvcnRXYXZlIOKGkCBSSSBT4peM4peMwrBhdWRpbyZmcmFiCkltcG9ydEltYWdlIOKGkCBSSSBP4omhUkkgU-KNnOKNieKJoSjDtzMvK-KGmTMpIOKXjMKwaW1nICZmcmFiCkltYWdlVG9UYWJsZSDihpAg4pmtVVMKVGFibGVBdmVyYWdlIOKGkCDDt08vK-KGr09fUyAgICAgIyBUYWJsZSB0byB3YXZlIFtUYWJsZV0KVGFibGVJbmRleCDihpAg4oqh4oanLTFP4oalMOKBhTrihq9PX1M6ICMgVGFibGUgdG8gd2F2ZSBbSW5kZXggKDAtMjU1KSwgVGFibGVdCgojIFNhbXBsZSB3YXZlZm9ybXMKV2F2ZeKCgSDihpAgQXZnW1NpbjMgw5cwLjdTaW4yIFNpbjFdCldhdmXigoIg4oaQIEFNMC40U2luNyBBTTAuNVNpbjIgU2luMQpXYXZl4oKDIOKGkCBBdmdbU2F3MSBQdWwy4oiYMC4yXQpXYXZl4oKEIOKGkCBCaXQwLjcgRk0wLjFTaW43IFNpbjEKClByaW50ICJXYXZl4oKEIgpFeHBvcnQgV2F2ZeKChApUZXN0IFdhdmXigoQKR3JhcGggV2F2ZeKChApJbWFnZSBXYXZl4oKECg==

S ← 2048                    # Samples per oscillator
O ← 256                     # Oscillators per table
T ← ×S O                    # Samples per table
SU ← ÷2+1                   # Signed to unsigned
US ← -1×2                   # Unsigned to signed
OU ← ÷⟜⇡S                   # Oscillator base, unsigned 0-1
OT ← ×τOU                   # Oscillator base, trigonometric 0-2π
R ← ↯¤T                     # Oscillator to table (repeat)
U ← ⍥(⊢⍉↯⊟:⟜÷2⊸⧻)           # Octave up (for playback only)
M ← ◌⍥⟜÷≠0.↥∩/↥⟜¯.          # Maximize
GH ← 200                    # Graph height
C ← ↥¯1↧1                   # Clamp
SW ← ⊏◿⧻,                   # Select (wrapped)
SI ← +⊙×⟜-⊃⊃(SW⌊)(SW⌈)(-⊸⌊) # Select (interpolated)
RI ← SI×⇡:÷,⧻,              # Resize (interpolated)

# Helpers
RndInt ← +1⌊×⚂ # Random integer 1-N [Max]

# Operations
Phs ← ↻⁅×⧻,                           # Phase shift [Amount, Wave]
Zer ← ↻⊗/↧⌵..                         # Phase shift to zero point [Wave]
Avg ← ÷:/+⟜⧻                          # Average [WaveArray]
Bit ← US↧1÷-1:⌊×,SU:⁅ⁿ:2+1×15-:1√↧1↥0 # Bitcrush [Amount (0-1), Wave]
AM ← ×+1÷2×-1:                        # Amplutude modulation [Amplitude, Modulator, Carrier]
FM ← SI×S+OU×                         # Frequency modulation [Amplitude, Modulator, Carrier]
Cnv ← /+×⍉◫⊙(↯♭):-1⊸+⧻,⧻⟜:            # Convolve [Convolution, Wave]
Rev ← ⇌                               # Reverse [Wave]
Inv ← ¯                               # Invert [Wave]
Mir ← ⊂.↙÷2S                          # Mirror 1st half [Wave]
Fad ← ⇌⬚1×⇌:⬚1⟜×÷⧻.⇡⌊÷2×S↧1↥0         # Fade in & out [Amount (0-1), Wave]

# Basic waves
Sin ← ∿×:OT     # Sine wave [Harmonic]
Saw ← US◿1×:OU  # Saw wave [Harmonic]
Pul ← US≤US:Saw # Pulse wave [Harmonic, Duty]
Sqr ← Pul:0.5   # Square wave [Harmonic]

# Utility
Print ← &pf
Export ← &ap C
Test ← &ap U3R M C
Graph ← &ims⇌⬚1⊞≠⇡GH⁅×-1GH SU U2C
Image ← &ims↯O_S SU R C
ImportWave ← RI S◌◌°audio&frab
ImportImage ← RI O≡RI S⍜⍉≡(÷3/+↙3) ◌°img &frab
ImageToTable ← ♭US
TableAverage ← ÷O/+↯O_S     # Table to wave [Table]
TableIndex ← ⊡↧-1O↥0⁅:↯O_S: # Table to wave [Index (0-255), Table]

# Sample waveforms
Wave₁ ← Avg[Sin3 ×0.7Sin2 Sin1]
Wave₂ ← AM0.4Sin7 AM0.5Sin2 Sin1
Wave₃ ← Avg[Saw1 Pul2∘0.2]
Wave₄ ← Bit0.7 FM0.1Sin7 Sin1

Print "Wave₄"
Export Wave₄
Test Wave₄
Graph Wave₄
Image Wave₄
