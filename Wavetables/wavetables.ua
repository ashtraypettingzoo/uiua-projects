# Uiua 0.13.0-dev.2
# https://uiua.org/pad?src=0_13_0-dev_2__IyBVaXVhIDAuMTMuMC1kZXYuMgojIFt1cmxdCgpTIOKGkCAyMDQ4ICAgICAgICAgICAgICAgICAgICAjIFNhbXBsZXMgcGVyIG9zY2lsbGF0b3IKTyDihpAgMjU2ICAgICAgICAgICAgICAgICAgICAgIyBPc2NpbGxhdG9ycyBwZXIgdGFibGUKVCDihpAgw5dTIE8gICAgICAgICAgICAgICAgICAgICMgU2FtcGxlcyBwZXIgdGFibGUKU1Ug4oaQIMO3MisxICAgICAgICAgICAgICAgICAgICMgU2lnbmVkIHRvIHVuc2lnbmVkClVTIOKGkCAtMcOXMiAgICAgICAgICAgICAgICAgICAjIFVuc2lnbmVkIHRvIHNpZ25lZApPVSDihpAgw7fin5zih6FTICAgICAgICAgICAgICAgICAgICMgT3NjaWxsYXRvciBiYXNlLCB1bnNpZ25lZCAwLTEKT1Qg4oaQIMOXz4RPVSAgICAgICAgICAgICAgICAgICAjIE9zY2lsbGF0b3IgYmFzZSwgdHJpZ29ub21ldHJpYyAwLTLPgApSIOKGkCDihq_CpFQgICAgICAgICAgICAgICAgICAgICAjIE9zY2lsbGF0b3IgdG8gdGFibGUgKHJlcGVhdCkKVSDihpAg4o2lKOKKouKNieKGr-KKnzrin5zDtzLiirjip7spICAgICAgICAgICAjIE9jdGF2ZSB1cCAoZm9yIHBsYXliYWNrIG9ubHkpCkdIIOKGkCAyMDAgICAgICAgICAgICAgICAgICAgICMgR3JhcGggaGVpZ2h0CkMg4oaQIOKGpcKvMeKGpzEgICAgICAgICAgICAgICAgICAgIyBDbGFtcApTVyDihpAg4oqP4pe_4qe7LCAgICAgICAgICAgICAgICAgICAjIFNlbGVjdCAod3JhcHBlZCkKU0kg4oaQICviipnDl-KfnC3iioPiioMoU1fijIopKFNX4oyIKSgt4oq44oyKKSAjIFNlbGVjdCAoaW50ZXJwb2xhdGVkKQpSSSDihpAgU0nDl-KHoTrDtyzip7ssICAgICAgICAgICAgICAjIFJlc2l6ZSAoaW50ZXJwb2xhdGVkKQpDUyDihpAg4oi_K863ICAgICAgICAgICAgICAgICAgICAjIENvc2luZQpOUiDihpAg4oan4oq44oalICAgICAgICAgICAgICAgICAgICAjIE5hTiByZXBsYWNlClNDIOKGkCBOUjHDt-KfnOKIvyAgICAgICAgICAgICAgICAgIyBTaW5jClZCIOKGkCDDty0x4p-c4oehTyAgICAgICAgICAgICAgICAgIyBWYWx1ZSBiYXNlClZSIOKGkCAr4oqZw5c6LeKfnDogICAgICAgICAgICAgICAgIyBWYWx1ZSByYW5nZSBbbWluLCBtYXgsIHZhbHVlc10KR0wg4oaQIDE2ICAgICAgICAgICAgICAgICAgICAgIyBHSUYgbGVuZ3RoIChmcmFtZXMpClLigoAg4oaQIOKYhzAgICAgICAgICAgICAgICAgICAgICAjIFJlcmFuayB6ZXJvClLigoEg4oaQIOKYhzEgICAgICAgICAgICAgICAgICAgICAjIFJlcmFuayBvbmUKViEg4oaQIFLigoHiiaFeIVLigoAKVlYhIOKGkCBS4oKB4omhXiFS4oKA4oqZUuKCgApBISDihpAgUuKCgeKJoV4hUuKCgQpBQSEg4oaQIFLigoHiiaFeIVLigoHiiplS4oKBClZBISDihpAgUuKCgeKJoV4hUuKCgOKKmVLigoEKVkFBISDihpAgUuKCgeKJoV4hUuKCgOKKmVLigoHiipniiplS4oKBCgojIENvbnZvbHV0aW9ucwpDSWQg4oaQIMKkwqQxICMgSWRlbnRpdHkgKGRlbHRhKSBjb252b2x1dGlvbgoKIyBUYWJsZSB2YWx1ZXMKVmFsIOKGkCBWUuKKmeKKmShWQikgICAgICMgVmFsdWVzIChsaW5lYXIpIFttaW4sIG1heF0KVmFsUEkg4oaQIFZS4oqZ4oqZKOKBvzpWQikgIyBWYWx1ZXMgKHBvd2VyIGluKSBbbWluLCBtYXgsIGJhc2VdClZhbFBPIOKGkCDih4xWYWxQSTogICAgIyBWYWx1ZXMgKHBvd2VyIG91dCkgW21pbiwgbWF4LCBiYXNlXQoKIyBIZWxwZXJzClJuZEludCDihpAgKzHijIrDl-KagiAjIFJhbmRvbSBpbnRlZ2VyIDEtTiBbTWF4XQoKIyBPcGVyYXRpb25zClN1bSDihpAgwrDilqEvK-KNmlLigoEgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFN1bSBbV2F2ZXMvVGFibGVzXQpBdmcg4oaQIFN1bcO34oq44qe7ICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgQXZlcmFnZSBbV2F2ZXMvVGFibGVzXQpBbXAg4oaQIFZBIcOXICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBBbXBsaWZ5IFtBbW91bnQsIFdhdmUvVGFibGVdCk1heCDihpAgQSEo4peM4o2l4p-cw7fiiaAwLuKGpeKIqS_ihqXin5zCry4pICAgICAgICAgICAgICAgICAgICMgTWF4aW1pemUgW1dhdmUvVGFibGVdClBocyDihpAgVkEhKOKGu-KBhcOX4qe7LCkgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFBoYXNlIHNoaWZ0IFtBbW91bnQocyksIFdhdmUvVGFibGVdClplciDihpAgQSEo4oa74oqXL-KGp-KMtS4uKSAgICAgICAgICAgICAgICAgICAgICAgICAgIyBQaGFzZSBzaGlmdCB0byB6ZXJvIHBvaW50IFtXYXZlL1RhYmxlXQpCaXQg4oaQIFZBIShVU-KGpzHDty0xOuKMisOXLFNVOuKBheKBvzoyKzHDlzE1LTox4oia4oanMeKGpTApICMgQml0Y3J1c2ggW0Ftb3VudCAoMC0xKSwgV2F2ZS9UYWJsZV0KUmV2IOKGkCBBIeKHjCAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFJldmVyc2UgW1dhdmUvVGFibGVdCkludiDihpAgQSHCryAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIEludmVydCBbV2F2ZS9UYWJsZV0KTWlyIOKGkCBBISjiioLih4wu4oaZw7cyUykgICAgICAgICAgICAgICAgICAgICAgICAgICMgTWlycm9yIDFzdCBoYWxmIFtXYXZlL1RhYmxlXQpGYWQg4oaQIFZBISjih4zirJoxw5fih4w64qyaMeKfnMOXw7fip7su4oeh4oyKw7cyw5dT4oalMCkgICAgICAgICAgICMgRmFkZSBpbiAmIG91dCBbQW1vdW50ICgwLTEpLCBXYXZlL1RhYmxlXQpBTSDihpAgVkFBISjDlysxw7cyw5ctMTopICAgICAgICAgICAgICAgICAgICAgICAjIEFtcGx1dHVkZSBtb2R1bGF0aW9uIFtBbXBsaXR1ZGUocyksIE1vZHVsYXRvcihzKSwgQ2FycmllcihzKV0KRk0g4oaQIFZBQSEoU0nDl1MrT1XDlykgICAgICAgICAgICAgICAgICAgICAgICAjIEZyZXF1ZW5jeSBtb2R1bGF0aW9uIFtBbXBsaXR1ZGUocyksIE1vZHVsYXRvcihzKSwgQ2FycmllcihzKV0KQ252IOKGkCBBQSEoLyvDl-KNieKXq-KKmSjihq_ima0pOi0x4oq4K-Knuyzip7vin5w6KSAgICAgICAgICAgICMgQ29udm9sdmUgW0ltcHVsc2UocyksIFdhdmUvVGFibGVdCgojIEJhc2ljIHdhdmVmb3JtcwpTaW4g4oaQIFYhKOKIv8OXOk9UKSAgICAgICAgICAgICAjIFNpbmUgd2F2ZSBbSGFybW9uaWNdClNhdyDihpAgViEoVVPil78xw5c6T1UpICAgICAgICAgICMgU2F3IHdhdmUgW0hhcm1vbmljXQpUcmkg4oaQIFYhKFVT4oy1VVPil78xKzAuMjXCr8OXOk9VKSAjIFRyaWFuZ2xlIHdhdmUgW0hhcm1vbmljXQpQdWwg4oaQIFZWIShVU-KJpFVTOlNhdykgICAgICAgICMgUHVsc2Ugd2F2ZSBbSGFybW9uaWMsIER1dHldClNxciDihpAgViEoUHVsOjAuNSkgICAgICAgICAgICMgU3F1YXJlIHdhdmUgW0hhcm1vbmljXQpOc3cg4oaQICjimIcxVVPijaUo4oqC4pqCKVNbXSkgICAgICAgICMgTm9pc2Ugd2F2ZSBbXQpOc3Qg4oaQICjijaUo4oqCTnN3KU9bXSkgICAgICAgICAgIyBOb2lzZSB0YWJsZSBbXQoKIyBVdGlsaXR5ClByaW50IOKGkCAmcGYKRXhwb3J0IOKGkCAmYXAgQ-KZrQpUZXN0IOKGkCAmYXAgVTNSIEPima0KR3JhcGgg4oaQIOKNouKKoig9MeKnuynilr09MOKHoeKMisO3R0wgT-KJoSjih4zirJox4oqe4omg4oehR0jigYXDly0xR0ggU1UgVTJDKQpJbWFnZSDihpAgJmltc-KGr09fUyBTVSBSIEPima0KSW1wb3J0SW1hZ2Ug4oaQIFJJIE_iiaFSSSBT4o2c4o2J4omhKMO3My8r4oaZMynil4zCsGltZyZmcmFiCkltcG9ydFdhdmUg4oaQIFLigoAgUkkgU-KXjOKXjMKwYXVkaW8mZnJhYgpJbXBvcnRUYWJsZSDihpAg4oavT19TIFJJIFTil4zil4zCsGF1ZGlvJmZyYWIgIyBJbXBvcnQgMjU2LXdhdmUgdGFibGUKVGFibGVBdmVyYWdlIOKGkCBS4oKAw7dPLyvihq9PX1MgICAgICAgICAgICAjIFRhYmxlIHRvIHdhdmUgW1RhYmxlXQpUYWJsZUluZGV4IOKGkCBS4oKA4oqh4oanLTFP4oalMOKBhTrihq9PX1M6ICAgICAgICAjIFRhYmxlIHRvIHdhdmUgW0luZGV4ICgwLTI1NSksIFRhYmxlXQpJbWFnZVRvVGFibGUg4oaQIFVTCkdyYXBoQ29udiDihpAgR3JhcGggQSEo4qyaMMOX4oavUzEpCgojIFNhbXBsZSB3YXZlZm9ybXMvd2F2ZXRhYmxlcwpXYXZl4oKBIOKGkCBBdmd7U2luMyBBbXAwLjdTaW4yIFNpbjF9CldhdmXigoIg4oaQIEFNMC40U2luNyBBTTAuNVNpbjIgU2luMQpXYXZl4oKCIOKGkCBBdmd7U2F3MSAoUHVsIDIgMC4yKX0KV2F2ZeKChCDihpAgQml0MC43IEZNMC4xU2luNyBTaW4xClRibOKCgSDihpAgRk0oVmFsIDAgMSkgU2luMiBTaW4xClRibOKCgiDihpAgTWF4IEF2Z3tBbXAoVmFsIDAgMSlTaW4yIFNpbjF9CgpQcmludCAiVGJs4oKBIgpUZXN0IFRibOKCgQpJbWFnZSBUYmzigoEKR3JhcGggVGJs4oKBCg==

S ← 2048                    # Samples per oscillator
O ← 256                     # Oscillators per table
T ← ×S O                    # Samples per table
SU ← ÷2+1                   # Signed to unsigned
US ← -1×2                   # Unsigned to signed
OU ← ÷⟜⇡S                   # Oscillator base, unsigned 0-1
OT ← ×τOU                   # Oscillator base, trigonometric 0-2π
R ← ↯¤T                     # Oscillator to table (repeat)
U ← ⍥(⊢⍉↯⊟:⟜÷2⊸⧻)           # Octave up (for playback only)
GH ← 200                    # Graph height
C ← ↥¯1↧1                   # Clamp
SW ← ⊏◿⧻,                   # Select (wrapped)
SI ← +⊙×⟜-⊃⊃(SW⌊)(SW⌈)(-⊸⌊) # Select (interpolated)
RI ← SI×⇡:÷,⧻,              # Resize (interpolated)
CS ← ∿+η                    # Cosine
NR ← ↧⊸↥                    # NaN replace
SC ← NR1÷⟜∿                 # Sinc
VB ← ÷-1⟜⇡O                 # Value base
VR ← +⊙×:-⟜:                # Value range [min, max, values]
GL ← 16                     # GIF length (frames)
R₀ ← ☇0                     # Rerank zero
R₁ ← ☇1                     # Rerank one
V! ← R₁≡^!R₀
VV! ← R₁≡^!R₀⊙R₀
A! ← R₁≡^!R₁
AA! ← R₁≡^!R₁⊙R₁
VA! ← R₁≡^!R₀⊙R₁
VAA! ← R₁≡^!R₀⊙R₁⊙⊙R₁

# Convolutions
CId ← ¤¤1 # Identity (delta) convolution

# Table values
Val ← VR⊙⊙(VB)     # Values (linear) [min, max]
ValPI ← VR⊙⊙(ⁿ:VB) # Values (power in) [min, max, base]
ValPO ← ⇌ValPI:    # Values (power out) [min, max, base]

# Helpers
RndInt ← +1⌊×⚂ # Random integer 1-N [Max]

# Operations
Sum ← °□/+⍚R₁                              # Sum [Waves/Tables]
Avg ← Sum÷⊸⧻                               # Average [Waves/Tables]
Amp ← VA!×                                 # Amplify [Amount, Wave/Table]
Max ← A!(◌⍥⟜÷≠0.↥∩/↥⟜¯.)                   # Maximize [Wave/Table]
Phs ← VA!(↻⁅×⧻,)                           # Phase shift [Amount(s), Wave/Table]
Zer ← A!(↻⊗/↧⌵..)                          # Phase shift to zero point [Wave/Table]
Bit ← VA!(US↧1÷-1:⌊×,SU:⁅ⁿ:2+1×15-:1√↧1↥0) # Bitcrush [Amount (0-1), Wave/Table]
Rev ← A!⇌                                  # Reverse [Wave/Table]
Inv ← A!¯                                  # Invert [Wave/Table]
Mir ← A!(⊂⇌.↙÷2S)                          # Mirror 1st half [Wave/Table]
Fad ← VA!(⇌⬚1×⇌:⬚1⟜×÷⧻.⇡⌊÷2×S↥0)           # Fade in & out [Amount (0-1), Wave/Table]
AM ← VAA!(×+1÷2×-1:)                       # Amplutude modulation [Amplitude(s), Modulator(s), Carrier(s)]
FM ← VAA!(SI×S+OU×)                        # Frequency modulation [Amplitude(s), Modulator(s), Carrier(s)]
Cnv ← AA!(/+×⍉◫⊙(↯♭):-1⊸+⧻,⧻⟜:)            # Convolve [Impulse(s), Wave/Table]

# Basic waveforms
Sin ← V!(∿×:OT)             # Sine wave [Harmonic]
Saw ← V!(US◿1×:OU)          # Saw wave [Harmonic]
Tri ← V!(US⌵US◿1+0.25¯×:OU) # Triangle wave [Harmonic]
Pul ← VV!(US≤US:Saw)        # Pulse wave [Harmonic, Duty]
Sqr ← V!(Pul:0.5)           # Square wave [Harmonic]
Nsw ← (☇1US⍥(⊂⚂)S[])        # Noise wave []
Nst ← (⍥(⊂Nsw)O[])          # Noise table []

# Utility
Print ← &pf
Export ← &ap C♭
Test ← &ap U3R C♭
Graph ← ⍢⊢(=1⧻)▽=0⇡⌊÷GL O≡(⇌⬚1⊞≠⇡GH⁅×-1GH SU U2C)
Image ← &ims↯O_S SU R C♭
ImportImage ← RI O≡RI S⍜⍉≡(÷3/+↙3)◌°img&frab
ImportWave ← R₀ RI S◌◌°audio&frab
ImportTable ← ↯O_S RI T◌◌°audio&frab # Import 256-wave table
TableAverage ← R₀÷O/+↯O_S            # Table to wave [Table]
TableIndex ← R₀⊡↧-1O↥0⁅:↯O_S:        # Table to wave [Index (0-255), Table]
ImageToTable ← US
GraphConv ← Graph A!(⬚0×↯S1)

# Sample waveforms/wavetables
Wave₁ ← Avg{Sin3 Amp0.7Sin2 Sin1}
Wave₂ ← AM0.4Sin7 AM0.5Sin2 Sin1
Wave₂ ← Avg{Saw1 (Pul 2 0.2)}
Wave₄ ← Bit0.7 FM0.1Sin7 Sin1
Tbl₁ ← FM(Val 0 1) Sin2 Sin1
Tbl₂ ← Max Avg{Amp(Val 0 1)Sin2 Sin1}

Print "Tbl₁"
Test Tbl₁
Image Tbl₁
Graph Tbl₁
