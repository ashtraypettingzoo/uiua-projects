# Uiua 0.13.0-dev.2
# https://uiua.org/pad?src=0_13_0-dev_2__IyBVaXVhIDAuMTMuMC1kZXYuMgojIFt1cmxdCgpTIOKGkCAyMDQ4ICAgICAgICAgICAgICAgICAgICAjIFNhbXBsZXMgcGVyIG9zY2lsbGF0b3IKTyDihpAgMjU2ICAgICAgICAgICAgICAgICAgICAgIyBPc2NpbGxhdG9ycyBwZXIgdGFibGUKVCDihpAgw5dTIE8gICAgICAgICAgICAgICAgICAgICMgU2FtcGxlcyBwZXIgdGFibGUKU1Ug4oaQIMO3MisxICAgICAgICAgICAgICAgICAgICMgU2lnbmVkIHRvIHVuc2lnbmVkClVTIOKGkCAtMcOXMiAgICAgICAgICAgICAgICAgICAjIFVuc2lnbmVkIHRvIHNpZ25lZApPVSDihpAgw7fin5zih6FTICAgICAgICAgICAgICAgICAgICMgT3NjaWxsYXRvciBiYXNlLCB1bnNpZ25lZCAwLTEKT1Qg4oaQIMOXz4RPVSAgICAgICAgICAgICAgICAgICAjIE9zY2lsbGF0b3IgYmFzZSwgdHJpZ29ub21ldHJpYyAwLTLPgApSIOKGkCDihq_CpFQgICAgICAgICAgICAgICAgICAgICAjIE9zY2lsbGF0b3IgdG8gdGFibGUgKHJlcGVhdCkKVSDihpAg4o2lKOKKouKNieKGr-KKnzrin5zDtzLiirjip7spICAgICAgICAgICAjIE9jdGF2ZSB1cCAoZm9yIHBsYXliYWNrIG9ubHkpCkdIIOKGkCAyMDAgICAgICAgICAgICAgICAgICAgICMgR3JhcGggaGVpZ2h0CkMg4oaQIOKGpcKvMeKGpzEgICAgICAgICAgICAgICAgICAgIyBDbGFtcApTVyDihpAg4oqP4pe_4qe7LCAgICAgICAgICAgICAgICAgICAjIFNlbGVjdCAod3JhcHBlZCkKU0kg4oaQICviipnDl-KfnC3iioPiioMoU1fijIopKFNX4oyIKSgt4oq44oyKKSAjIFNlbGVjdCAoaW50ZXJwb2xhdGVkKQpSSSDihpAgU0nDl-KHoTrDtyzip7ssICAgICAgICAgICAgICAjIFJlc2l6ZSAoaW50ZXJwb2xhdGVkKQpDUyDihpAg4oi_K863ICAgICAgICAgICAgICAgICAgICAjIENvc2luZQpOUiDihpAg4oan4oq44oalICAgICAgICAgICAgICAgICAgICAjIE5hTiByZXBsYWNlClNDIOKGkCBOUjHDt-KfnOKIvyAgICAgICAgICAgICAgICAgIyBTaW5jClZCIOKGkCDDty0x4p-c4oehTyAgICAgICAgICAgICAgICAgIyBWYWx1ZSBiYXNlClZSIOKGkCAr4oqZw5c6LeKfnDogICAgICAgICAgICAgICAgIyBWYWx1ZSByYW5nZSBbbWluLCBtYXgsIHZhbHVlc10KR0wg4oaQIDE2ICAgICAgICAgICAgICAgICAgICAgIyBHSUYgbGVuZ3RoIChmcmFtZXMpClLigoAg4oaQIOKYhzAgICAgICAgICAgICAgICAgICAgICAjIFJlcmFuayB6ZXJvClLigoEg4oaQIOKYhzEgICAgICAgICAgICAgICAgICAgICAjIFJlcmFuayBvbmUKViEg4oaQIFLigoHiiaFeIVLigoAKVlYhIOKGkCBS4oKB4omhXiFS4oKA4oqZUuKCgApWVkEhIOKGkCBS4oKB4omhXiFS4oKA4oqZUuKCgOKKmeKKmVLigoEKVkEhIOKGkCBS4oKB4omhXiFS4oKA4oqZUuKCgQpWQUEhIOKGkCBS4oKB4omhXiFS4oKA4oqZUuKCgeKKmeKKmVLigoEKQSEg4oaQIFLigoHiiaFeIVLigoEKQUEhIOKGkCBS4oKB4omhXiFS4oKB4oqZUuKCgQoKIyBDb252b2x1dGlvbnMKCkNJZCDihpAgwqTCpDEgIyBJZGVudGl0eSAoZGVsdGEpIGNvbnZvbHV0aW9uCgojIFRhYmxlIHZhbHVlcwoKVmFsIOKGkCBWUuKKmeKKmShWQikgICAgICMgVmFsdWVzIChsaW5lYXIpIFttaW4sIG1heF0KVmFsUEkg4oaQIFZS4oqZ4oqZKOKBvzpWQikgIyBWYWx1ZXMgKHBvd2VyIGluKSBbbWluLCBtYXgsIGJhc2VdClZhbFBPIOKGkCDih4xWYWxQSTogICAgIyBWYWx1ZXMgKHBvd2VyIG91dCkgW21pbiwgbWF4LCBiYXNlXQoKIyBIZWxwZXJzCgpSbmRJbnQg4oaQICsx4oyKw5fimoIgIyBSYW5kb20gaW50ZWdlciAxLU4gW01heF0KCiMgT3BlcmF0aW9ucwoKU3VtIOKGkCDCsOKWoS8r4o2aUuKCgSAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFN1bSBbV2F2ZXMvVGFibGVzXQpBdmcg4oaQIFN1bcO34oq44qe7ICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIEF2ZXJhZ2UgW1dhdmVzL1RhYmxlc10KQW1wIOKGkCBWQSHDlyAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIEFtcGxpZnkgW0Ftb3VudCwgV2F2ZS9UYWJsZV0KTWF4IOKGkCBBISjil4zijaXin5zDt-KJoDAu4oal4oipL-KGpeKfnMKvLikgICAgICAgICAgICAgICAgICAgICMgTWF4aW1pemUgW1dhdmUvVGFibGVdClBocyDihpAgVkEhKOKGu-KBhcOX4qe7LCkgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBQaGFzZSBzaGlmdCBbQW1vdW50KHMpLCBXYXZlL1RhYmxlXQpaZXIg4oaQIEEhKOKGu-KKly_ihqfijLUuLikgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFBoYXNlIHNoaWZ0IHRvIHplcm8gcG9pbnQgW1dhdmUvVGFibGVdCkJpdCDihpAgVkEhKFVT4oanMcO3LTE64oyKw5csU1U64oGF4oG_OjIrMcOXMTUtOjHiiJrihqcx4oalMCkgICMgQml0Y3J1c2ggW0Ftb3VudChzKSAoMC0xKSwgV2F2ZS9UYWJsZV0KUmV2IOKGkCBBIeKHjCAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBSZXZlcnNlIFtXYXZlL1RhYmxlXQpJbnYg4oaQIEEhwq8gICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgSW52ZXJ0IFtXYXZlL1RhYmxlXQpNaXIg4oaQIEEhKOKKguKHjC7ihpnDtzJTKSAgICAgICAgICAgICAgICAgICAgICAgICAgICMgTWlycm9yIDFzdCBoYWxmIFtXYXZlL1RhYmxlXQpGYWQg4oaQIFZBISjih4zirJoxw5fih4w64qyaMeKfnMOXw7fip7su4oeh4oyKw7cyw5dT4oalMCkgICAgICAgICAgICAjIEZhZGUgaW4gJiBvdXQgW0Ftb3VudChzKSAoMC0xKSwgV2F2ZS9UYWJsZV0KQ3JzIOKGkCBWQUEhKCviipkow5ctOjEpOuKfnMOX4oqZOikgICAgICAgICAgICAgICAgICAgIyBDcm9zc2ZhZGUgW0Ftb3VudChzKSAoMC0xKSwgU3RhcnQgV2F2ZS9UYWJsZSwgRW5kIFdhdmUvVGFibGVdClN0ciDihpAgVlZBISjiioLiiplSSTotOlPin5xSSeKKmeKKg-KGmeKGmOKIqSjihqctMVPihqUx4oGFw5dT4oanMeKGpTApKSAjIFN0cmV0Y2ggW1RvICgwLTEpLCBGcm9tICgwLTEpLCBXYXZlL1RhYmxlXQpBTSDihpAgVkFBISjDlysxw7cyw5ctMTopICAgICAgICAgICAgICAgICAgICAgICAgIyBBbXBsdXR1ZGUgbW9kdWxhdGlvbiBbQW1wbGl0dWRlKHMpLCBNb2R1bGF0b3IocyksIENhcnJpZXIocyldCkZNIOKGkCBWQUEhKFNJw5dTK09Vw5cpICAgICAgICAgICAgICAgICAgICAgICAgICMgRnJlcXVlbmN5IG1vZHVsYXRpb24gW0FtcGxpdHVkZShzKSwgTW9kdWxhdG9yKHMpLCBDYXJyaWVyKHMpXQpDbnYg4oaQIEFBISgvK8OX4o2J4per4oqZKOKGr-KZrSk6LTHiirgr4qe7LOKnu-KfnDopICAgICAgICAgICAgICMgQ29udm9sdmUgW0ltcHVsc2UocyksIFdhdmUvVGFibGVdCgojIEJhc2ljIHdhdmVmb3JtcwoKU2luIOKGkCBWISjiiL_DlzpPVCkgICAgICAgICAgICAgIyBTaW5lIHdhdmUgW0hhcm1vbmljXQpTYXcg4oaQIFYhKFVT4pe_McOXOk9VKSAgICAgICAgICAjIFNhdyB3YXZlIFtIYXJtb25pY10KVHJpIOKGkCBWIShVU-KMtVVT4pe_MSswLjI1wq_DlzpPVSkgIyBUcmlhbmdsZSB3YXZlIFtIYXJtb25pY10KUHVsIOKGkCBWViEoVVPiiaRVUzpTYXcpICAgICAgICAjIFB1bHNlIHdhdmUgW0hhcm1vbmljLCBEdXR5XQpTcXIg4oaQIFYhKFB1bDowLjUpICAgICAgICAgICAjIFNxdWFyZSB3YXZlIFtIYXJtb25pY10KTnN3IOKGkCAo4piHMVVT4o2lKOKKguKagilTW10pICAgICAgICAjIE5vaXNlIHdhdmUgW10KTnN0IOKGkCAo4o2lKOKKgk5zdylPW10pICAgICAgICAgICMgTm9pc2UgdGFibGUgW10KCiMgVXRpbGl0eQoKUHJpbnQg4oaQICZwZgpFeHBvcnQg4oaQICZhcCBD4pmtClRlc3Qg4oaQICZhcCBVM1IgQ-KZrQpHcmFwaCDihpAg4o2i4oqiKD0x4qe7KeKWvT0w4oeh4oyKw7dHTCBP4omhKOKHjOKsmjHiip7iiaDih6FHSOKBhcOXLTFHSCBTVSBVMkMpCkltYWdlIOKGkCAmaW1z4oavT19TIFNVIFIgQ-KZrQpJbXBvcnRJbWFnZSDihpAgUkkgT-KJoVJJIFPijZzijYniiaEow7czLyvihpkzKeKXjMKwaW1nJmZyYWIKSW1wb3J0V2F2ZSDihpAgUuKCgCBSSSBT4peM4peMwrBhdWRpbyZmcmFiCkltcG9ydFRhYmxlIOKGkCDihq9PX1MgUkkgVOKXjOKXjMKwYXVkaW8mZnJhYiAjIEltcG9ydCAyNTYtd2F2ZSB0YWJsZQpUYWJsZUF2ZXJhZ2Ug4oaQIFLigoDDt08vK-KGr09fUyAgICAgICAgICAgICMgVGFibGUgdG8gd2F2ZSBbVGFibGVdClRhYmxlSW5kZXgg4oaQIFLigoDiiqHihqctMU_ihqUw4oGFOuKGr09fUzogICAgICAgICMgVGFibGUgdG8gd2F2ZSBbSW5kZXggKDAtMjU1KSwgVGFibGVdCkltYWdlVG9UYWJsZSDihpAgVVMKR3JhcGhDb252IOKGkCBHcmFwaCBBISjirJoww5fihq9TMSkKCiMgU2FtcGxlIHdhdmVmb3Jtcy93YXZldGFibGVzCgpXYXZl4oKBIOKGkCBBdmd7U2luMyBBbXAwLjdTaW4yIFNpbjF9CldhdmXigoIg4oaQIEFNMC40U2luNyBBTTAuNVNpbjIgU2luMQpXYXZl4oKCIOKGkCBBdmd7U2F3MSAoUHVsIDIgMC4yKX0KV2F2ZeKChCDihpAgQml0MC43IEZNMC4xU2luNyBTaW4xClRibOKCgSDihpAgRk0oVmFsIDAgMSkgU2luMiBTaW4xClRibOKCgiDihpAgTWF4IEF2Z3tBbXAoVmFsIDAgMSlTaW4yIFNpbjF9ClRibOKCgyDihpAgQml0KFZhbCAwIDEpIENycyhWYWxQSSAwIDEgMikgU2luMSBTaW4zCgpQcmludCAiVGJs4oKDIgpUZXN0IFRibOKCgwpJbWFnZSBUYmzigoMKR3JhcGggVGJs4oKDCg==

S ← 2048                    # Samples per oscillator
O ← 256                     # Oscillators per table
T ← ×S O                    # Samples per table
SU ← ÷2+1                   # Signed to unsigned
US ← -1×2                   # Unsigned to signed
OU ← ÷⟜⇡S                   # Oscillator base, unsigned 0-1
OT ← ×τOU                   # Oscillator base, trigonometric 0-2π
R ← ↯¤T                     # Oscillator to table (repeat)
U ← ⍥(⊢⍉↯⊟:⟜÷2⊸⧻)           # Octave up (for playback only)
GH ← 200                    # Graph height
C ← ↥¯1↧1                   # Clamp
SW ← ⊏◿⧻,                   # Select (wrapped)
SI ← +⊙×⟜-⊃⊃(SW⌊)(SW⌈)(-⊸⌊) # Select (interpolated)
RI ← SI×⇡:÷,⧻,              # Resize (interpolated)
CS ← ∿+η                    # Cosine
NR ← ↧⊸↥                    # NaN replace
SC ← NR1÷⟜∿                 # Sinc
VB ← ÷-1⟜⇡O                 # Value base
VR ← +⊙×:-⟜:                # Value range [min, max, values]
GL ← 16                     # GIF length (frames)
R₀ ← ☇0                     # Rerank zero
R₁ ← ☇1                     # Rerank one
V! ← R₁≡^!R₀
VV! ← R₁≡^!R₀⊙R₀
VVA! ← R₁≡^!R₀⊙R₀⊙⊙R₁
VA! ← R₁≡^!R₀⊙R₁
VAA! ← R₁≡^!R₀⊙R₁⊙⊙R₁
A! ← R₁≡^!R₁
AA! ← R₁≡^!R₁⊙R₁

# Convolutions

CId ← ¤¤1 # Identity (delta) convolution

# Table values

Val ← VR⊙⊙(VB)     # Values (linear) [min, max]
ValPI ← VR⊙⊙(ⁿ:VB) # Values (power in) [min, max, base]
ValPO ← ⇌ValPI:    # Values (power out) [min, max, base]

# Helpers

RndInt ← +1⌊×⚂ # Random integer 1-N [Max]

# Operations

Sum ← °□/+⍚R₁                               # Sum [Waves/Tables]
Avg ← Sum÷⊸⧻                                # Average [Waves/Tables]
Amp ← VA!×                                  # Amplify [Amount, Wave/Table]
Max ← A!(◌⍥⟜÷≠0.↥∩/↥⟜¯.)                    # Maximize [Wave/Table]
Phs ← VA!(↻⁅×⧻,)                            # Phase shift [Amount(s), Wave/Table]
Zer ← A!(↻⊗/↧⌵..)                           # Phase shift to zero point [Wave/Table]
Bit ← VA!(US↧1÷-1:⌊×,SU:⁅ⁿ:2+1×15-:1√↧1↥0)  # Bitcrush [Amount(s) (0-1), Wave/Table]
Rev ← A!⇌                                   # Reverse [Wave/Table]
Inv ← A!¯                                   # Invert [Wave/Table]
Mir ← A!(⊂⇌.↙÷2S)                           # Mirror 1st half [Wave/Table]
Fad ← VA!(⇌⬚1×⇌:⬚1⟜×÷⧻.⇡⌊÷2×S↥0)            # Fade in & out [Amount(s) (0-1), Wave/Table]
Crs ← VAA!(+⊙(×-:1):⟜×⊙:)                   # Crossfade [Amount(s) (0-1), Start Wave/Table, End Wave/Table]
Str ← VVA!(⊂⊙RI:-:S⟜RI⊙⊃↙↘∩(↧-1S↥1⁅×S↧1↥0)) # Stretch [To (0-1), From (0-1), Wave/Table]
AM ← VAA!(×+1÷2×-1:)                        # Amplutude modulation [Amplitude(s), Modulator(s), Carrier(s)]
FM ← VAA!(SI×S+OU×)                         # Frequency modulation [Amplitude(s), Modulator(s), Carrier(s)]
Cnv ← AA!(/+×⍉◫⊙(↯♭):-1⊸+⧻,⧻⟜:)             # Convolve [Impulse(s), Wave/Table]

# Basic waveforms

Sin ← V!(∿×:OT)             # Sine wave [Harmonic]
Saw ← V!(US◿1×:OU)          # Saw wave [Harmonic]
Tri ← V!(US⌵US◿1+0.25¯×:OU) # Triangle wave [Harmonic]
Pul ← VV!(US≤US:Saw)        # Pulse wave [Harmonic, Duty]
Sqr ← V!(Pul:0.5)           # Square wave [Harmonic]
Nsw ← (☇1US⍥(⊂⚂)S[])        # Noise wave []
Nst ← (⍥(⊂Nsw)O[])          # Noise table []

# Utility

Print ← &pf
Export ← &ap C♭
Test ← &ap U3R C♭
Graph ← ⍢⊢(=1⧻)▽=0⇡⌊÷GL O≡(⇌⬚1⊞≠⇡GH⁅×-1GH SU U2C)
Image ← &ims↯O_S SU R C♭
ImportImage ← RI O≡RI S⍜⍉≡(÷3/+↙3)◌°img&frab
ImportWave ← R₀ RI S◌◌°audio&frab
ImportTable ← ↯O_S RI T◌◌°audio&frab # Import 256-wave table
TableAverage ← R₀÷O/+↯O_S            # Table to wave [Table]
TableIndex ← R₀⊡↧-1O↥0⁅:↯O_S:        # Table to wave [Index (0-255), Table]
ImageToTable ← US
GraphConv ← Graph A!(⬚0×↯S1)

# Sample waveforms/wavetables

Wave₁ ← Avg{Sin3 Amp0.7Sin2 Sin1}
Wave₂ ← AM0.4Sin7 AM0.5Sin2 Sin1
Wave₂ ← Avg{Saw1 (Pul 2 0.2)}
Wave₄ ← Bit0.7 FM0.1Sin7 Sin1
Tbl₁ ← FM(Val 0 1) Sin2 Sin1
Tbl₂ ← Max Avg{Amp(Val 0 1)Sin2 Sin1}
Tbl₃ ← Bit(Val 0 1) Crs(ValPI 0 1 2) Sin1 Sin3

Print "Tbl₃"
Test Tbl₃
Image Tbl₃
Graph Tbl₃
