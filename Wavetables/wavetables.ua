# Experimental!
# Uiua 0.13.0-dev.2
# https://github.com/ashtraypettingzoo/uiua-projects/blob/main/Wavetables/wavetables.ua
# https://uiua.org/pad?src=0_13_0-dev_2__IyBFeHBlcmltZW50YWwhCiMgVWl1YSAwLjEzLjAtZGV2LjIKIyBodHRwczovL2dpdGh1Yi5jb20vYXNodHJheXBldHRpbmd6b28vdWl1YS1wcm9qZWN0cy9ibG9iL21haW4vV2F2ZXRhYmxlcy93YXZldGFibGVzLnVhCiMgW3VybF0KCiMgVE9ETzogaGlnaC9sb3cgcGFzcyBmaWx0ZXJzCiMgVE9ETzogSVIgaW1wb3J0aW5nCiMgVE9ETzogZml4IFN0ciAoc3RyZXRjaCkgYnV6emluZwojIFRPRE86IFNpZCAoc2lkZSBieSBzaWRlKQojIFRPRE86IFNocCAod2F2ZXNoYXBlKQojIFRPRE86IEVycm9yIGhhbmRsaW5nCgpTIOKGkCAyMDQ4ICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBTYW1wbGVzIHBlciBvc2NpbGxhdG9yCk8g4oaQIDI1NiAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIE9zY2lsbGF0b3JzIHBlciB0YWJsZQpUIOKGkCDDl1MgTyAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgU2FtcGxlcyBwZXIgdGFibGUKU1Ug4oaQIMO3MisxICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFNpZ25lZCB0byB1bnNpZ25lZApVUyDihpAgLTHDlzIgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgVW5zaWduZWQgdG8gc2lnbmVkCk9VIOKGkCDDt-KfnOKHoVMgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgT3NjaWxsYXRvciBiYXNlLCB1bnNpZ25lZCAwLTEKT1Qg4oaQIMOXz4RPVSAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBPc2NpbGxhdG9yIGJhc2UsIHRyaWdvbm9tZXRyaWMgMC0yz4AKUiDihpAg4oavwqRUICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgT3NjaWxsYXRvciB0byB0YWJsZSAocmVwZWF0KQpVIOKGkCDijaUo4oqi4o2J4oav4oqfOuKfnMO3MuKKuOKnuykgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIE9jdGF2ZSB1cCAoZm9yIHBsYXliYWNrIG9ubHkpCkdIIOKGkCAyMDAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIEdyYXBoIGhlaWdodApDIOKGkCDihqXCrzHihqcxICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIENsYW1wClNXIOKGkCDiio_il7_ip7ssICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFNlbGVjdCAod3JhcHBlZCkKU0kg4oaQICviipnDl-KfnC3iioPiioMoU1fijIopKFNX4oyIKSgt4oq44oyKKSAgICAgICAgICAgICAgICAgICAgIyBTZWxlY3QgKGludGVycG9sYXRlZCkKUkkg4oaQIFNJw5fih6E6w7cs4qe7LCAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgUmVzaXplIChpbnRlcnBvbGF0ZWQpCkNTIOKGkCDiiL8rzrcgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIENvc2luZQpOUiDihpAg4oan4oq44oalICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBOYU4gcmVwbGFjZQpTQyDihpAgTlIxw7fin5ziiL8gICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFNpbmMKVkIg4oaQIMO3LTHin5zih6FPICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBWYWx1ZSBiYXNlClZSIOKGkCAr4oqZw5c6LeKfnDogICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgVmFsdWUgcmFuZ2UgW21pbiwgbWF4LCB2YWx1ZXNdCkdMIOKGkCAxNiAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIEdJRiBsZW5ndGggKGZyYW1lcykKU0gg4oaQIDIwMCAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgU3BlY3RydW0gZ3JhcGggaGVpZ2h0CkZWIOKGkCDDtzLiiJpTICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIEZvdXJpZXIgbWF4IHZhbHVlClNWIOKGkCArMeKMisOXw7cyU-KBvzLDt-KfnOKHocO3NFMgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFNwZWN0cnVtIGluZGV4IHZhbHVlcwpJUyDihpAg4qisKCZnaWZzMTV8Jmltcyk9MuKnu-KWsy4gICAgICAgICAgICAgICAgICAgICAgIyBTaG93IGltYWdlIG9yIGdpZgpTTSDihpAg4oeM4oinKOKKgiviirjiioPii4XiiJgow5fiipnii4XiiqIpKeKKmSg6wqTCsOKKgikgICAgICAgICAgICAgICAgICAgIyBTY2FuIGFkZCArIG11bHRpcGx5Ck1CIOKGkCDDtzox4oG_MTYgMiAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIE1pbmltdW0gMTYtYml0IHZhbHVlCkZUIOKGkCDiioMo4oigwrDihIIpKMO3RlbijLUp4oaZKzHDtzJTIGZmdCAgICAgICAgICAgICAgICAgICAgIyBGb3VyaWVyIHRyYW5zZm9ybSBbcGhhc2UsIGFtcCDihpAgd2F2ZV0KRlUg4oaQIOKXjMKw4oSCwrBmZnTiioI64o2cwrDihILCr-KGmDHih4zihpgxLuKEguKKgyjDl-KIvykow5dDUyniipkow5dGVikgICAgICAjIEZvdXJpZXIgdW50cmFuc2Zvcm0gW3dhdmUg4oaQIHBoYXNlLCBhbXBdCkRTIOKGkCB8MSDijaUo4oqCOiIvIik-MOKnuy4gICAgICAgICAgICAgICAgICAgICAgICAgICAjIEFkZCBzbGFzaCB0byBkaXJlY3RvcnkgbmFtZQpDRyDihpAg4o2i4oqiKD0x4qe7KeKJoSjih4zirJox4oqe4omg4oehR0jigYXDly0xR0ggU1UgVTJDKeKWvT0w4oeh4oyKw7dHTCBPICMgQ3JlYXRlIGdyYXBoClNUIOKGkCDiioLihq86QDDihqUwLTriipko4qe7LiQiXyIpICAgICAgICAgICAgICAgICAgICAgICAgIyBOdW0gdG8gc3RyaW5nIHcvIHRyYWlsaW5nIHplcm9lcwpDRSDihpAg4omN4qyaQCDihq_iipk6wqTip7su4oqZ4oeM4oeMICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIENoZWNrIGV4dGVuc2lvbiBbZXh0LCBuYW1lXQpUcnVlIOKGkCAxICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBUcnVlIGNvbnN0YW50CkZhbHNlIOKGkCAwICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIEZhbHNlIGNvbnN0YW50ClLigoAg4oaQIOKYhzAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBSZXJhbmsgemVybwpS4oKBIOKGkCDimIcxICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgUmVyYW5rIG9uZQpWISDihpAgUuKCgeKJoV4hUuKCgApWViEg4oaQIFLigoHiiaFeIVLigoDiiplS4oKAClZWQSEg4oaQIFLigoHiiaFeIVLigoDiiplS4oKA4oqZ4oqZUuKCgQpWQSEg4oaQIFLigoHiiaFeIVLigoDiiplS4oKBClZBQSEg4oaQIFLigoHiiaFeIVLigoDiiplS4oKB4oqZ4oqZUuKCgQpBISDihpAgUuKCgeKJoV4hUuKCgQpBQSEg4oaQIFLigoHiiaFeIVLigoHiiplS4oKBCgojIC0tLS0gQ29udm9sdXRpb25zCiMgSWRlbnRpdHkgKGRlbHRhKSBjb252b2x1dGlvbgpDSWQg4oaQIMKkwqQxCgojIC0tLS0gVGFibGUgdmFsdWVzCiMgVmFsdWVzIChsaW5lYXIpIFttaW4sIG1heF0KVmFsIOKGkCBWUuKKmeKKmShWQikKIyBWYWx1ZXMgKHBvd2VyIGluKSBbbWluLCBtYXgsIGJhc2VdClZhbFBJIOKGkCBWUuKKmeKKmSjigb86VkIpCiMgVmFsdWVzIChwb3dlciBvdXQpIFttaW4sIG1heCwgYmFzZV0KVmFsUE8g4oaQIOKHjFZhbFBJOgoKIyAtLS0tIEhlbHBlcnMKIyBSYW5kb20gKG1pbi1tYXgpIFtNaW4sIE1heF0KUm5kIOKGkCArw5fimoI64p-cLQojIFJhbmRvbSAobWluLW1heCkgW01pbiwgTWF4XQpSbmRJIOKGkCDijIpSbmQ6KzE64o2lOjwsLAoKIyAtLS0tIFdhdmUgT3BlcmF0aW9ucwojIFN1bSBbe1dhdmVzL1RhYmxlc31dClN1bSDihpAgwrDilqEvK-KNmlLigoEKIyBBdmVyYWdlIFt7V2F2ZXMvVGFibGVzfV0KQXZnIOKGkCBTdW3Dt-KKuOKnuwojIEFtcGxpZnkgW0Ftb3VudCwgV2F2ZS9UYWJsZV0KQW1wIOKGkCBWQSHDlwojIE1heGltaXplIFtXYXZlL1RhYmxlXQpNYXgg4oaQIEEhKOKXjOKNpeKfnMO34omgMC7ihqXiiKkv4oal4p-cwq8uKQojIENlbnRlciAoYnkgcGVha3MpIFtXYXZlL1RhYmxlXQpDdHAg4oaQIEEhKC3DtzIrL-KGp-KfnC_ihqUuKQojIENlbnRlciAoYnkgbWVhbikgW1dhdmUvVGFibGVdCkN0bSDihpAgQSEoLcO3Uy8rLikKIyBQaGFzZSBzaGlmdCBbQW1vdW50LCBXYXZlL1RhYmxlXQpQaHMg4oaQIFZBISjihrvigYXDl-KnuywpCiMgUGhhc2Ugc2hpZnQgdG8gemVybyBwb2ludCBbV2F2ZS9UYWJsZV0KWmVyIOKGkCBBISjihrviipcv4oan4oy1Li4pCiMgQml0Y3J1c2ggW0Ftb3VudCAoMC0xKSwgV2F2ZS9UYWJsZV0KQml0IOKGkCBWQSEoVVPihqcxw7ctMTrijIrDlyxTVTrigYXigb86Misxw5cxNS06MeKImuKGpzHihqUwKQojIFJldmVyc2UgW1dhdmUvVGFibGVdClJldiDihpAgQSHih4wKIyBJbnZlcnQgW1dhdmUvVGFibGVdCkludiDihpAgQSHCrwojIE1pcnJvciAxc3QgaGFsZiBbV2F2ZS9UYWJsZV0KTWlyIOKGkCBBISjiioLih4wu4oaZw7cyUykKIyBGYWRlIHN0YXJ0ICYgZW5kIFtBbW91bnQgKDAtMSksIFdhdmUvVGFibGVdCkZhZCDihpAgVkEhKOKHjOKsmjHDl-KHjDrirJox4p-cw5fDt-Knuy7ih6HijIrDtzLDl1PihqUwKQojIENyb3NzZmFkZSBbQW1vdW50ICgwLTEpLCBTdGFydCBXYXZlL1RhYmxlLCBFbmQgV2F2ZS9UYWJsZV0KQ3JzIOKGkCBWQUEhKCviipkow5ctOjEpOuKfnMOX4oqZOikKIyBTdHJldGNoIFtUbyAoMC0xKSwgRnJvbSAoMC0xKSwgV2F2ZS9UYWJsZV0KU3RyIOKGkCBWVkEhKOKKguKKmVJJOi06U-KfnFJJ4oqZ4oqD4oaZ4oaY4oipKOKGpy0xU-KGpTHigYXDl1Pihqcx4oalMCkpCiMgU2hpZnQgdXAvZG93biBbQW1vdW50ICjCrzEtMSksIFdhdmUvVGFibGVdClNoZiDihpAgVkEhKCspCiMgU2xpY2UgW0Ftb3VudCAoMC0xKSwgV2F2ZS9UYWJsZV0KU2xjIOKGkCBWQSEoUkkgU-KGr-KZreKBheKGpTHDl1MpCiMgU3BlZWQgdXAvZG93biBbT2N0YXZlcywgV2F2ZS9UYWJsZV0KU3BkIOKGkCBWQSEoU2xj4oG_OjIpCiMgU3BsaWNlIFtBbW91bnQgKDAtMSksIFdhdmUvVGFibGUgMSwgV2F2ZS9UYWJsZSAyXQpTcGwg4oaQIFZBQSEo4oqC4oqT4oaZ4oaY4p-cOuKBhcOXU-KGpzHihqUwKQojIENsaXAKQ2xwIOKGkCBBIShDKQojIFdyYXAgW1dhdmUvVGFibGVdCldycCDihpAgQSEoVVPil78rTUIxU1UpCiMgQW1wbHV0dWRlIG1vZHVsYXRpb24gW0FtcGxpdHVkZSwgTW9kdWxhdG9yLCBDYXJyaWVyXQpBTSDihpAgVkFBISjDlysxw7cyw5ctMTopCiMgUmluZyBtb2R1bGF0aW9uIFtNb2R1bGF0b3IsIENhcnJpZXJdClJNIOKGkCBBQSHDlwojIEZyZXF1ZW5jeSBtb2R1bGF0aW9uIFtBbXBsaXR1ZGUsIE1vZHVsYXRvciwgQ2Fycmllcl0KRk0g4oaQIFZBQSEoU0nDl1MrT1XDlykKIyBDb252b2x2ZSBbSW1wdWxzZSwgV2F2ZS9UYWJsZV0KQ252IOKGkCBBQSEoLyvDl-KNieKXq-KKmSjihq_ima0pOi0x4oq4K-Knuyzip7vin5w6KQojIFBoYXNlIHJhbmRvbWl6ZSBbQW1vdW50ICgwLTEpLCBXYXZlL1RhYmxlXQpQaHIg4oaQIOKJoShGVTriipkrOkZUKTriiaHDl1LigoEtz4DDl8-E4o2lKOKKguKagikrMcO3MlNbXVLigoDiiplS4oKBCiMgSGFybW9uaWMgd2F2ZSBmaWx0ZXIgW0Ftb3VudCAoMC0xKSwgRmlsdGVyIFdhdmUvVGFibGUsIFNvdXJjZSBXYXZlL1RhYmxlXQpId2Yg4oaQIFZBQSEoRlXiipnDlzorMcOX4oqZKC0x4peM4oipRlQpKQojIERvd25zYW1wbGUgW0Ftb3VudCAoMC0xKSwgV2F2ZS9UYWJsZV0KRHduIOKGkCBWQSEoCiAg4oyIw7c6Uy7igYXihqUx4oG_OjIrMcOXLTHigpkyUy06MQogIOKGr8KkU-KNieKGr-KfnMO34p-cKOKJoS8r4oav4oqCOikKKQoKIyAtLS0tIFRhYmxlIG9wZXJhdGlvbnMKIyBDb21iIGZpbHRlciAoZmVlZGJhY2spIFtBbXBsaXR1ZGUsIEhhcm1vbmljLCBXYXZlL1RhYmxlXQpUQ21iIOKGkCDihq9PX1MgU03iipko4qyaMOKGr-KKguKMiMO3OlQu4oGFw7c6U-KKmVIpCiMgQ29tYiBmaWx0ZXIgKGZlZWRmb3J3YXJkKSBbQW1wbGl0dWRlLCBIYXJtb25pYywgV2F2ZS9UYWJsZV0KVENtZiDihpAg4oavT19TK8OX4oqZKOKKguKGrzpbMF3CpOKfnOKGmOKMisO3OsKvU-KKmSguUikpCiMgTWF4aW1pemUgdGFibGUgW1dhdmUvVGFibGVdClRNYXgg4oaQIOKGr09fU-KXjOKNpeKfnMO34omgMC7ihqXiiKkv4oal4p-cwq8uUgojIENlbnRlciB0YWJsZSAoYnkgcGVha3MpIFtXYXZlL1RhYmxlXQpUQ3RwIOKGkCDihq9PX1Mtw7cyKy_ihqfin5wv4oalLlIKIyBDZW50ZXIgdGFibGUgKGJ5IG1lYW4pIFtXYXZlL1RhYmxlXQpUQ3RtIOKGkCDihq9PX1Mtw7dULysuUgoKIyAtLS0tIFdhdmVmb3JtcwojIFNpbmUgd2F2ZSBbSGFybW9uaWNdClNpbiDihpAgViEo4oi_w5c6T1QpCiMgU2F3IHdhdmUgW0hhcm1vbmljXQpTYXcg4oaQIFYhKFVT4pe_McOXOk9VKQojIFRyaWFuZ2xlIHdhdmUgW0hhcm1vbmljXQpUcmkg4oaQIFYhKFVT4oy1VVPil78xKzAuMjXCr8OXOk9VKQojIFB1bHNlIHdhdmUgW0hhcm1vbmljLCBEdXR5XQpQdWwg4oaQIFZWIShVU-KJpFVTOlNhdykKIyBTcXVhcmUgd2F2ZSBbSGFybW9uaWNdClNxciDihpAgViEoUHVsOjAuNSkKIyBOb2lzZSB3YXZlIFtdCk5zdyDihpAgKOKYhzFVU-KNpSjiioLimoIpU1tdKQojIFNpbGVudCAoZW1wdHkpIHdhdmUKU2lsIOKGkCBS4oKB4qyaMOKGr8KkU1tdCgojIC0tLS0gVGFibGVzCiMgTm9pc2UgdGFibGUgW10KTnN0IOKGkCAo4o2lKOKKgk5zdylPW10pCgojIC0tLS0gU2V0dGluZ3MKRGVza3RvcCDihpAgRmFsc2UKSW5EaXJBdWRpbyDihpAgImluIgpJbkRpckltYWdlIOKGkCAiaW4iCk91dERpckF1ZGlvIOKGkCAib3V0IgpPdXREaXJJbWFnZSDihpAgIm91dCIKT3V0RGlyR3JhcGgg4oaQICJvdXQiCk91dERpclNwZWN0cnVtIOKGkCAib3V0IgpPdXRTdWZmaXhJbWFnZSDihpAgIiIKT3V0U3VmZml4R3JhcGgg4oaQICJnIgpPdXRTdWZmaXhTcGVjdHJ1bSDihpAgInMiCgojIC0tLS0gVXRpbGl0eQojIFByaW50IHN0cmluZyBbU3RyaW5nXQpQcmludCDihpAgJnBmCiMgUGxheSB3YXZlIG9yIHRhYmxlIFtXYXZlL1RhYmxlXQpFeHBvcnQg4oaQICZhcCBD4pmtCiMgUGxheSB0ZXN0IHRvbmUgb2Ygd2F2ZSBvciB0YWJsZSBbV2F2ZS9UYWJsZV0KVGVzdCDihpAgJmFwIFUzUiBD4pmtCiMgRGlzcGxheSBncmFwaCBbV2F2ZS9UYWJsZV0KR3JhcGgg4oaQIElTIENHCiMgRGlzcGxheSBpbWFnZSBbV2F2ZS9UYWJsZV0KSW1hZ2Ug4oaQICZpbXPihq9PX1MgU1UgUiBD4pmtCiMgUmVhZCBpbWFnZSBmcm9tIGZpbGUgW0ZpbGVuYW1lXQpJbXBvcnRJbWFnZSDihpAgUkkgT-KJoVJJIFPijZzijYniiaEow7czLyvihpkzKeKXjMKwaW1nJmZyYWLijaUo4oqCRFMgSW5EaXJJbWFnZSlEZXNrdG9wCiMgUmVhZCB3YXZlIGZyb20gZmlsZSBbRmlsZW5hbWVdCkltcG9ydFdhdmUg4oaQIFLigoEgUkkgU-KXjOKXjMKwYXVkaW8mZnJhYuKNpSjiioJEUyBJbkRpckF1ZGlvKURlc2t0b3AKIyBSZWFkIDI1Ni13YXZlIHRhYmxlIGZyb20gZmlsZSBbRmlsZW5hbWVdCkltcG9ydFRhYmxlIOKGkCDihq9PX1MgUkkgVOKXjOKXjMKwYXVkaW8mZnJhYuKNpSjiioJEUyBJbkRpckF1ZGlvKURlc2t0b3AKIyBUYWJsZSB0byB3YXZlIFtUYWJsZV0KVGFibGVBdmVyYWdlIOKGkCBS4oKBw7dPLyvihq9PX1MKIyBUYWJsZSB0byB3YXZlIFtJbmRleCAoMC0yNTUpLCBUYWJsZV0KVGFibGVJbmRleCDihpAgUuKCgeKKoeKGpy0xT-KGpTDigYU64oavT19TOgojIENvbnZlcnQgaW1hZ2UgdG8gdGFibGUgW0ltYWdlXQpJbWFnZVRvVGFibGUg4oaQIFVTCiMgRGlzcGxheSBncmFwaCBvZiBpbXB1bHNlIFtXYXZlL1RhYmxlXQpHcmFwaENvbnYg4oaQIEdyYXBoIEEhKOKsmjDDl-KGr1MxKQojIERpc3BsYXkgc3BlY3RydW0gZ3JhcGggW1dhdmUvVGFibGVdCkdyYXBoU3BlY3RydW0g4oaQIElT4o2i4oqiKD0x4qe7KeKJoSjih4ziip7iiaXDt1NI4oehU0jiio9TVsO3RlbihpnDtzJT4oy1ZmZ0KeKWvT0w4oeh4oyKw7dHTCBPCiMgRGlzcGxheSBmdWxsIHNwZWN0cnVtIGdyYXBoIFtXYXZlL1RhYmxlXQpHcmFwaFNwZWN0cnVtRnVsbCDihpAgSVPijaLiiqIoPTHip7sp4omhKOKHjOKKnuKJpcO3U0jih6FTSMO3RlbijLVmZnQp4pa9PTDih6HijIrDt0dMIE8KIyBTYXZlIHdhdmUgb3IgdGFibGUgdG8gZmlsZSBbRmlsZW5hbWUsIFdhdmUvVGFibGVdCldyaXRlQXVkaW8g4oaQICZmd2HijaUo4oqCRFMmZm1kLk91dERpckF1ZGlvKURlc2t0b3DiioLiipkoIi53YXYiYXVkaW8id2F2IiZhc3JD4pmtKQojIFNhdmUgaW1hZ2UgdG8gZmlsZSBbRmlsZW5hbWUsIFdhdmUvVGFibGVdCldyaXRlSW1hZ2Ug4oaQICgKICDiioLiipko4oqCT3V0U3VmZml4SW1hZ2UiLnBuZyJpbWcicG5nIuKItVsuLjoxXeKGr09fUyBTVSBSIEPima0pCiAgJmZ3YeKNpSjiioJEUyZmbWQuT3V0RGlySW1hZ2UpRGVza3RvcCkKIyBTYXZlIGdyYXBoIHRvIGZpbGUgW0ZpbGVuYW1lLCBXYXZlL1RhYmxlXQpXcml0ZUdyYXBoIOKGkCAoCiAg4o2lKOKKgkRTJmZtZC5PdXREaXJHcmFwaClEZXNrdG9w4oqCOk91dFN1ZmZpeEdyYXBo4oqZKD4z4qe74pazLuKItVsuLjoxXUNHKQogICZmd2HiqKwo4oqCOiIucG5nIuKKmShpbWcicG5nIil84oqCOiIuZ2lmIuKKmShnaWYxNikpOgopCiMgU2F2ZSBzcGVjdHJ1bSBncmFwaCB0byBmaWxlIFtGaWxlbmFtZSwgV2F2ZS9UYWJsZV0KV3JpdGVTcGVjdHJ1bSDihpAgKAogIOKKmSjijaLiiqIoPTHip7sp4omhKOKHjOKKnuKJpcO3U0jih6FTSOKKj1NWw7dGVuKGmcO3MlPijLVmZnQp4pa9PTDih6HijIrDt0dMIE8pCiAg4o2lKOKKgkRTJmZtZC5PdXREaXJTcGVjdHJ1bSlEZXNrdG9w4oqCOk91dFN1ZmZpeFNwZWN0cnVt4oqZKD4z4qe74pazLuKItVsuLjoxXSkKICAmZndh4qisKOKKgjoiLnBuZyLiipkoaW1nInBuZyIpfOKKgjoiLmdpZiLiipkoZ2lmMTYpKToKKQojIFNhdmUgZnVsbCBzcGVjdHJ1bSBncmFwaCB0byBmaWxlIFtGaWxlbmFtZSwgV2F2ZS9UYWJsZV0KV3JpdGVTcGVjdHJ1bUZ1bGwg4oaQICgKICDiipko4o2i4oqiKD0x4qe7KeKJoSjih4ziip7iiaXDt1NI4oehU0jDt0ZW4oy1ZmZ0KeKWvT0w4oeh4oyKw7dHTCBPKQogIOKNpSjiioJEUyZmbWQuT3V0RGlyU3BlY3RydW0pRGVza3RvcOKKgjpPdXRTdWZmaXhTcGVjdHJ1beKKmSg-M-Knu-KWsy7iiLVbLi46MV0pCiAgJmZ3YeKorCjiioI6Ii5wbmci4oqZKGltZyJwbmciKXziioI6Ii5naWYi4oqZKGdpZjE2KSk6CikKIyBHZW5lcmF0ZXMgdW51c2VkIG5hbWUgZm9yIG91dHB1dApOZXdOYW1lIOKGkCAoCiAg4qisKOKNmiZmbGR7Li4uLi4iLi8ifQogIHwge091dERpckF1ZGlvIE91dERpckltYWdlLk91dERpckdyYXBoLk91dERpclNwZWN0cnVtfQogICAg4o2aKOKNmuKGmOKnuzrijaUo4pa94oi1JmZpZi4pPjHip7suJmZsZC7iioIiLi8iRFMmZm1kLikKCiAgKURlc2t0b3AKICDijaIoKzEKICB8IHviioI6Ii53YXYi4p-cKOKKguKKgjpPdXRTdWZmaXhJbWFnZToiLnBuZyIpCiAgICAg4p-cKOKKguKKgjpPdXRTdWZmaXhHcmFwaDoiLmdpZiIp4p-cKOKKguKKgjpPdXRTdWZmaXhHcmFwaDoiLnBuZyIpCiAgICAg4p-cKOKKguKKgjpPdXRTdWZmaXhTcGVjdHJ1bToiLmdpZiIp4p-cKOKKguKKgjpPdXRTdWZmaXhTcGVjdHJ1bToiLnBuZyIpfVNUNSwsCiAgICAv4oal4omhKOKIiMKw4pahOikKCiAgKSAwCiAgU1Q14oqZ4peMCikKIyBHZXRzIG5hbWUgb2YgcmFuZG9tIC53YXYgZmlsZSBbU3ViZm9sZGVyXQpSYW5kb21BdWRpb0ZpbGUg4oaQICgKICDiqKwoJmZsZCIuLyJ84o2a4oaY4qe7OuKWveKItSZmaWYuJmZsZC7iioIiLi8iRFMmZm1kLuKKgkluRGlyQXVkaW8pRGVza3RvcAogIOKWveKJoShDRSIud2F2IsKw4pahKS4KICDiqKwoIiLil4x8wrDilqHiio_ijIrDl-KaguKnuy4pPjDip7suCikKIyBHZXRzIG5hbWUgb2YgcmFuZG9tIC5wbmcgZmlsZSBbU3ViZm9sZGVyXQpSYW5kb21JbWFnZUZpbGUg4oaQICgKICDiqKwoJmZsZCIuLyJ84o2a4oaY4qe7OuKWveKItSZmaWYuJmZsZC7iioIiLi8iRFMmZm1kLuKKgkluRGlySW1hZ2UpRGVza3RvcAogIOKWveKJoShDRSIucG5nIsKw4pahKS4KICDiqKwoIiLil4x8wrDilqHiio_ijIrDl-KaguKnuy4pPjDip7suCikKIyBSZXBlYXQgZnVuY3Rpb24gW0Z1bmN0aW9uLCBOdW1dClJlcGVhdCEg4oaQIOKNpV4hCiMgQ2FsbCByYW5kb20gZnVuY3Rpb24gKDIgY2hvaWNlcykKRG9SYW5kb23igLwg4oaQIOKorF4hXiHijIrDlzLimoIKIyBDYWxsIHJhbmRvbSBmdW5jdGlvbiAoMyBjaG9pY2VzKQpEb1JhbmRvbeKAvCEg4oaQIOKorCheIXxeIXxeISnijIrDlzPimoIKIyBDYWxsIHJhbmRvbSBmdW5jdGlvbiAoNCBjaG9pY2VzKQpEb1JhbmRvbeKAvOKAvCDihpAg4qisKF4hfF4hfF4hfF4hKeKMisOXNOKaggojIEV4aXQgKHVzZSBhdCBlbmQgb2Ygc2NyaXB0KQpFeGl0IOKGkCDijaUoJmV4aXQwKURlc2t0b3AKCiMgLS0tLSBTYW1wbGUgd2F2ZWZvcm1zL3dhdmV0YWJsZXMKV2F2ZeKCgSDihpAgQXZne1NpbjMgQW1wMC43U2luMiBTaW4xfQpXYXZl4oKCIOKGkCBBTTAuNFNpbjcgQU0wLjVTaW4yIFNpbjEKV2F2ZeKCgiDihpAgQXZne1NhdzEgKFB1bCAyIDAuMil9CldhdmXigoQg4oaQIEJpdDAuNyBGTTAuMVNpbjcgU2luMQpUYmzigoEg4oaQIEZNKFZhbCAwIDEpIFNpbjIgU2luMQpUYmzigoIg4oaQIE1heCBBdmd7QW1wKFZhbCAwIDEpU2luMiBTaW4xfQpUYmzigoMg4oaQIEJpdChWYWwgMCAxKSBDcnMoVmFsUEkgMCAxIDIpIFNpbjEgU2luMwoKUHJpbnQgIlRibOKCgyIKVGVzdCBUYmzigoMKSW1hZ2UgVGJs4oKDCkdyYXBoIFRibOKCgwpHcmFwaFNwZWN0cnVtIFRibOKCgwoKRXhpdAo=

# TODO: high/low pass filters
# TODO: IR importing
# TODO: fix Str (stretch) buzzing
# TODO: Sid (side by side)
# TODO: Shp (waveshape)
# TODO: Error handling

S ← 2048                                       # Samples per oscillator
O ← 256                                        # Oscillators per table
T ← ×S O                                       # Samples per table
SU ← ÷2+1                                      # Signed to unsigned
US ← -1×2                                      # Unsigned to signed
OU ← ÷⟜⇡S                                      # Oscillator base, unsigned 0-1
OT ← ×τOU                                      # Oscillator base, trigonometric 0-2π
R ← ↯¤T                                        # Oscillator to table (repeat)
U ← ⍥(⊢⍉↯⊟:⟜÷2⊸⧻)                              # Octave up (for playback only)
GH ← 200                                       # Graph height
C ← ↥¯1↧1                                      # Clamp
SW ← ⊏◿⧻,                                      # Select (wrapped)
SI ← +⊙×⟜-⊃⊃(SW⌊)(SW⌈)(-⊸⌊)                    # Select (interpolated)
RI ← SI×⇡:÷,⧻,                                 # Resize (interpolated)
CS ← ∿+η                                       # Cosine
NR ← ↧⊸↥                                       # NaN replace
SC ← NR1÷⟜∿                                    # Sinc
VB ← ÷-1⟜⇡O                                    # Value base
VR ← +⊙×:-⟜:                                   # Value range [min, max, values]
GL ← 16                                        # GIF length (frames)
SH ← 200                                       # Spectrum graph height
FV ← ÷2√S                                      # Fourier max value
SV ← +1⌊×÷2Sⁿ2÷⟜⇡÷4S                           # Spectrum index values
IS ← ⨬(&gifs15|&ims)=2⧻△.                      # Show image or gif
SM ← ⇌∧(⊂+⊸⊃⋅∘(×⊙⋅⊢))⊙(:¤°⊂)                   # Scan add + multiply
MB ← ÷:1ⁿ16 2                                  # Minimum 16-bit value
FT ← ⊃(∠°ℂ)(÷FV⌵)↙+1÷2S fft                    # Fourier transform [phase, amp ← wave]
FU ← ◌°ℂ°fft⊂:⍜°ℂ¯↘1⇌↘1.ℂ⊃(×∿)(×CS)⊙(×FV)      # Fourier untransform [wave ← phase, amp]
DS ← |1 ⍥(⊂:"/")>0⧻.                           # Add slash to directory name
CG ← ⍢⊢(=1⧻)≡(⇌⬚1⊞≠⇡GH⁅×-1GH SU U2C)▽=0⇡⌊÷GL O # Create graph
ST ← ⊂↯:@0↥0-:⊙(⧻.$"_")                        # Num to string w/ trailing zeroes
CE ← ≍⬚@ ↯⊙:¤⧻.⊙⇌⇌                             # Check extension [ext, name]
True ← 1                                       # True constant
False ← 0                                      # False constant
R₀ ← ☇0                                        # Rerank zero
R₁ ← ☇1                                        # Rerank one
V! ← R₁≡^!R₀
VV! ← R₁≡^!R₀⊙R₀
VVA! ← R₁≡^!R₀⊙R₀⊙⊙R₁
VA! ← R₁≡^!R₀⊙R₁
VAA! ← R₁≡^!R₀⊙R₁⊙⊙R₁
A! ← R₁≡^!R₁
AA! ← R₁≡^!R₁⊙R₁

# ---- Convolutions
# Identity (delta) convolution
CId ← ¤¤1

# ---- Table values
# Values (linear) [min, max]
Val ← VR⊙⊙(VB)
# Values (power in) [min, max, base]
ValPI ← VR⊙⊙(ⁿ:VB)
# Values (power out) [min, max, base]
ValPO ← ⇌ValPI:

# ---- Helpers
# Random (min-max) [Min, Max]
Rnd ← +×⚂:⟜-
# Random (min-max) [Min, Max]
RndI ← ⌊Rnd:+1:⍥:<,,

# ---- Wave Operations
# Sum [{Waves/Tables}]
Sum ← °□/+⍚R₁
# Average [{Waves/Tables}]
Avg ← Sum÷⊸⧻
# Amplify [Amount, Wave/Table]
Amp ← VA!×
# Maximize [Wave/Table]
Max ← A!(◌⍥⟜÷≠0.↥∩/↥⟜¯.)
# Center (by peaks) [Wave/Table]
Ctp ← A!(-÷2+/↧⟜/↥.)
# Center (by mean) [Wave/Table]
Ctm ← A!(-÷S/+.)
# Phase shift [Amount, Wave/Table]
Phs ← VA!(↻⁅×⧻,)
# Phase shift to zero point [Wave/Table]
Zer ← A!(↻⊗/↧⌵..)
# Bitcrush [Amount (0-1), Wave/Table]
Bit ← VA!(US↧1÷-1:⌊×,SU:⁅ⁿ:2+1×15-:1√↧1↥0)
# Reverse [Wave/Table]
Rev ← A!⇌
# Invert [Wave/Table]
Inv ← A!¯
# Mirror 1st half [Wave/Table]
Mir ← A!(⊂⇌.↙÷2S)
# Fade start & end [Amount (0-1), Wave/Table]
Fad ← VA!(⇌⬚1×⇌:⬚1⟜×÷⧻.⇡⌊÷2×S↥0)
# Crossfade [Amount (0-1), Start Wave/Table, End Wave/Table]
Crs ← VAA!(+⊙(×-:1):⟜×⊙:)
# Stretch [To (0-1), From (0-1), Wave/Table]
Str ← VVA!(⊂⊙RI:-:S⟜RI⊙⊃↙↘∩(↧-1S↥1⁅×S↧1↥0))
# Shift up/down [Amount (¯1-1), Wave/Table]
Shf ← VA!(+)
# Slice [Amount (0-1), Wave/Table]
Slc ← VA!(RI S↯♭⁅↥1×S)
# Speed up/down [Octaves, Wave/Table]
Spd ← VA!(Slcⁿ:2)
# Splice [Amount (0-1), Wave/Table 1, Wave/Table 2]
Spl ← VAA!(⊂⊓↙↘⟜:⁅×S↧1↥0)
# Clip
Clp ← A!(C)
# Wrap [Wave/Table]
Wrp ← A!(US◿+MB1SU)
# Amplutude modulation [Amplitude, Modulator, Carrier]
AM ← VAA!(×+1÷2×-1:)
# Ring modulation [Modulator, Carrier]
RM ← AA!×
# Frequency modulation [Amplitude, Modulator, Carrier]
FM ← VAA!(SI×S+OU×)
# Convolve [Impulse, Wave/Table]
Cnv ← AA!(/+×⍉◫⊙(↯♭):-1⊸+⧻,⧻⟜:)
# Phase randomize [Amount (0-1), Wave/Table]
Phr ← ≡(FU:⊙+:FT):≡×R₁-π×τ⍥(⊂⚂)+1÷2S[]R₀⊙R₁
# Harmonic wave filter [Amount (0-1), Filter Wave/Table, Source Wave/Table]
Hwf ← VAA!(FU⊙×:+1×⊙(-1◌∩FT))
# Downsample [Amount (0-1), Wave/Table]
Dwn ← VA!(
  ⌈÷:S.⁅↥1ⁿ:2+1×-1ₙ2S-:1
  ↯¤S⍉↯⟜÷⟜(≡/+↯⊂:)
)

# ---- Table operations
# Comb filter (feedback) [Amplitude, Harmonic, Wave/Table]
TCmb ← ↯O_S SM⊙(⬚0↯⊂⌈÷:T.⁅÷:S⊙R)
# Comb filter (feedforward) [Amplitude, Harmonic, Wave/Table]
TCmf ← ↯O_S+×⊙(⊂↯:[0]¤⟜↘⌊÷:¯S⊙(.R))
# Maximize table [Wave/Table]
TMax ← ↯O_S◌⍥⟜÷≠0.↥∩/↥⟜¯.R
# Center table (by peaks) [Wave/Table]
TCtp ← ↯O_S-÷2+/↧⟜/↥.R
# Center table (by mean) [Wave/Table]
TCtm ← ↯O_S-÷T/+.R

# ---- Waveforms
# Sine wave [Harmonic]
Sin ← V!(∿×:OT)
# Saw wave [Harmonic]
Saw ← V!(US◿1×:OU)
# Triangle wave [Harmonic]
Tri ← V!(US⌵US◿1+0.25¯×:OU)
# Pulse wave [Harmonic, Duty]
Pul ← VV!(US≤US:Saw)
# Square wave [Harmonic]
Sqr ← V!(Pul:0.5)
# Noise wave []
Nsw ← (☇1US⍥(⊂⚂)S[])
# Silent (empty) wave
Sil ← R₁⬚0↯¤S[]

# ---- Tables
# Noise table []
Nst ← (⍥(⊂Nsw)O[])

# ---- Settings
Desktop ← False
InDirAudio ← "in"
InDirImage ← "in"
OutDirAudio ← "out"
OutDirImage ← "out"
OutDirGraph ← "out"
OutDirSpectrum ← "out"
OutSuffixImage ← ""
OutSuffixGraph ← "g"
OutSuffixSpectrum ← "s"

# ---- Utility
# Print string [String]
Print ← &pf
# Play wave or table [Wave/Table]
Export ← &ap C♭
# Play test tone of wave or table [Wave/Table]
Test ← &ap U3R C♭
# Display graph [Wave/Table]
Graph ← IS CG
# Display image [Wave/Table]
Image ← &ims↯O_S SU R C♭
# Read image from file [Filename]
ImportImage ← RI O≡RI S⍜⍉≡(÷3/+↙3)◌°img&frab⍥(⊂DS InDirImage)Desktop
# Read wave from file [Filename]
ImportWave ← R₁ RI S◌◌°audio&frab⍥(⊂DS InDirAudio)Desktop
# Read 256-wave table from file [Filename]
ImportTable ← ↯O_S RI T◌◌°audio&frab⍥(⊂DS InDirAudio)Desktop
# Table to wave [Table]
TableAverage ← R₁÷O/+↯O_S
# Table to wave [Index (0-255), Table]
TableIndex ← R₁⊡↧-1O↥0⁅:↯O_S:
# Convert image to table [Image]
ImageToTable ← US
# Display graph of impulse [Wave/Table]
GraphConv ← Graph A!(⬚0×↯S1)
# Display spectrum graph [Wave/Table]
GraphSpectrum ← IS⍢⊢(=1⧻)≡(⇌⊞≥÷SH⇡SH⊏SV÷FV↙÷2S⌵fft)▽=0⇡⌊÷GL O
# Display full spectrum graph [Wave/Table]
GraphSpectrumFull ← IS⍢⊢(=1⧻)≡(⇌⊞≥÷SH⇡SH÷FV⌵fft)▽=0⇡⌊÷GL O
# Save wave or table to file [Filename, Wave/Table]
WriteAudio ← &fwa⍥(⊂DS&fmd.OutDirAudio)Desktop⊂⊙(".wav"audio"wav"&asrC♭)
# Save image to file [Filename, Wave/Table]
WriteImage ← (
  ⊂⊙(⊂OutSuffixImage".png"img"png"∵[..:1]↯O_S SU R C♭)
  &fwa⍥(⊂DS&fmd.OutDirImage)Desktop)
# Save graph to file [Filename, Wave/Table]
WriteGraph ← (
  ⍥(⊂DS&fmd.OutDirGraph)Desktop⊂:OutSuffixGraph⊙(>3⧻△.∵[..:1]CG)
  &fwa⨬(⊂:".png"⊙(img"png")|⊂:".gif"⊙(gif16)):
)
# Save spectrum graph to file [Filename, Wave/Table]
WriteSpectrum ← (
  ⊙(⍢⊢(=1⧻)≡(⇌⊞≥÷SH⇡SH⊏SV÷FV↙÷2S⌵fft)▽=0⇡⌊÷GL O)
  ⍥(⊂DS&fmd.OutDirSpectrum)Desktop⊂:OutSuffixSpectrum⊙(>3⧻△.∵[..:1])
  &fwa⨬(⊂:".png"⊙(img"png")|⊂:".gif"⊙(gif16)):
)
# Save full spectrum graph to file [Filename, Wave/Table]
WriteSpectrumFull ← (
  ⊙(⍢⊢(=1⧻)≡(⇌⊞≥÷SH⇡SH÷FV⌵fft)▽=0⇡⌊÷GL O)
  ⍥(⊂DS&fmd.OutDirSpectrum)Desktop⊂:OutSuffixSpectrum⊙(>3⧻△.∵[..:1])
  &fwa⨬(⊂:".png"⊙(img"png")|⊂:".gif"⊙(gif16)):
)
# Generates unused name for output
NewName ← (
  ⨬(⍚&fld{....."./"}
  | {OutDirAudio OutDirImage.OutDirGraph.OutDirSpectrum}
    ⍚(⍚↘⧻:⍥(▽∵&fif.)>1⧻.&fld.⊂"./"DS&fmd.)

  )Desktop
  ⍢(+1
  | {⊂:".wav"⟜(⊂⊂:OutSuffixImage:".png")
     ⟜(⊂⊂:OutSuffixGraph:".gif")⟜(⊂⊂:OutSuffixGraph:".png")
     ⟜(⊂⊂:OutSuffixSpectrum:".gif")⟜(⊂⊂:OutSuffixSpectrum:".png")}ST5,,
    /↥≡(∈°□:)

  ) 0
  ST5⊙◌
)
# Gets name of random .wav file [Subfolder]
RandomAudioFile ← (
  ⨬(&fld"./"|⍚↘⧻:▽∵&fif.&fld.⊂"./"DS&fmd.⊂InDirAudio)Desktop
  ▽≡(CE".wav"°□).
  ⨬(""◌|°□⊏⌊×⚂⧻.)>0⧻.
)
# Gets name of random .png file [Subfolder]
RandomImageFile ← (
  ⨬(&fld"./"|⍚↘⧻:▽∵&fif.&fld.⊂"./"DS&fmd.⊂InDirImage)Desktop
  ▽≡(CE".png"°□).
  ⨬(""◌|°□⊏⌊×⚂⧻.)>0⧻.
)
# Repeat function [Function, Num]
Repeat! ← ⍥^!
# Call random function (2 choices)
DoRandom‼ ← ⨬^!^!⌊×2⚂
# Call random function (3 choices)
DoRandom‼! ← ⨬(^!|^!|^!)⌊×3⚂
# Call random function (4 choices)
DoRandom‼‼ ← ⨬(^!|^!|^!|^!)⌊×4⚂
# Exit (use at end of script)
Exit ← ⍥(&exit0)Desktop

# ---- Sample waveforms/wavetables
Wave₁ ← Avg{Sin3 Amp0.7Sin2 Sin1}
Wave₂ ← AM0.4Sin7 AM0.5Sin2 Sin1
Wave₂ ← Avg{Saw1 (Pul 2 0.2)}
Wave₄ ← Bit0.7 FM0.1Sin7 Sin1
Tbl₁ ← FM(Val 0 1) Sin2 Sin1
Tbl₂ ← Max Avg{Amp(Val 0 1)Sin2 Sin1}
Tbl₃ ← Bit(Val 0 1) Crs(ValPI 0 1 2) Sin1 Sin3

Print "Tbl₃"
Test Tbl₃
Image Tbl₃
Graph Tbl₃
GraphSpectrum Tbl₃

Exit
