# Uiua 0.13.0-dev.2
# https://uiua.org/pad?src=0_13_0-dev_2__IyBVaXVhIDAuMTMuMC1kZXYuMgojIFt1cmxdCgpTIOKGkCAyMDQ4ICAgICAgICAgICAgICAgICAgICAjIFNhbXBsZXMgcGVyIG9zY2lsbGF0b3IKTyDihpAgMjU2ICAgICAgICAgICAgICAgICAgICAgIyBPc2NpbGxhdG9ycyBwZXIgdGFibGUKVCDihpAgw5dTIE8gICAgICAgICAgICAgICAgICAgICMgU2FtcGxlcyBwZXIgdGFibGUKU1Ug4oaQIMO3MisxICAgICAgICAgICAgICAgICAgICMgU2lnbmVkIHRvIHVuc2lnbmVkClVTIOKGkCAtMcOXMiAgICAgICAgICAgICAgICAgICAjIFVuc2lnbmVkIHRvIHNpZ25lZApPVSDihpAgw7fin5zih6FTICAgICAgICAgICAgICAgICAgICMgT3NjaWxsYXRvciBiYXNlLCB1bnNpZ25lZCAwLTEKT1Qg4oaQIMOXz4RPVSAgICAgICAgICAgICAgICAgICAjIE9zY2lsbGF0b3IgYmFzZSwgdHJpZ29ub21ldHJpYyAwLTLPgApSIOKGkCDihq_CpFQgICAgICAgICAgICAgICAgICAgICAjIE9zY2lsbGF0b3IgdG8gdGFibGUgKHJlcGVhdCkKVSDihpAg4o2lKOKKouKNieKGr-KKnzrin5zDtzLiirjip7spICAgICAgICAgICAjIE9jdGF2ZSB1cCAoZm9yIHBsYXliYWNrIG9ubHkpCk0g4oaQIOKXjOKNpeKfnMO34omgMC7ihqXiiKkv4oal4p-cwq8uICAgICAgICAgICMgTWF4aW1pemUKR0gg4oaQIDIwMCAgICAgICAgICAgICAgICAgICAgIyBHcmFwaCBoZWlnaHQKQyDihpAg4oalwq8x4oanMSAgICAgICAgICAgICAgICAgICAjIENsYW1wClNXIOKGkCDiio_il7_ip7ssICAgICAgICAgICAgICAgICAgICMgU2VsZWN0ICh3cmFwcGVkKQpTSSDihpAgK-KKmcOX4p-cLeKKg-KKgyhTV-KMiikoU1fijIgpKC3iirjijIopICMgU2VsZWN0IChpbnRlcnBvbGF0ZWQpCgojIE9wZXJhdGlvbnMKUGhzIOKGkCDihrvigYXDl-KnuywgICAgIyBQaGFzZSBzaGlmdCBbQW1vdW50LCBXYXZlXQpaZXIg4oaQIOKGu-KKly_ihqfijLUuLiAgIyBQaGFzZSBzaGlmdCB0byB6ZXJvIHBvaW50IFtXYXZlXQpBdmcg4oaQIMO3Oi8r4p-c4qe7ICAgIyBBdmVyYWdlIFtXYXZlQXJyYXldCkFNIOKGkCDDlysxw7cyw5ctMTogIyBBbXBsdXR1ZGUgbW9kdWxhdGlvbiBbQW1wbGl0dWRlLCBNb2R1bGF0b3IsIENhcnJpZXJdCkZNIOKGkCBTScOXUytPVcOXICAjIEZyZXF1ZW5jeSBtb2R1bGF0aW9uIFtBbXBsaXR1ZGUsIE1vZHVsYXRvciwgQ2Fycmllcl0KCiMgQmFzaWMgd2F2ZXMKU2luIOKGkCDiiL_DlzpPVCAgICAgIyBTaW5lIHdhdmUgW0hhcm1vbmljXQpTYXcg4oaQIFVT4pe_McOXOk9VICAjIFNhdyB3YXZlIFtIYXJtb25pY10KUHVsIOKGkCBVU-KJpFVTOlNhdyAjIFB1bHNlIHdhdmUgW0hhcm1vbmljLCBEdXR5XQpTcXIg4oaQIFB1bDowLjUgICAjIFNxdWFyZSB3YXZlIFtIYXJtb25pY10KCiMgVXRpbGl0eQpQcmludCDihpAgJnBmCkV4cG9ydCDihpAgJmFwIEMKVGVzdCDihpAgJmFwIFUzUiBNIEMKR3JhcGgg4oaQICZpbXPih4zirJox4oqe4omg4oehR0jigYXDly0xR0ggU1UgVTIgQwpJbWFnZSDihpAgJmltc-KGr09fUyBTVSBSIEMKSW1wb3J0V2F2ZSDihpAgU0nDl-KHoVPDt1Pip7su4peM4peMwrBhdWRpbyZmcmFiCgojIFNhbXBsZSB3YXZlZm9ybXMKV2F2ZeKCgSDihpAgQXZnW1NpbjMgw5cwLjdTaW4yIFNpbjFdCldhdmXigoIg4oaQIEFNMC40U2luNyBBTTAuNVNpbjIgU2luMQpXYXZl4oKDIOKGkCBBdmdbU2F3MSBQdWwy4oiYMC4yXQpXYXZl4oKEIOKGkCBGTTAuMVNpbjcgU2luMQoKUHJpbnQgIldhdmXigoQiCkV4cG9ydCBXYXZl4oKEClRlc3QgV2F2ZeKChApHcmFwaCBXYXZl4oKECkltYWdlIFdhdmXigoQK

S ← 2048                    # Samples per oscillator
O ← 256                     # Oscillators per table
T ← ×S O                    # Samples per table
SU ← ÷2+1                   # Signed to unsigned
US ← -1×2                   # Unsigned to signed
OU ← ÷⟜⇡S                   # Oscillator base, unsigned 0-1
OT ← ×τOU                   # Oscillator base, trigonometric 0-2π
R ← ↯¤T                     # Oscillator to table (repeat)
U ← ⍥(⊢⍉↯⊟:⟜÷2⊸⧻)           # Octave up (for playback only)
M ← ◌⍥⟜÷≠0.↥∩/↥⟜¯.          # Maximize
GH ← 200                    # Graph height
C ← ↥¯1↧1                   # Clamp
SW ← ⊏◿⧻,                   # Select (wrapped)
SI ← +⊙×⟜-⊃⊃(SW⌊)(SW⌈)(-⊸⌊) # Select (interpolated)

# Operations
Phs ← ↻⁅×⧻,    # Phase shift [Amount, Wave]
Zer ← ↻⊗/↧⌵..  # Phase shift to zero point [Wave]
Avg ← ÷:/+⟜⧻   # Average [WaveArray]
AM ← ×+1÷2×-1: # Amplutude modulation [Amplitude, Modulator, Carrier]
FM ← SI×S+OU×  # Frequency modulation [Amplitude, Modulator, Carrier]

# Basic waves
Sin ← ∿×:OT     # Sine wave [Harmonic]
Saw ← US◿1×:OU  # Saw wave [Harmonic]
Pul ← US≤US:Saw # Pulse wave [Harmonic, Duty]
Sqr ← Pul:0.5   # Square wave [Harmonic]

# Utility
Print ← &pf
Export ← &ap C
Test ← &ap U3R M C
Graph ← &ims⇌⬚1⊞≠⇡GH⁅×-1GH SU U2 C
Image ← &ims↯O_S SU R C
ImportWave ← SI×⇡S÷S⧻.◌◌°audio&frab

# Sample waveforms
Wave₁ ← Avg[Sin3 ×0.7Sin2 Sin1]
Wave₂ ← AM0.4Sin7 AM0.5Sin2 Sin1
Wave₃ ← Avg[Saw1 Pul2∘0.2]
Wave₄ ← FM0.1Sin7 Sin1

Print "Wave₄"
Export Wave₄
Test Wave₄
Graph Wave₄
Image Wave₄
