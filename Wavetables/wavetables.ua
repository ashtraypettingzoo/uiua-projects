# Experimental!
# Uiua 0.13.0-dev.2
# https://github.com/ashtraypettingzoo/uiua-projects/blob/main/Wavetables/wavetables.ua
# https://uiua.org/pad?src=0_13_0-dev_2__IyBFeHBlcmltZW50YWwhCiMgVWl1YSAwLjEzLjAtZGV2LjIKIyBodHRwczovL2dpdGh1Yi5jb20vYXNodHJheXBldHRpbmd6b28vdWl1YS1wcm9qZWN0cy9ibG9iL21haW4vV2F2ZXRhYmxlcy93YXZldGFibGVzLnVhCiMgW3VybF0KCiMgVE9ETzogSVIgaW1wb3J0aW5nCiMgVE9ETzogZml4IFN0ciAoc3RyZXRjaCkgYnV6emluZwojIFRPRE86IFNpZCAoc2lkZSBieSBzaWRlKQojIFRPRE86IFNocCAod2F2ZXNoYXBlKQojIFRPRE86IEVycm9yIGhhbmRsaW5nCgpTIOKGkCAyMDQ4ICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBTYW1wbGVzIHBlciBvc2NpbGxhdG9yCk8g4oaQIDI1NiAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIE9zY2lsbGF0b3JzIHBlciB0YWJsZQpUIOKGkCDDl1MgTyAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgU2FtcGxlcyBwZXIgdGFibGUKU1Ug4oaQIMO3MisxICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFNpZ25lZCB0byB1bnNpZ25lZApVUyDihpAgLTHDlzIgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgVW5zaWduZWQgdG8gc2lnbmVkCk9VIOKGkCDDt-KfnOKHoVMgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgT3NjaWxsYXRvciBiYXNlLCB1bnNpZ25lZCAwLTEKT1Qg4oaQIMOXz4RPVSAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBPc2NpbGxhdG9yIGJhc2UsIHRyaWdvbm9tZXRyaWMgMC0yz4AKUiDihpAg4oavwqRUICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgT3NjaWxsYXRvciB0byB0YWJsZSAocmVwZWF0KQpVIOKGkCDijaUo4oqi4o2J4oav4oqfOuKfnMO3MuKKuOKnuykgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIE9jdGF2ZSB1cCAoZm9yIHBsYXliYWNrIG9ubHkpCkdIIOKGkCAyMDAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIEdyYXBoIGhlaWdodApDIOKGkCDihqXCrzHihqcxICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIENsYW1wClNXIOKGkCDiio_il7_ip7ssICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFNlbGVjdCAod3JhcHBlZCkKU0kg4oaQICviipnDl-KfnC3iioPiioMoU1fijIopKFNX4oyIKSgt4oq44oyKKSAgICAgICAgICAgICAgICAgICAgIyBTZWxlY3QgKGludGVycG9sYXRlZCkKUkkg4oaQIFNJw5fih6E6w7cs4qe7LCAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgUmVzaXplIChpbnRlcnBvbGF0ZWQpCkNTIOKGkCDiiL8rzrcgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIENvc2luZQpOUiDihpAg4oan4oq44oalICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBOYU4gcmVwbGFjZQpTQyDihpAgTlIxw7fin5ziiL8gICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFNpbmMKVkIg4oaQIMO3LTHin5zih6FPICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBWYWx1ZSBiYXNlClZSIOKGkCAr4oqZw5c6LeKfnDogICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgVmFsdWUgcmFuZ2UgW21pbiwgbWF4LCB2YWx1ZXNdCkdMIOKGkCAxNiAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIEdJRiBsZW5ndGggKGZyYW1lcykKU0gg4oaQIDIwMCAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgU3BlY3RydW0gZ3JhcGggaGVpZ2h0CkZWIOKGkCDDtzLiiJpTICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIEZvdXJpZXIgbWF4IHZhbHVlClNWIOKGkCArMeKMisOXw7cyU-KBvzLDt-KfnOKHocO3NFMgICAgICAgICAgICAgICAgICAgICAgICAgICAjIFNwZWN0cnVtIGluZGV4IHZhbHVlcwpJUyDihpAg4qisKCZnaWZzMTV8Jmltcyk9MuKnu-KWsy4gICAgICAgICAgICAgICAgICAgICAgIyBTaG93IGltYWdlIG9yIGdpZgpTTSDihpAg4oeM4oinKOKKgiviirjiioPii4XiiJgow5fiipnii4XiiqIpKeKKmSg6wqTCsOKKgikgICAgICAgICAgICAgICAgICAgIyBTY2FuIGFkZCArIG11bHRpcGx5Ck1CIOKGkCDDtzox4oG_MTYgMiAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIE1pbmltdW0gMTYtYml0IHZhbHVlCkZUIOKGkCDiioMo4oigwrDihIIpKMO3RlbijLUp4oaZKzHDtzJTIGZmdCAgICAgICAgICAgICAgICAgICAgIyBGb3VyaWVyIHRyYW5zZm9ybSBbcGhhc2UsIGFtcCDihpAgd2F2ZV0KRlUg4oaQIOKXjMKw4oSCwrBmZnTiioI64o2cwrDihILCr-KGmDHih4zihpgxLuKEguKKgyjDl-KIvykow5dDUyniipkow5dGVikgICAgICAjIEZvdXJpZXIgdW50cmFuc2Zvcm0gW3dhdmUg4oaQIHBoYXNlLCBhbXBdCkRTIOKGkCB8MSDijaUo4oqCOiIvIik-MOKnuy4gICAgICAgICAgICAgICAgICAgICAgICAgICAjIEFkZCBzbGFzaCB0byBkaXJlY3RvcnkgbmFtZQpDRyDihpAg4o2i4oqiKD0x4qe7KeKJoSjih4zirJox4oqe4omg4oehR0jigYXDly0xR0ggU1UgVTJDKeKWvT0w4oeh4oyKw7dHTCBPICMgQ3JlYXRlIGdyYXBoClNUIOKGkCDiioLihq86QDDihqUwLTriipko4qe7LiQiXyIpICAgICAgICAgICAgICAgICAgICAgICAgIyBOdW0gdG8gc3RyaW5nIHcvIHRyYWlsaW5nIHplcm9lcwpDRSDihpAg4omN4qyaQCDihq_iipk6wqTip7su4oqZ4oeM4oeMICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIENoZWNrIGV4dGVuc2lvbiBbZXh0LCBuYW1lXQpUcnVlIOKGkCAxICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBUcnVlIGNvbnN0YW50CkZhbHNlIOKGkCAwICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAjIEZhbHNlIGNvbnN0YW50ClLigoAg4oaQIOKYhzAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgIyBSZXJhbmsgemVybwpS4oKBIOKGkCDimIcxICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICAgICMgUmVyYW5rIG9uZQpWISDihpAgUuKCgeKJoV4hUuKCgApWViEg4oaQIFLigoHiiaFeIVLigoDiiplS4oKAClZWQSEg4oaQIFLigoHiiaFeIVLigoDiiplS4oKA4oqZ4oqZUuKCgQpWQSEg4oaQIFLigoHiiaFeIVLigoDiiplS4oKBClZBQSEg4oaQIFLigoHiiaFeIVLigoDiiplS4oKB4oqZ4oqZUuKCgQpBISDihpAgUuKCgeKJoV4hUuKCgQpBQSEg4oaQIFLigoHiiaFeIVLigoHiiplS4oKBCgojIC0tLS0gSW1wdWxzZXMKIyBJZGVudGl0eSAoZGVsdGEpIGltcHVsc2UKSUlkIOKGkCDCpMKkMQojIFBoYXNlIHNoaWZ0ZWQgZGVsdGEgaW1wdWxzZSBbUGhhc2UgKDAtMSldCklQaHMg4oaQIFYhKOKGuzrirJow4oavwqRTIElJZOKBhcOXUykKIyBMb3ctcGFzcyAoc2luYykgaW1wdWxzZSBbSGFybW9uaWNdCklMUFMg4oaQIFYhKMO3LysuU0PDly3PgE9UKQojIEhpZ2gtcGFzcyAoc2luYykgaW1wdWxzZSBbSGFybW9uaWNdCklIUFMg4oaQIFYhKC06SVBoczAuNUlMUFMpCiMgTG93LXBhc3MgKHdpbmRvd2VkKSBpbXB1bHNlIFtIYXJtb25pYywgV2luZG93XQpJTFBXIOKGkCBWViEoCiAgPjAsT1Q6U0PDly3PgE9UCiAg4qisKCsxw5cwfMOXw5fiioMoPjB8PM-EKeKfnCgrKzAuNDLiipMow5fCrzAuNXzDlzAuMDgp4oipQ1Pin5wow5cyKSkrz4DDly3PgCkKICDDty8rLsOXKQojIEhpZ2gtcGFzcyAod2luZG93ZWQpIGltcHVsc2UgW0hhcm1vbmljLCBXaW5kb3ddCklIUFcg4oaQIFZWISgtOklQaHMwLjVJTFBXKQojIExvdy1wYXNzIChwdWxzZSkgaW1wdWxzZSBbSGFybW9uaWNdCklMUFAg4oaQIFYhKOKGu-KBhcO3MitTOuKsmjDihq_CpFPihq_CpCzDtzoxLuKBheKGp1PDtzpT4oalMcOXMikKIyBIaWdoLXBhc3MgKHB1bHNlKSBpbXB1bHNlIFtIYXJtb25pY10KSUhQUCDihpAgViEoLTpJUGhzMC41SUxQUCkKIyBMb3ctcGFzcyAoZXhwb25lbnRpYWwpIGltcHVsc2UgW0hhcm1vbmljXQpJTFBFIOKGkCBWISjDty8rLuKBvzplwq_Dl09Vw5figpll4oG_MyAxMCkKIyBIaWdoLXBhc3MgKGV4cG9uZW50aWFsKSBpbXB1bHNlIFtIYXJtb25pY10KSUhQRSDihpAgViEoLTpJUGhzMElMUEUpCgojIC0tLS0gVGFibGUgdmFsdWVzCiMgVmFsdWVzIChsaW5lYXIpIFtNaW4sIE1heF0KVmFsIOKGkCBWUuKKmeKKmShWQikKIyBWYWx1ZXMgKHBvd2VyIGluKSBbTWluLCBNYXgsIEJhc2VdClZhbFBJIOKGkCBWUuKKmeKKmSjigb86VkIpCiMgVmFsdWVzIChwb3dlciBvdXQpIFtNaW4sIE1heCwgQmFzZV0KVmFsUE8g4oaQIOKHjFZhbFBJOgoKIyAtLS0tIEhlbHBlcnMKIyBSYW5kb20gKG1pbi1tYXgpIFtNaW4sIE1heF0KUm5kIOKGkCArw5fimoI64p-cLQojIFJhbmRvbSAobWluLW1heCkgW01pbiwgTWF4XQpSbmRJIOKGkCDijIpSbmQ6KzE64o2lOjwsLAoKIyAtLS0tIFdhdmUgT3BlcmF0aW9ucwojIFN1bSBbe1dhdmVzL1RhYmxlc31dClN1bSDihpAgwrDilqEvK-KNmlLigoEKIyBBdmVyYWdlIFt7V2F2ZXMvVGFibGVzfV0KQXZnIOKGkCBTdW3Dt-KKuOKnuwojIEFtcGxpZnkgW0Ftb3VudCwgV2F2ZS9UYWJsZV0KQW1wIOKGkCBWQSHDlwojIE1heGltaXplIFtXYXZlL1RhYmxlXQpNYXgg4oaQIEEhKOKXjOKNpeKfnMO34omgMC7ihqXiiKkv4oal4p-cwq8uKQojIENlbnRlciAoYnkgcGVha3MpIFtXYXZlL1RhYmxlXQpDdHAg4oaQIEEhKC3DtzIrL-KGp-KfnC_ihqUuKQojIENlbnRlciAoYnkgbWVhbikgW1dhdmUvVGFibGVdCkN0bSDihpAgQSEoLcO3Uy8rLikKIyBQaGFzZSBzaGlmdCBbQW1vdW50LCBXYXZlL1RhYmxlXQpQaHMg4oaQIFZBISjihrvigYXDl-KnuywpCiMgUGhhc2Ugc2hpZnQgdG8gemVybyBwb2ludCBbV2F2ZS9UYWJsZV0KWmVyIOKGkCBBISjihrviipcv4oan4oy1Li4pCiMgQml0Y3J1c2ggW0Ftb3VudCAoMC0xKSwgV2F2ZS9UYWJsZV0KQml0IOKGkCBWQSEoVVPihqcxw7ctMTrijIrDlyxTVTrigYXigb86Misxw5cxNS06MeKImuKGpzHihqUwKQojIFJldmVyc2UgW1dhdmUvVGFibGVdClJldiDihpAgQSHih4wKIyBJbnZlcnQgW1dhdmUvVGFibGVdCkludiDihpAgQSHCrwojIE1pcnJvciAxc3QgaGFsZiBbV2F2ZS9UYWJsZV0KTWlyIOKGkCBBISjiioLih4wu4oaZw7cyUykKIyBGYWRlIHN0YXJ0ICYgZW5kIFtBbW91bnQgKDAtMSksIFdhdmUvVGFibGVdCkZhZCDihpAgVkEhKOKHjOKsmjHDl-KHjDrirJox4p-cw5fDt-Knuy7ih6HijIrDtzLDl1PihqUwKQojIENyb3NzZmFkZSBbQW1vdW50ICgwLTEpLCBTdGFydCBXYXZlL1RhYmxlLCBFbmQgV2F2ZS9UYWJsZV0KQ3JzIOKGkCBWQUEhKCviipkow5ctOjEpOuKfnMOX4oqZOikKIyBTdHJldGNoIFtUbyAoMC0xKSwgRnJvbSAoMC0xKSwgV2F2ZS9UYWJsZV0KU3RyIOKGkCBWVkEhKOKKguKKmVJJOi06U-KfnFJJ4oqZ4oqD4oaZ4oaY4oipKOKGpy0xU-KGpTHigYXDl1Pihqcx4oalMCkpCiMgU2hpZnQgdXAvZG93biBbQW1vdW50ICjCrzEtMSksIFdhdmUvVGFibGVdClNoZiDihpAgVkEhKCspCiMgU2xpY2UgW0Ftb3VudCAoMC0xKSwgV2F2ZS9UYWJsZV0KU2xjIOKGkCBWQSEoUkkgU-KGr-KZreKBheKGpTHDl1MpCiMgU3BlZWQgdXAvZG93biBbT2N0YXZlcywgV2F2ZS9UYWJsZV0KU3BkIOKGkCBWQSEoU2xj4oG_OjIpCiMgU3BsaWNlIFtBbW91bnQgKDAtMSksIFdhdmUvVGFibGUgMSwgV2F2ZS9UYWJsZSAyXQpTcGwg4oaQIFZBQSEo4oqC4oqT4oaZ4oaY4p-cOuKBhcOXU-KGpzHihqUwKQojIENsaXAgKGhhcmQpIFtXYXZlL1RhYmxlXQpDbHAg4oaQIEEhKEMpCiMgV3JhcCBbV2F2ZS9UYWJsZV0KV3JwIOKGkCBBIShVU-KXvytNQjFTVSkKIyBGb2xkIFtXYXZlL1RhYmxlXQpGbGQg4oaQIEEhKC0x4oy1LTLil780LTEpCiMgQ2xpcCAoc29mdCkgW0V4cG9uZW50ICjiiaUyKSwgV2F2ZS9UYWJsZV0KQ2xzIOKGkCBWQSEo4o2c4oy1KMO3LTHiipkt4p-c4oqD4oG_w5c6KUM64oalMikKIyBTYXR1cmF0ZSAodGFuaCkgW0dhaW4sIFdhdmUvVGFibGVdClNhdCDihpAgVkEhKMO34oqDKy0x4oG_OmXDlzLDlykKIyBGdXp6ICh0YW5oKSBbR2FpbiwgU2hpZnQgKMKvMS0xKSwgV2F2ZS9UYWJsZV0KRnV6IOKGkCBTYXTiiplTaGYKIyBSZWN0aWZ5IFtXYXZlL1RhYmxlXQpSY3Qg4oaQIEEhKOKMtSkKIyBBbXBsdXR1ZGUgbW9kdWxhdGlvbiBbQW1wbGl0dWRlLCBNb2R1bGF0b3IsIENhcnJpZXJdCkFNIOKGkCBWQUEhKMOXKzHDtzLDly0xOikKIyBSaW5nIG1vZHVsYXRpb24gW01vZHVsYXRvciwgQ2Fycmllcl0KUk0g4oaQIEFBIcOXCiMgRnJlcXVlbmN5IG1vZHVsYXRpb24gW0FtcGxpdHVkZSwgTW9kdWxhdG9yLCBDYXJyaWVyXQpGTSDihpAgVkFBIShTScOXUytPVcOXKQojIENvbnZvbHZlIFtJbXB1bHNlLCBXYXZlL1RhYmxlXQpDbnYg4oaQIEFBISjil4zCsOKEgsKwZmZ0w5fiiKlmZnTDl-KImlPirJow4oavwqRTKQojIFBoYXNlIHJhbmRvbWl6ZSBbQW1vdW50ICgwLTEpLCBXYXZlL1RhYmxlXQpQaHIg4oaQIOKJoShGVTriipkrOkZUKTriiaHDl1LigoEtz4DDl8-E4o2lKOKKguKagikrMcO3MlNbXVLigoDiiplS4oKBCiMgSGFybW9uaWMgd2F2ZSBmaWx0ZXIgW0Ftb3VudCAoMC0xKSwgRmlsdGVyIFdhdmUvVGFibGUsIFNvdXJjZSBXYXZlL1RhYmxlXQpId2Yg4oaQIFZBQSEoRlXiipnDlzorMcOX4oqZKC0x4peM4oipRlQpKQojIERvd25zYW1wbGUgW0Ftb3VudCAoMC0xKSwgV2F2ZS9UYWJsZV0KRHduIOKGkCBWQSEoCiAg4oyIw7c6Uy7igYXihqUx4oG_OjIrMcOXLTHigpkyUy06MQogIOKGr8KkU-KNieKGr-KfnMO34p-cKOKJoS8r4oav4oqCOikKKQojIExvdy1wYXNzIChzaW5jKSBbSGFybW9uaWMsIFdhdmUvVGFibGVdCkxQUyDihpAgUGhzMC41Q252IElMUFMKIyBIaWdoLXBhc3MgKHNpbmMpIFtIYXJtb25pYywgV2F2ZS9UYWJsZV0KSFBTIOKGkCBQaHMwLjVDbnYgSUhQUwojIExvdy1wYXNzICh3aW5kb3dlZCkgW0hhcm1vbmljLCBXaW5kb3csIFdhdmUvVGFibGVdCkxQVyDihpAgUGhzMC41Q252IElMUFcKIyBIaWdoLXBhc3MgKHdpbmRvd2VkKSBbSGFybW9uaWMsIFdpbmRvdywgV2F2ZS9UYWJsZV0KSFBXIOKGkCBQaHMwLjVDbnYgSUhQVwojIExvdy1wYXNzIChwdWxzZSkgW0hhcm1vbmljLCBXYXZlL1RhYmxlXQpMUFAg4oaQIFBoczAuNUNudiBJTFBQCiMgSGlnaC1wYXNzIChwdWxzZSkgW0hhcm1vbmljLCBXYXZlL1RhYmxlXQpIUFAg4oaQIFBoczAuNUNudiBJSFBQCiMgTG93LXBhc3MgKGV4cG9uZW50aWFsKSBbSGFybW9uaWMsIFdhdmUvVGFibGVdCkxQRSDihpAgQ252IElMUEUKIyBIaWdoLXBhc3MgKGV4cG9uZW50aWFsKSBbSGFybW9uaWMsIFdhdmUvVGFibGVdCkhQRSDihpAgQ252IElIUEUKIyBMb3ctcGFzcyAoZGVmYXVsdCkgW0hhcm1vbmljLCBXYXZlL1RhYmxlXQpMUCDihpAgTFBXLgojIExvdy1wYXNzIChkZWZhdWx0KSBbSGFybW9uaWMsIFdhdmUvVGFibGVdCkhQIOKGkCBIUFcuCgojIC0tLS0gVGFibGUgb3BlcmF0aW9ucwojIENvbWIgZmlsdGVyIChmZWVkYmFjaykgW0FtcGxpdHVkZSwgSGFybW9uaWMsIFdhdmUvVGFibGVdClRDbWIg4oaQIOKGr09fUyBTTeKKmSjirJow4oav4oqC4oyIw7c6VC7igYXDtzpT4oqZUikKIyBDb21iIGZpbHRlciAoZmVlZGZvcndhcmQpIFtBbXBsaXR1ZGUsIEhhcm1vbmljLCBXYXZlL1RhYmxlXQpUQ21mIOKGkCDihq9PX1Mrw5fiipko4oqC4oavOlswXcKk4p-c4oaY4oyKw7c6wq9T4oqZKC5SKSkKIyBNYXhpbWl6ZSB0YWJsZSBbV2F2ZS9UYWJsZV0KVE1heCDihpAg4oavT19T4peM4o2l4p-cw7fiiaAwLuKGpeKIqS_ihqXin5zCry5SCiMgQ2VudGVyIHRhYmxlIChieSBwZWFrcykgW1dhdmUvVGFibGVdClRDdHAg4oaQIOKGr09fUy3DtzIrL-KGp-KfnC_ihqUuUgojIENlbnRlciB0YWJsZSAoYnkgbWVhbikgW1dhdmUvVGFibGVdClRDdG0g4oaQIOKGr09fUy3Dt1QvKy5SCgojIC0tLS0gV2F2ZWZvcm1zCiMgU2luZSB3YXZlIFtIYXJtb25pY10KU2luIOKGkCBWISjiiL_DlzpPVCkKIyBTYXcgd2F2ZSBbSGFybW9uaWNdClNhdyDihpAgViEoVVPil78xw5c6T1UpCiMgVHJpYW5nbGUgd2F2ZSBbSGFybW9uaWNdClRyaSDihpAgViEoVVPijLVVU-KXvzErMC4yNcKvw5c6T1UpCiMgUHVsc2Ugd2F2ZSBbSGFybW9uaWMsIER1dHldClB1bCDihpAgVlYhKFVT4omkVVM6U2F3KQojIFNxdWFyZSB3YXZlIFtIYXJtb25pY10KU3FyIOKGkCBWIShQdWw6MC41KQojIE5vaXNlIHdhdmUgW10KTnN3IOKGkCAo4piHMVVT4o2lKOKKguKagilTW10pCiMgU2lsZW50IChlbXB0eSkgd2F2ZQpTaWwg4oaQIFLigoHirJow4oavwqRTW10KCiMgLS0tLSBUYWJsZXMKIyBOb2lzZSB0YWJsZSBbXQpOc3Qg4oaQICjijaUo4oqCTnN3KU9bXSkKCiMgLS0tLSBTZXR0aW5ncwpEZXNrdG9wIOKGkCBGYWxzZQpJbkRpckF1ZGlvIOKGkCAiaW4iCkluRGlySW1hZ2Ug4oaQICJpbiIKT3V0RGlyQXVkaW8g4oaQICJvdXQiCk91dERpckltYWdlIOKGkCAib3V0IgpPdXREaXJHcmFwaCDihpAgIm91dCIKT3V0RGlyU3BlY3RydW0g4oaQICJvdXQiCk91dFN1ZmZpeEltYWdlIOKGkCAiIgpPdXRTdWZmaXhHcmFwaCDihpAgImciCk91dFN1ZmZpeFNwZWN0cnVtIOKGkCAicyIKCiMgLS0tLSBVdGlsaXR5CiMgUHJpbnQgc3RyaW5nIFtTdHJpbmddClByaW50IOKGkCAmcGYKIyBQbGF5IHdhdmUgb3IgdGFibGUgW1dhdmUvVGFibGVdCkV4cG9ydCDihpAgJmFwIEPima0KIyBQbGF5IHRlc3QgdG9uZSBvZiB3YXZlIG9yIHRhYmxlIFtXYXZlL1RhYmxlXQpUZXN0IOKGkCAmYXAgVTNSIEPima0KIyBEaXNwbGF5IGdyYXBoIFtXYXZlL1RhYmxlXQpHcmFwaCDihpAgSVMgQ0cKIyBEaXNwbGF5IGltYWdlIFtXYXZlL1RhYmxlXQpJbWFnZSDihpAgJmltc-KGr09fUyBTVSBSIEPima0KIyBSZWFkIGltYWdlIGZyb20gZmlsZSBbRmlsZW5hbWVdCkltcG9ydEltYWdlIOKGkCBSSSBP4omhUkkgU-KNnOKNieKJoSjDtzMvK-KGmTMp4peMwrBpbWcmZnJhYuKNpSjiioJEUyBJbkRpckltYWdlKURlc2t0b3AKIyBSZWFkIHdhdmUgZnJvbSBmaWxlIFtGaWxlbmFtZV0KSW1wb3J0V2F2ZSDihpAgUuKCgSBSSSBT4peM4peMwrBhdWRpbyZmcmFi4o2lKOKKgkRTIEluRGlyQXVkaW8pRGVza3RvcAojIFJlYWQgMjU2LXdhdmUgdGFibGUgZnJvbSBmaWxlIFtGaWxlbmFtZV0KSW1wb3J0VGFibGUg4oaQIOKGr09fUyBSSSBU4peM4peMwrBhdWRpbyZmcmFi4o2lKOKKgkRTIEluRGlyQXVkaW8pRGVza3RvcAojIFRhYmxlIHRvIHdhdmUgW1RhYmxlXQpUYWJsZUF2ZXJhZ2Ug4oaQIFLigoHDt08vK-KGr09fUwojIFRhYmxlIHRvIHdhdmUgW0luZGV4ICgwLTI1NSksIFRhYmxlXQpUYWJsZUluZGV4IOKGkCBS4oKB4oqh4oanLTFP4oalMOKBhTrihq9PX1M6CiMgQ29udmVydCBpbWFnZSB0byB0YWJsZSBbSW1hZ2VdCkltYWdlVG9UYWJsZSDihpAgVVMKIyBEaXNwbGF5IGdyYXBoIG9mIGltcHVsc2UgW1dhdmUvVGFibGVdCkdyYXBoSW1wdWxzZSDihpAgR3JhcGggQSEo4qyaMMOX4oavUzEpCiMgRGlzcGxheSBzcGVjdHJ1bSBncmFwaCBbV2F2ZS9UYWJsZV0KR3JhcGhTcGVjdHJ1bSDihpAgSVPijaLiiqIoPTHip7sp4omhKOKHjOKKnuKJpcO3U0jih6FTSOKKj1NWw7dGVuKGmcO3MlPijLVmZnQp4pa9PTDih6HijIrDt0dMIE8KIyBEaXNwbGF5IGZ1bGwgc3BlY3RydW0gZ3JhcGggW1dhdmUvVGFibGVdCkdyYXBoU3BlY3RydW1GdWxsIOKGkCBJU-KNouKKoig9MeKnuyniiaEo4oeM4oqe4omlw7dTSOKHoVNIw7dGVuKMtWZmdCnilr09MOKHoeKMisO3R0wgTwojIFNhdmUgd2F2ZSBvciB0YWJsZSB0byBmaWxlIFtGaWxlbmFtZSwgV2F2ZS9UYWJsZV0KV3JpdGVBdWRpbyDihpAgJmZ3YeKNpSjiioJEUyZmbWQuT3V0RGlyQXVkaW8pRGVza3RvcOKKguKKmSgiLndhdiJhdWRpbyJ3YXYiJmFzckPima0pCiMgU2F2ZSBpbWFnZSB0byBmaWxlIFtGaWxlbmFtZSwgV2F2ZS9UYWJsZV0KV3JpdGVJbWFnZSDihpAgKAogIOKKguKKmSjiioJPdXRTdWZmaXhJbWFnZSIucG5nImltZyJwbmci4oi1Wy4uOjFd4oavT19TIFNVIFIgQ-KZrSkKICAmZndh4o2lKOKKgkRTJmZtZC5PdXREaXJJbWFnZSlEZXNrdG9wKQojIFNhdmUgZ3JhcGggdG8gZmlsZSBbRmlsZW5hbWUsIFdhdmUvVGFibGVdCldyaXRlR3JhcGgg4oaQICgKICDijaUo4oqCRFMmZm1kLk91dERpckdyYXBoKURlc2t0b3DiioI6T3V0U3VmZml4R3JhcGjiipkoPjPip7vilrMu4oi1Wy4uOjFdQ0cpCiAgJmZ3YeKorCjiioI6Ii5wbmci4oqZKGltZyJwbmciKXziioI6Ii5naWYi4oqZKGdpZjE2KSk6CikKIyBTYXZlIHNwZWN0cnVtIGdyYXBoIHRvIGZpbGUgW0ZpbGVuYW1lLCBXYXZlL1RhYmxlXQpXcml0ZVNwZWN0cnVtIOKGkCAoCiAg4oqZKOKNouKKoig9MeKnuyniiaEo4oeM4oqe4omlw7dTSOKHoVNI4oqPU1bDt0ZW4oaZw7cyU-KMtWZmdCnilr09MOKHoeKMisO3R0wgTykKICDijaUo4oqCRFMmZm1kLk91dERpclNwZWN0cnVtKURlc2t0b3DiioI6T3V0U3VmZml4U3BlY3RydW3iipkoPjPip7vilrMu4oi1Wy4uOjFdKQogICZmd2HiqKwo4oqCOiIucG5nIuKKmShpbWcicG5nIil84oqCOiIuZ2lmIuKKmShnaWYxNikpOgopCiMgU2F2ZSBmdWxsIHNwZWN0cnVtIGdyYXBoIHRvIGZpbGUgW0ZpbGVuYW1lLCBXYXZlL1RhYmxlXQpXcml0ZVNwZWN0cnVtRnVsbCDihpAgKAogIOKKmSjijaLiiqIoPTHip7sp4omhKOKHjOKKnuKJpcO3U0jih6FTSMO3RlbijLVmZnQp4pa9PTDih6HijIrDt0dMIE8pCiAg4o2lKOKKgkRTJmZtZC5PdXREaXJTcGVjdHJ1bSlEZXNrdG9w4oqCOk91dFN1ZmZpeFNwZWN0cnVt4oqZKD4z4qe74pazLuKItVsuLjoxXSkKICAmZndh4qisKOKKgjoiLnBuZyLiipkoaW1nInBuZyIpfOKKgjoiLmdpZiLiipkoZ2lmMTYpKToKKQojIEdlbmVyYXRlcyB1bnVzZWQgbmFtZSBmb3Igb3V0cHV0Ck5ld05hbWUg4oaQICgKICDiqKwo4o2aJmZsZHsuLi4uLiIuLyJ9CiAgfCB7T3V0RGlyQXVkaW8gT3V0RGlySW1hZ2UuT3V0RGlyR3JhcGguT3V0RGlyU3BlY3RydW19CiAgICDijZoo4o2a4oaY4qe7OuKNpSjilr3iiLUmZmlmLik-MeKnuy4mZmxkLuKKgiIuLyJEUyZmbWQuKQogIClEZXNrdG9wCiAg4o2iKCsxCiAgfCB74oqCOiIud2F2IuKfnCjiioLiioI6T3V0U3VmZml4SW1hZ2U6Ii5wbmciKQogICAgIOKfnCjiioLiioI6T3V0U3VmZml4R3JhcGg6Ii5naWYiKeKfnCjiioLiioI6T3V0U3VmZml4R3JhcGg6Ii5wbmciKQogICAgIOKfnCjiioLiioI6T3V0U3VmZml4U3BlY3RydW06Ii5naWYiKeKfnCjiioLiioI6T3V0U3VmZml4U3BlY3RydW06Ii5wbmciKX1TVDUsLAogICAgL-KGpeKJoSjiiIjCsOKWoTopCiAgKSAwCiAgU1Q14oqZ4peMCikKIyBHZXRzIG5hbWUgb2YgcmFuZG9tIC53YXYgZmlsZSBbU3ViZm9sZGVyXQpSYW5kb21BdWRpb0ZpbGUg4oaQICgKICDiqKwoJmZsZCIuLyJ84o2a4oaY4qe7OuKWveKItSZmaWYuJmZsZC7iioIiLi8iRFMmZm1kLuKKgkluRGlyQXVkaW8pRGVza3RvcAogIOKWveKJoShDRSIud2F2IsKw4pahKS4KICDiqKwoIiLil4x8wrDilqHiio_ijIrDl-KaguKnuy4pPjDip7suCikKIyBHZXRzIG5hbWUgb2YgcmFuZG9tIC5wbmcgZmlsZSBbU3ViZm9sZGVyXQpSYW5kb21JbWFnZUZpbGUg4oaQICgKICDiqKwoJmZsZCIuLyJ84o2a4oaY4qe7OuKWveKItSZmaWYuJmZsZC7iioIiLi8iRFMmZm1kLuKKgkluRGlySW1hZ2UpRGVza3RvcAogIOKWveKJoShDRSIucG5nIsKw4pahKS4KICDiqKwoIiLil4x8wrDilqHiio_ijIrDl-KaguKnuy4pPjDip7suCikKIyBSZXBlYXQgZnVuY3Rpb24gW0Z1bmN0aW9uLCBOdW1dClJlcGVhdCEg4oaQIOKNpV4hCiMgQ2FsbCByYW5kb20gZnVuY3Rpb24gKDIgY2hvaWNlcykKRG9SYW5kb23igLwg4oaQIOKorF4hXiHijIrDlzLimoIKIyBDYWxsIHJhbmRvbSBmdW5jdGlvbiAoMyBjaG9pY2VzKQpEb1JhbmRvbeKAvCEg4oaQIOKorCheIXxeIXxeISnijIrDlzPimoIKIyBDYWxsIHJhbmRvbSBmdW5jdGlvbiAoNCBjaG9pY2VzKQpEb1JhbmRvbeKAvOKAvCDihpAg4qisKF4hfF4hfF4hfF4hKeKMisOXNOKaggojIEV4aXQgKHVzZSBhdCBlbmQgb2Ygc2NyaXB0KQpFeGl0IOKGkCDijaUoJmV4aXQwKURlc2t0b3AKCiMgLS0tLSBTYW1wbGUgd2F2ZWZvcm1zL3dhdmV0YWJsZXMKV2F2ZeKCgSDihpAgQXZne1NpbjMgQW1wMC43U2luMiBTaW4xfQpXYXZl4oKCIOKGkCBBTTAuNFNpbjcgQU0wLjVTaW4yIFNpbjEKV2F2ZeKCgiDihpAgQXZne1NhdzEgKFB1bCAyIDAuMil9CldhdmXigoQg4oaQIEJpdDAuNyBGTTAuMVNpbjcgU2luMQpUYmzigoEg4oaQIEZNKFZhbCAwIDEpIFNpbjIgU2luMQpUYmzigoIg4oaQIE1heCBBdmd7QW1wKFZhbCAwIDEpU2luMiBTaW4xfQpUYmzigoMg4oaQIEJpdChWYWwgMCAxKSBDcnMoVmFsUEkgMCAxIDIpIFNpbjEgU2luMwoKUHJpbnQgIlRibOKCgyIKVGVzdCBUYmzigoMKSW1hZ2UgVGJs4oKDCkdyYXBoIFRibOKCgwpHcmFwaFNwZWN0cnVtIFRibOKCgwoKRXhpdAo=

# TODO: IR importing
# TODO: fix Str (stretch) buzzing
# TODO: Sid (side by side)
# TODO: Shp (waveshape)
# TODO: Error handling

S ← 2048                                       # Samples per oscillator
O ← 256                                        # Oscillators per table
T ← ×S O                                       # Samples per table
SU ← ÷2+1                                      # Signed to unsigned
US ← -1×2                                      # Unsigned to signed
OU ← ÷⟜⇡S                                      # Oscillator base, unsigned 0-1
OT ← ×τOU                                      # Oscillator base, trigonometric 0-2π
R ← ↯¤T                                        # Oscillator to table (repeat)
U ← ⍥(⊢⍉↯⊟:⟜÷2⊸⧻)                              # Octave up (for playback only)
GH ← 200                                       # Graph height
C ← ↥¯1↧1                                      # Clamp
SW ← ⊏◿⧻,                                      # Select (wrapped)
SI ← +⊙×⟜-⊃⊃(SW⌊)(SW⌈)(-⊸⌊)                    # Select (interpolated)
RI ← SI×⇡:÷,⧻,                                 # Resize (interpolated)
CS ← ∿+η                                       # Cosine
NR ← ↧⊸↥                                       # NaN replace
SC ← NR1÷⟜∿                                    # Sinc
VB ← ÷-1⟜⇡O                                    # Value base
VR ← +⊙×:-⟜:                                   # Value range [min, max, values]
GL ← 16                                        # GIF length (frames)
SH ← 200                                       # Spectrum graph height
FV ← ÷2√S                                      # Fourier max value
SV ← +1⌊×÷2Sⁿ2÷⟜⇡÷4S                           # Spectrum index values
IS ← ⨬(&gifs15|&ims)=2⧻△.                      # Show image or gif
SM ← ⇌∧(⊂+⊸⊃⋅∘(×⊙⋅⊢))⊙(:¤°⊂)                   # Scan add + multiply
MB ← ÷:1ⁿ16 2                                  # Minimum 16-bit value
FT ← ⊃(∠°ℂ)(÷FV⌵)↙+1÷2S fft                    # Fourier transform [phase, amp ← wave]
FU ← ◌°ℂ°fft⊂:⍜°ℂ¯↘1⇌↘1.ℂ⊃(×∿)(×CS)⊙(×FV)      # Fourier untransform [wave ← phase, amp]
DS ← |1 ⍥(⊂:"/")>0⧻.                           # Add slash to directory name
CG ← ⍢⊢(=1⧻)≡(⇌⬚1⊞≠⇡GH⁅×-1GH SU U2C)▽=0⇡⌊÷GL O # Create graph
ST ← ⊂↯:@0↥0-:⊙(⧻.$"_")                        # Num to string w/ trailing zeroes
CE ← ≍⬚@ ↯⊙:¤⧻.⊙⇌⇌                             # Check extension [ext, name]
True ← 1                                       # True constant
False ← 0                                      # False constant
R₀ ← ☇0                                        # Rerank zero
R₁ ← ☇1                                        # Rerank one
V! ← R₁≡^!R₀
VV! ← R₁≡^!R₀⊙R₀
VVA! ← R₁≡^!R₀⊙R₀⊙⊙R₁
VA! ← R₁≡^!R₀⊙R₁
VAA! ← R₁≡^!R₀⊙R₁⊙⊙R₁
A! ← R₁≡^!R₁
AA! ← R₁≡^!R₁⊙R₁

# ---- Impulses
# Identity (delta) impulse
IId ← ¤¤1
# Phase shifted delta impulse [Phase (0-1)]
IPhs ← V!(↻:⬚0↯¤S IId⁅×S)
# Low-pass (sinc) impulse [Harmonic]
ILPS ← V!(÷/+.SC×-πOT)
# High-pass (sinc) impulse [Harmonic]
IHPS ← V!(-:IPhs0.5ILPS)
# Low-pass (windowed) impulse [Harmonic, Window]
ILPW ← VV!(
  >0,OT:SC×-πOT
  ⨬(+1×0|××⊃(>0|<τ)⟜(++0.42⊓(×¯0.5|×0.08)∩CS⟜(×2))+π×-π)
  ÷/+.×)
# High-pass (windowed) impulse [Harmonic, Window]
IHPW ← VV!(-:IPhs0.5ILPW)
# Low-pass (pulse) impulse [Harmonic]
ILPP ← V!(↻⁅÷2+S:⬚0↯¤S↯¤,÷:1.⁅↧S÷:S↥1×2)
# High-pass (pulse) impulse [Harmonic]
IHPP ← V!(-:IPhs0.5ILPP)
# Low-pass (exponential) impulse [Harmonic]
ILPE ← V!(÷/+.ⁿ:e¯×OU×ₙeⁿ3 10)
# High-pass (exponential) impulse [Harmonic]
IHPE ← V!(-:IPhs0ILPE)

# ---- Table values
# Values (linear) [Min, Max]
Val ← VR⊙⊙(VB)
# Values (power in) [Min, Max, Base]
ValPI ← VR⊙⊙(ⁿ:VB)
# Values (power out) [Min, Max, Base]
ValPO ← ⇌ValPI:

# ---- Helpers
# Random (min-max) [Min, Max]
Rnd ← +×⚂:⟜-
# Random (min-max) [Min, Max]
RndI ← ⌊Rnd:+1:⍥:<,,

# ---- Wave Operations
# Sum [{Waves/Tables}]
Sum ← °□/+⍚R₁
# Average [{Waves/Tables}]
Avg ← Sum÷⊸⧻
# Amplify [Amount, Wave/Table]
Amp ← VA!×
# Maximize [Wave/Table]
Max ← A!(◌⍥⟜÷≠0.↥∩/↥⟜¯.)
# Center (by peaks) [Wave/Table]
Ctp ← A!(-÷2+/↧⟜/↥.)
# Center (by mean) [Wave/Table]
Ctm ← A!(-÷S/+.)
# Phase shift [Amount, Wave/Table]
Phs ← VA!(↻⁅×⧻,)
# Phase shift to zero point [Wave/Table]
Zer ← A!(↻⊗/↧⌵..)
# Bitcrush [Amount (0-1), Wave/Table]
Bit ← VA!(US↧1÷-1:⌊×,SU:⁅ⁿ:2+1×15-:1√↧1↥0)
# Reverse [Wave/Table]
Rev ← A!⇌
# Invert [Wave/Table]
Inv ← A!¯
# Mirror 1st half [Wave/Table]
Mir ← A!(⊂⇌.↙÷2S)
# Fade start & end [Amount (0-1), Wave/Table]
Fad ← VA!(⇌⬚1×⇌:⬚1⟜×÷⧻.⇡⌊÷2×S↥0)
# Crossfade [Amount (0-1), Start Wave/Table, End Wave/Table]
Crs ← VAA!(+⊙(×-:1):⟜×⊙:)
# Stretch [To (0-1), From (0-1), Wave/Table]
Str ← VVA!(⊂⊙RI:-:S⟜RI⊙⊃↙↘∩(↧-1S↥1⁅×S↧1↥0))
# Shift up/down [Amount (¯1-1), Wave/Table]
Shf ← VA!(+)
# Slice [Amount (0-1), Wave/Table]
Slc ← VA!(RI S↯♭⁅↥1×S)
# Speed up/down [Octaves, Wave/Table]
Spd ← VA!(Slcⁿ:2)
# Splice [Amount (0-1), Wave/Table 1, Wave/Table 2]
Spl ← VAA!(⊂⊓↙↘⟜:⁅×S↧1↥0)
# Clip (hard) [Wave/Table]
Clp ← A!(C)
# Wrap [Wave/Table]
Wrp ← A!(US◿+MB1SU)
# Fold [Wave/Table]
Fld ← A!(-1⌵-2◿4-1)
# Clip (soft) [Exponent (≥2), Wave/Table]
Cls ← VA!(⍜⌵(÷-1⊙-⟜⊃ⁿ×:)C:↥2)
# Saturate (tanh) [Gain, Wave/Table]
Sat ← VA!(÷⊃+-1ⁿ:e×2×)
# Fuzz (tanh) [Gain, Shift (¯1-1), Wave/Table]
Fuz ← Sat⊙Shf
# Rectify [Wave/Table]
Rct ← A!(⌵)
# Amplutude modulation [Amplitude, Modulator, Carrier]
AM ← VAA!(×+1÷2×-1:)
# Ring modulation [Modulator, Carrier]
RM ← AA!×
# Frequency modulation [Amplitude, Modulator, Carrier]
FM ← VAA!(SI×S+OU×)
# Convolve [Impulse, Wave/Table]
Cnv ← AA!(◌°ℂ°fft×∩fft×√S⬚0↯¤S)
# Phase randomize [Amount (0-1), Wave/Table]
Phr ← ≡(FU:⊙+:FT):≡×R₁-π×τ⍥(⊂⚂)+1÷2S[]R₀⊙R₁
# Harmonic wave filter [Amount (0-1), Filter Wave/Table, Source Wave/Table]
Hwf ← VAA!(FU⊙×:+1×⊙(-1◌∩FT))
# Downsample [Amount (0-1), Wave/Table]
Dwn ← VA!(
  ⌈÷:S.⁅↥1ⁿ:2+1×-1ₙ2S-:1
  ↯¤S⍉↯⟜÷⟜(≡/+↯⊂:)
)
# Low-pass (sinc) [Harmonic, Wave/Table]
LPS ← Phs0.5Cnv ILPS
# High-pass (sinc) [Harmonic, Wave/Table]
HPS ← Phs0.5Cnv IHPS
# Low-pass (windowed) [Harmonic, Window, Wave/Table]
LPW ← Phs0.5Cnv ILPW
# High-pass (windowed) [Harmonic, Window, Wave/Table]
HPW ← Phs0.5Cnv IHPW
# Low-pass (pulse) [Harmonic, Wave/Table]
LPP ← Phs0.5Cnv ILPP
# High-pass (pulse) [Harmonic, Wave/Table]
HPP ← Phs0.5Cnv IHPP
# Low-pass (exponential) [Harmonic, Wave/Table]
LPE ← Cnv ILPE
# High-pass (exponential) [Harmonic, Wave/Table]
HPE ← Cnv IHPE
# Low-pass (default) [Harmonic, Wave/Table]
LP ← LPW.
# Low-pass (default) [Harmonic, Wave/Table]
HP ← HPW.

# ---- Table operations
# Comb filter (feedback) [Amplitude, Harmonic, Wave/Table]
TCmb ← ↯O_S SM⊙(⬚0↯⊂⌈÷:T.⁅÷:S⊙R)
# Comb filter (feedforward) [Amplitude, Harmonic, Wave/Table]
TCmf ← ↯O_S+×⊙(⊂↯:[0]¤⟜↘⌊÷:¯S⊙(.R))
# Maximize table [Wave/Table]
TMax ← ↯O_S◌⍥⟜÷≠0.↥∩/↥⟜¯.R
# Center table (by peaks) [Wave/Table]
TCtp ← ↯O_S-÷2+/↧⟜/↥.R
# Center table (by mean) [Wave/Table]
TCtm ← ↯O_S-÷T/+.R

# ---- Waveforms
# Sine wave [Harmonic]
Sin ← V!(∿×:OT)
# Saw wave [Harmonic]
Saw ← V!(US◿1×:OU)
# Triangle wave [Harmonic]
Tri ← V!(US⌵US◿1+0.25¯×:OU)
# Pulse wave [Harmonic, Duty]
Pul ← VV!(US≤US:Saw)
# Square wave [Harmonic]
Sqr ← V!(Pul:0.5)
# Noise wave []
Nsw ← (☇1US⍥(⊂⚂)S[])
# Silent (empty) wave
Sil ← R₁⬚0↯¤S[]

# ---- Tables
# Noise table []
Nst ← (⍥(⊂Nsw)O[])

# ---- Settings
Desktop ← False
InDirAudio ← "in"
InDirImage ← "in"
OutDirAudio ← "out"
OutDirImage ← "out"
OutDirGraph ← "out"
OutDirSpectrum ← "out"
OutSuffixImage ← ""
OutSuffixGraph ← "g"
OutSuffixSpectrum ← "s"

# ---- Utility
# Print string [String]
Print ← &pf
# Play wave or table [Wave/Table]
Export ← &ap C♭
# Play test tone of wave or table [Wave/Table]
Test ← &ap U3R C♭
# Display graph [Wave/Table]
Graph ← IS CG
# Display image [Wave/Table]
Image ← &ims↯O_S SU R C♭
# Read image from file [Filename]
ImportImage ← RI O≡RI S⍜⍉≡(÷3/+↙3)◌°img&frab⍥(⊂DS InDirImage)Desktop
# Read wave from file [Filename]
ImportWave ← R₁ RI S◌◌°audio&frab⍥(⊂DS InDirAudio)Desktop
# Read 256-wave table from file [Filename]
ImportTable ← ↯O_S RI T◌◌°audio&frab⍥(⊂DS InDirAudio)Desktop
# Table to wave [Table]
TableAverage ← R₁÷O/+↯O_S
# Table to wave [Index (0-255), Table]
TableIndex ← R₁⊡↧-1O↥0⁅:↯O_S:
# Convert image to table [Image]
ImageToTable ← US
# Display graph of impulse [Wave/Table]
GraphImpulse ← Graph A!(⬚0×↯S1)
# Display spectrum graph [Wave/Table]
GraphSpectrum ← IS⍢⊢(=1⧻)≡(⇌⊞≥÷SH⇡SH⊏SV÷FV↙÷2S⌵fft)▽=0⇡⌊÷GL O
# Display full spectrum graph [Wave/Table]
GraphSpectrumFull ← IS⍢⊢(=1⧻)≡(⇌⊞≥÷SH⇡SH÷FV⌵fft)▽=0⇡⌊÷GL O
# Save wave or table to file [Filename, Wave/Table]
WriteAudio ← &fwa⍥(⊂DS&fmd.OutDirAudio)Desktop⊂⊙(".wav"audio"wav"&asrC♭)
# Save image to file [Filename, Wave/Table]
WriteImage ← (
  ⊂⊙(⊂OutSuffixImage".png"img"png"∵[..:1]↯O_S SU R C♭)
  &fwa⍥(⊂DS&fmd.OutDirImage)Desktop)
# Save graph to file [Filename, Wave/Table]
WriteGraph ← (
  ⍥(⊂DS&fmd.OutDirGraph)Desktop⊂:OutSuffixGraph⊙(>3⧻△.∵[..:1]CG)
  &fwa⨬(⊂:".png"⊙(img"png")|⊂:".gif"⊙(gif16)):
)
# Save spectrum graph to file [Filename, Wave/Table]
WriteSpectrum ← (
  ⊙(⍢⊢(=1⧻)≡(⇌⊞≥÷SH⇡SH⊏SV÷FV↙÷2S⌵fft)▽=0⇡⌊÷GL O)
  ⍥(⊂DS&fmd.OutDirSpectrum)Desktop⊂:OutSuffixSpectrum⊙(>3⧻△.∵[..:1])
  &fwa⨬(⊂:".png"⊙(img"png")|⊂:".gif"⊙(gif16)):
)
# Save full spectrum graph to file [Filename, Wave/Table]
WriteSpectrumFull ← (
  ⊙(⍢⊢(=1⧻)≡(⇌⊞≥÷SH⇡SH÷FV⌵fft)▽=0⇡⌊÷GL O)
  ⍥(⊂DS&fmd.OutDirSpectrum)Desktop⊂:OutSuffixSpectrum⊙(>3⧻△.∵[..:1])
  &fwa⨬(⊂:".png"⊙(img"png")|⊂:".gif"⊙(gif16)):
)
# Generates unused name for output
NewName ← (
  ⨬(⍚&fld{....."./"}
  | {OutDirAudio OutDirImage.OutDirGraph.OutDirSpectrum}
    ⍚(⍚↘⧻:⍥(▽∵&fif.)>1⧻.&fld.⊂"./"DS&fmd.)
  )Desktop
  ⍢(+1
  | {⊂:".wav"⟜(⊂⊂:OutSuffixImage:".png")
     ⟜(⊂⊂:OutSuffixGraph:".gif")⟜(⊂⊂:OutSuffixGraph:".png")
     ⟜(⊂⊂:OutSuffixSpectrum:".gif")⟜(⊂⊂:OutSuffixSpectrum:".png")}ST5,,
    /↥≡(∈°□:)
  ) 0
  ST5⊙◌
)
# Gets name of random .wav file [Subfolder]
RandomAudioFile ← (
  ⨬(&fld"./"|⍚↘⧻:▽∵&fif.&fld.⊂"./"DS&fmd.⊂InDirAudio)Desktop
  ▽≡(CE".wav"°□).
  ⨬(""◌|°□⊏⌊×⚂⧻.)>0⧻.
)
# Gets name of random .png file [Subfolder]
RandomImageFile ← (
  ⨬(&fld"./"|⍚↘⧻:▽∵&fif.&fld.⊂"./"DS&fmd.⊂InDirImage)Desktop
  ▽≡(CE".png"°□).
  ⨬(""◌|°□⊏⌊×⚂⧻.)>0⧻.
)
# Repeat function [Function, Num]
Repeat! ← ⍥^!
# Call random function (2 choices)
DoRandom‼ ← ⨬^!^!⌊×2⚂
# Call random function (3 choices)
DoRandom‼! ← ⨬(^!|^!|^!)⌊×3⚂
# Call random function (4 choices)
DoRandom‼‼ ← ⨬(^!|^!|^!|^!)⌊×4⚂
# Exit (use at end of script)
Exit ← ⍥(&exit0)Desktop

# ---- Sample waveforms/wavetables
Wave₁ ← Avg{Sin3 Amp0.7Sin2 Sin1}
Wave₂ ← AM0.4Sin7 AM0.5Sin2 Sin1
Wave₂ ← Avg{Saw1 (Pul 2 0.2)}
Wave₄ ← Bit0.7 FM0.1Sin7 Sin1
Tbl₁ ← FM(Val 0 1) Sin2 Sin1
Tbl₂ ← Max Avg{Amp(Val 0 1)Sin2 Sin1}
Tbl₃ ← Bit(Val 0 1) Crs(ValPI 0 1 2) Sin1 Sin3

Print "Tbl₃"
Test Tbl₃
Image Tbl₃
Graph Tbl₃
GraphSpectrum Tbl₃

Exit
